///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////
scoreboardtable = [
	uigrid 2 $.UI_pad_3XL 0 [
		uialign 0 -1
		loopscoreboard cn 0 [
			local ai ; ai = (isai $cn)
			uicolor $c_diag2 0 $.UI_pad_5XL [
				pushif highlight (scoreboardhighlight $cn) [
					uimodvgradient 0xFFFFFF $highlight
					uiclamp-e
				]
				uispace $.UI_pad_S 0 [
					uialign -1
					uihlist $.UI_pad_L [
						if (= $getfollow $cn) [
							UItriangle 0 0 $.UI_pad_L 0 270
						] [ uifill $.UI_pad_L $.UI_pad_L ]
						uifill $.UI_pad_DXS 0 [
							uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
							uialign- -1
						]
						if $scoreboardshowfrags [
							if $getmode [
								uifill $.UI_pad_6XL 0 [
									uitext (getclientfrags $cn) 0.6
									uialign- 1
								]
							]
						]
						if $scoreboardmultiplayer [
							if $showpj [
								uifill $.UI_pad_6XL 0 [
									if (|| $ai [= $cn $getclientnum]) [uitext "^f4-" 0.6] [
										uitext (scoreboardpj $cn) 0.6
										uialign- 1
									]
								]
							]
							if $showping [
								uifill $.UI_pad_6XL 0 [
									if $ai [uitext "^f4-" 0.6] [
										uitext (scoreboardping $cn) 0.6
										uialign- 1
									]
								]
							]
						]
						if (|| $scoreboardshowclientnum $showclientnum) [
							uifill $.UI_pad_6XL 0 [
								if $ai [uitext "^f4BOT" 0.6] [
									uitext $cn 0.6
									uialign- 1
								]
							]
						]
					]
				]
				uioutline $c_line1
				uiclamp-e
			]
		]
	]
]


_c_spec1  = 0xC0303840 // spec team background
_c_spec2  = 0x607080   // spec team outline

_c_azul1  = 0xC0203070 // team 1 background
_c_azul2  = 0x4060D0   // team 1 outline

_c_rojo1  = 0xC0702020 // team 2 background
_c_rojo2  = 0xD04040   // team 2 outline


scoreboardtableteam = [
	if (! (numscoreboard $arg1)) [ uifill $sbw ] [
		uivlist 0 [
			uialign 0 -1
			loopscoreboard cn $arg1 [
				local ai ; ai = (isai $cn)
				uicolor $_c_@(case $arg1 1 [result azul1] 2 [result rojo1]) 0 $.UI_pad_5XL [
					pushif highlight (scoreboardhighlight $cn) [
						uimodvgradient 0xFFFFFF $highlight
						uiclamp-e
					]
					uispace $.UI_pad_S 0 [
						uialign -1
						uihlist $.UI_pad_L [
							if (= $getfollow $cn) [
								UItriangle 0 0 $.UI_pad_L 0 270
							] [ uifill $.UI_pad_L $.UI_pad_L ]
							uifill $.UI_pad_DXS 0 [
								uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
								uialign- -1
							]
							//if (scoreboardshowflags) [
							//    uifill $.UI_pad_DXS 0 [
							//        pushif i (getclientfrags $cn) [
							//            uitext $i 0.6
							//            uialign- -1
							//        ]
							//    ]
							//]
							if $scoreboardshowfrags [
								if $getmode [
									uifill $.UI_pad_6XL 0 [
										uitext (getclientfrags $cn) 0.6
										uialign- 1
									]
								]
							]
							if $scoreboardmultiplayer [
								if $showpj [
									uifill $.UI_pad_6XL 0 [
										if (|| $ai [= $cn $getclientnum]) [uitext "^f4-" 0.6] [
											uitext (scoreboardpj $cn) 0.6
											uialign- 1
										]
									]
								]
								if $showping [
									uifill $.UI_pad_6XL 0 [
										if $ai [uitext "^f4-" 0.6] [
											uitext (scoreboardping $cn) 0.6
											uialign- 1
										]
									]
								]
							]
							if (|| $scoreboardshowclientnum $showclientnum) [
								uifill $.UI_pad_6XL 0 [
									if $ai [uitext "^f4BOT" 0.6] [
										uitext $cn 0.6
										uialign- 1
									]
								]
							]
						]
					]
					uioutline $_c_@(case $arg1 1 [result azul2] 2 [result rojo2])
					uiclamp-e
				]
			]
		]
	]
]

scoreboardtablespec = [
	uigrid $arg1 0 0 [
		uialign -1 -1
		loopscoreboard cn -1 [
			uicolor $_c_spec1 0 $.UI_pad_5XL [
				pushif highlight (scoreboardhighlight $cn) [
					uimodvgradient 0xFFFFFF $highlight
					uiclamp-e
				]
				uispace $.UI_pad_S 0 [
					uialign -1
					uihlist 0.01 [
						uifill (+f $.UI_pad_D5XL $sbnw) 0 [
							uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.6
							uialign- -1
						]
						if (|| $scoreboardshowclientnum $showclientnum) [
							uifill $.UI_pad_6XL 0 [
								uitext $cn 0.6
								uialign- 1
							]
						]
					]
				]
				uioutline $_c_spec2
				uiclamp-e
			]
		]
	]
]

scoreboardheader0 = [
	uihlist 0 [
		uiclamp.e
		uihgradient $c_line1 $c_menu 0 0 [
			uispace $.UI_pad_3XL 0 [
				uialign -1
				uifill $.UI_pad_DM 0 [
					uiclip $.UI_pad_DM 0 [
						uialign -1
						uivlist 0 [
							uitext (concatword "^f2" $scoreboardmap) 0.6
							if (!= $getmode 0) [
								uitext (concatword (? (|| $intermission $paused) "^f3" "^f8") $scoreboardtime) 0.6
							]
							uialign* -1
						]
					]
					UIbar 0 1
					uialign- 1
				]
			]
		]
		uihgradient $c_menu $c_line1 0 0 [
			uispace $.UI_pad_3XL 0 [
				uialign 1
				uitext (getmodeshortname $getmode)
			]
		]
		uiclamp*e
	]
	uitext $getfrags 1.5
]

scoreboardheader1 = [
	uihlist 0 [
		uiclamp.e
		uihgradient $_c_azul1 $c_menu 0 0 [
			uicolortext (getteamname 1) 0x38FFFFFF 1.6
			uialign- 0
			//if (!= $gamespeed 100) [uitext $prettygamespeed]
			uispace $.UI_pad_3XL 0 [
				uialign -1
				uifill 0 0 [
					uiclip 0.18 0 [
						uialign -1
						uivlist 0 [
							uitext (concatword "^f2" $scoreboardmap) 0.6
							uitext (concatword (? (|| $intermission $paused) "^f3" "^f8") $scoreboardtime) 0.6
							uialign* -1
						]
					]
					UIbar 0 1
					uialign- 1
				]
			]
			uispace $.UI_pad_L 0 [
				uialign 1
				uicolortext (getteamscore 1) $_c_azul2 1.5
			]
		]
		uihgradient $c_menu $_c_rojo1 0 0 [
			uicolortext (getteamname 2) 0x38FFFFFF 1.6
			uialign- 0
			uispace $.UI_pad_L 0 [
				uialign -1
				uicolortext (getteamscore 2) $_c_rojo2 1.5
			]
			uispace $.UI_pad_3XL 0 [
				uialign 1
				uitext (getmodeshortname $getmode)
			]
		]
		uiclamp*e
	]
	UIbar 0 1
]

getmodeshortname = [
	at [
		"Coop-Edit"
		"rDM"
		"pDM"
		"rTDM"
		"pTDM"
		"rCTF"
		"pCTF"
	] (min 6 (max 0 $arg1))
]


// 0.012    BOX PADDING
// 0.02     SPECTATOR ARROW
// 0.30     CLIENT NAME
// 0.04     FLAGS SCORED
// 0.05     FRAGS
// 0.05     PJ
// 0.05     PING
// 0.05     CN

newui "scoreboard" [
	uieschide 0
	uiallowinput 0
	uialign 0 -1
	refreshscoreboard
	local m ; m = (m_teammode (getmode))
	// name box width
	sbnw = (+f (? (&& $getmode $scoreboardshowfrags) $.UI_pad_DSS) (
		if $scoreboardmultiplayer [+f (? $showpj $.UI_pad_DSS) (? $showping $.UI_pad_DSS)]
	))
	// full box width
	sbw = (+f $.UI_pad_XL $.UI_pad_3XL $.UI_pad_DXS $sbnw (
		? (|| $scoreboardshowclientnum $showclientnum) $.UI_pad_DSS
	))
	uifont "default_outline" [
		uivlist $.UI_pad_3XL [
			uialign 0 -1
			uivlist $.UI_pad_L [
				uifill 0 $.UI_pad_6XL
				uiclip 1 0 [
					uitext (scoreboardservinfo) 0.85
				]
				uifont "default" [
					uicolor $c_menu (+f $.UI_pad_3XL (*f $sbw 2)) 0.07 [
						scoreboardheader@m
						uioutline $c_line1
						uiclamp-e
					]
				]
			]
			if $m [
				uihlist $.UI_pad_3XL [
					scoreboardtableteam 1
					scoreboardtableteam 2
				]
				scoreboardtablespec 2
			] [
				scoreboardtable
				scoreboardtablespec 2
			]
		]
	]
]