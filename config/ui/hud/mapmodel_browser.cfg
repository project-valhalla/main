
uiLiteMenu "mapmodel_browser" [
	uialign 1
	if (> $.mm_totalpg 1) [
		uiscrolldown [ UIscrollsound .mm_curpage (? (iskeyheld "LCTRL")  999  1) (- $.mm_totalpg 1) ]
		uiscrollup   [ UIscrollsound .mm_curpage (? (iskeyheld "LCTRL") -999 -1) 0 ]
	]
] [
	uiHudCell 0 0 [
		uivlist $uiPad:L [
			uihlist 0 [
				uiButton "hold2 $c_diag2" [
					uiCheckbox [= $.mm_doaction 1] 0 0 0 [
						if $arg1 [ uiTriangle $c_line1 0 $uiPad:O5 0 180 ]
					]
					uitext "Create" 0.6
				] $.z 0 [ .mm_doaction = 1 ] -1
				uigroup [
					uifill $.z $uiPad:6XL
					uihlist $uiPad:O3- [
						uiclamp.y
						uifill $uiPad:DXS 0 [
							uifonttext "mono" (+ $.mm_curpage 1) 0.7
							uialign- 1 -1
						]
						uioffset 0 $uiPad:L- [
							uiclip 0 $uiPad:6XL [ uicolortext "/" $c_gray 1.2 ]
						] // text takes up a lot of vertical space, have to "cut" it
						uifill $uiPad:DXS 0 [
							uifonttext "mono" (max $.mm_totalpg 1) 0.7
							uialign- -1 1
						]
						uiclamp*y
					]
				]
				uiButton "hold2 $c_diag2" [
					uitext "Replace" 0.6
					uiCheckbox [= $.mm_doaction 0] 0 0 0 [
						if $arg1 [ uiTriangle $c_line1 0 $uiPad:O5 0 180 ]
					]
				] $.z 0 [ .mm_doaction = 0 ] 1
				uiclamp*y
			]
			uigroup [
				uifill $.x $.y
				uihover [] [ uihold [] [ .mm_selidx = -1 ] ]
				uigrid 3 0 0 [
					uialign -1 -1
					loopwhile+ i (* $.mm_curpage 15) 15 [< $i $nummapmodels] [ // XXX this condition doesn't work as intended, needs redesign
						n = (mod $i 15) // page normalized index
						uitarget $.z $.z [
							if (= $n $.mm_selidx) [
								uimodcolor $c_grdmod
								uimodelpreview (mapmodelname $i 1) "mapmodel"
								uiclamp*e
								uispace $uiPad:S $uiPad:O3 [
									uifonttext "def.ol" $i 0.55
								] ; uialign- -1 -1
								if (iskeyheld "LSHIFT") [
									uispace $uiPad:S $uiPad:O3 [
										uifonttext "webdings.ol" "q" 0.65
									] ; uialign- 1 -1
									uirelease [ clearmodel (mapmodelname $i) ]
								] [
									uirelease [
										if $.mm_doaction [ newent mapmodel $i ] [
											if $enthavesel [
												entloop [ if (=s $enttype "mapmodel") [
													entset "mapmodel" $i (entattr 1) (entattr 2) (entattr 3) (entattr 4)
												] ]
											] [ newent mapmodel $i ]
										]
									]
								]
							] [
								uimodcolor $c_boxmod
								uimodelpreview (mapmodelname $i 1) "mapmodel"
								uiclamp*e
								uispace $uiPad:S $uiPad:O3 [
									uifontcolortext "def.ol" $i (|A! 0xA0) 0.55
								] ; uialign- -1 -1
								uihover [ .mm_selidx = $n ; uiSetMillis ]
								uioutline $c_line1 $.z $.z
							]
							if (mapmodelloaded $i) [
								uispace $uiPad:S $uiPad:O3 [
									uifonttext "webdings.ol" "a" 0.55
								] ; uialign- 1 -1
							]
							uiHoverOnce [ uiHoverSound ]
						]
					]
				]
				if (> $.mm_selidx -1) [ // drawing on top to avoid overlap from other cells
					uioffset (*f $.z (mod $.mm_selidx 3)) (*f $.z (/ $.mm_selidx 3)) [
						uioutline (uiCosWave:INT $c_green $c_line1 500) $.z $.z
					] ; uialign- -1 -1
				]
			]
			uihlist 0 [
				uifont "webdings" [
					uifill $.z
					if (> $.mm_curpage 0) [
						uiButtonArea "hold2 $c_diag2" 0 0 [
							uiclamp.e
							uitext "7" 0.9
							uirelease [ -- .mm_curpage ]
						]
					] [ uicolortext "7" $c_gray 0.9 ]
				]
				uifill $.z 0 [ uiFastImg "" "ui/" "io/" "mouseM" $uiPad:6XL ]
				uifont "webdings" [
					uifill $.z
					if (< (+ $.mm_curpage 1) $.mm_totalpg) [
						uiButtonArea "hold2 $c_diag2" 0 0 [
							uiclamp.e
							uitext "8" 0.9
							uirelease [ ++ .mm_curpage ]
						]
					] [ uicolortext "8" $c_gray 0.9 ]
				]
				uiclamp*y
			]
		]
	]
] [
	.y = (-f 1 $uiPad:DM $uiPad:DS)
	.z = (/f $.y 5)
	.x = (*f $.z 3)
	.mm_selidx   = -1
	.mm_totalpg  = (ceil (/f $nummapmodels 15))
	.mm_curpage  = 0
	hideui "texture_browser"
	hideui "map_browser"
	hideui "geo_prefab"
	hideui "editvars" 
] [ if $editing [ showui "editvars" ] ]
