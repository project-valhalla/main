
cosmetics = 5
.cosmeticItems = [
	[ "cosmetic/skull"  "Skull"      ]
	[ "cosmetic/cowboy" "Cowboy hat" ]
	[ "cosmetic/helmet" "Helmet"     ]
	[ "cosmetic/wizard" "Wizard hat" ]
	[ "cosmetic/wings"  "Wings"      ]
]

uiLiteMenu "profile_cosmetics" [
	uialign 1 0
] [
	uispace $uiPad:D4XL $uiPad:D4XL [
		uiHudCell 0 0 [
			uivlist $uiPad:L [
				uigroup [
					uifill $.x $.y
					uihover [] [ uihold [] [ .mm_selidx = -1 ] ]
					uigrid 3 0 0 [
						uialign -1 -1
						loop i $cosmetics [
							uitarget $.z $.z [
								if (= $i $.mm_selidx) [
									uimodcolor $c_grdmod
									uimodelpreview (at $.cosmeticItems $i 0)
									uiclamp*e
									uispace $uiPad:S $uiPad:O3 [
										uifonttext "def.ol" (at $.cosmeticItems $i 1) 0.55
									] ; uialign- -1 -1
									if (iskeyheld "LSHIFT") [
										uispace $uiPad:S 0 [
											uifonttext "modernpics.ol" "R" 0.65
										] ; uialign- -1 1
										uirelease [ clearmodel (+s "mapmodel/" (mapmodelname $i)) ]
									] [
										uirelease [
											playercosmetic $i
										]
									]
								] [
									uimodcolor $c_boxmod
									uimodelpreview (at $.cosmeticItems $i 0)
									uiclamp*e
									uispace $uiPad:S $uiPad:O3 [
										uifontcolortext "def.ol" (at $.cosmeticItems $i 1) (|A! 0xA0) 0.55
									] ; uialign- -1 -1
									uihover [ .mm_selidx = $i ; uiSetMillis ]
									uioutline $c_line1 $.z $.z
								]
								uiHoverOnce [ uiHoverSound 3 ]
							]
						]
					]
					if (> $.mm_selidx -1) [ // drawing on top to avoid overlap from other cells
						uioffset (*f $.z (mod $.mm_selidx 3)) (*f $.z (/ $.mm_selidx 3)) [
							uioutline (uiCosWave:INT $c_green $c_line1 500) $.z $.z
						] ; uialign- -1 -1
					]
				]
				uihlist 0 [
					uifont "modernpics" [
						uifill $.z
						if (> $.mm_curpage 0) [
							uiButtonArea "hold2 $c_diag2" 0 0 [
								uiclamp.e
								uitext "<" 0.9
								uirelease [ -- .mm_curpage ]
							]
						] [ uicolortext "<" $c_gray 0.9 ]
					]
					uifill $.z 0 [ uiFastImg "" "ui/" "io/" "mouseM" $uiPad:6XL ]
					uifont "modernpics" [
						uifill $.z
						if (< (+ $.mm_curpage 1) $.mm_totalpg) [
							uiButtonArea "hold2 $c_diag2" 0 0 [
								uiclamp.e
								uitext ">" 0.9
								uirelease [ ++ .mm_curpage ]
							]
						] [ uicolortext ">" $c_gray 0.9 ]
					]
					uiclamp*y
				]
			]
		]
	]
]

uiLiteMenu "profile_models" [
	uialign 0 1
] [
	uispace $uiPad:D4XL $uiPad:D4XL [
		uiHudCell 0 0 [
			 uiOptionSld playermodel [
				0 "ASK"   $c_white
				1 "EMBLA" $c_white
			] [ ] 1 0.5
		]
	]
]

uiLiteMenu "profile_colors" [
	uialign 1 0
] [
	uispace $uiPad:D4XL 0 [
		uiHudCell 0 0 [
			uivlist 0 [
				uiclamp.y
				looplist2 idx var [
					0 "playercolor"
					1 "playercolorblue"
					2 "playercolorred"
				] [
					uivlist $uiPad:L [
						uitext (at "Solo ^f8Aesir ^f3Vanir" $idx) 0.7
						uigrid (? $idx 2 3) $uiPad:S $uiPad:S [
							uihover [ .ps_team = $idx ]
							loop n (+ (getvarmax $var) 1) [
								uicolor (getplayercolor $n $idx) $uiPad:SXL $uiPad:5XL [
									uioutline (if (= $$var $n) [ result $c_baige ] [ getplayercolor $n $idx ])
									uiclamp-e
									uipress [ $var $n ]
								]
							]
						]
					]
				]
			]
		]
	]
]

uiLiteMenu "profile_flags" [
	uialign 1 0
] [
	uispace $uiPad:D2XL 0 [
		uiHudCell 0 0 [
			uivlist 0 [
				uitext "Choose a flag to display next to your name" 0.5
				uiScrollBox "customflags" $uiPad:USS $uiPad:UXS [ uivlist 0 [
					uialign -1 -1
					.customflag_entry "" "None" [
						uiFastImg "" "ui/" "none" "" $uiPad:6XL $uiPad:6XL
					]
					.customflag_entry "geo" "Your country" [
						uiFastImg "" "countryflag/" "geo" "" $uiPad:6XL $uiPad:6XL
					]
					looplist e $.customflags [
						.customflag_entry (at $e 0) (at $e 1) [
							uiFastImg "" "countryflag/" (at $e 0) "" $uiPad:SXL $uiPad:5XL
						]
					]
				]
			]
		] ]
	]
]

uiLiteMenu "profile_bar" [
	uialign -1 0
] [
	uispace $uiPad:D4XL 0 [
		uivlist $uiPad:S [
			uiHudCell 0 0 [
				uivlist $uiPad:XL [
					uiFastImgStretched [<fade:0.5>] "ui/" "ui_bar0" "" [
						uiFastImg (? $uihover? [<mad:1,1,1>] [<mad:0.6,0.6,0.6>]) "ui/" "model" "" $uiPad:SXL
						uipress [
							showui "profile_models"
						]
					] $uiPad:DM
					uiFastImgStretched [<fade:0.5>] "ui/" "ui_bar0" "" [
						uiFastImg (? $uihover? [<mad:1,1,1>] [<mad:0.6,0.6,0.6>]) "ui/" "paint" "" $uiPad:SXL
						uipress [
							showui "profile_colors"
						]
					] $uiPad:DM
					uiFastImgStretched [<fade:0.5>] "ui/" "ui_bar0" "" [
						uiFastImg (? $uihover? [<mad:1,1,1>] [<mad:0.6,0.6,0.6>]) "ui/" "hat" "" $uiPad:SXL
						uipress [
							showui "profile_cosmetics"
						]
					] $uiPad:DM
					uiFastImgStretched [<fade:0.5>] "ui/" "ui_bar0" "" [
						uiFastImg (? $uihover? [<mad:1,1,1>] [<mad:0.6,0.6,0.6>]) "ui/" "flag" "" $uiPad:SXL
						uipress [
							showui "profile_flags"
						]
					] $uiPad:DM
				]
			]
			uiHudCell 0 0 [
				uispace $uiPad:S $uiPad:S [
					uihlist $uiPad:M [
						uiFastImg <mad:0.6,0.6,0.6> "ui/" "rotate" "" $uiPad:SXL
						uifonttext "default.ol" "Click and drag to rotate" 0.5
					]
				]
			]
		]
	]
]

uiLiteMenu "profile" [
	uialign 0 -1
] [
	uiHudCell 0 0 [
		uihlist $uiPad:M [
			local mad
			mad = ""
			loop i 7 [
				uigroup [
					if $uihover? [
						mad = "<mad:0.25,0.25,0.25>"
					] [
						mad = ""
					]
					uipress [
						setweapon (at $.weaponList $i)
					]
					uiFastImgStretched (+s [<fade:0.9>] $mad) "ui/" "ui_bar0" "" [
						uispace $uiPad:L $uiPad:L [
							uiFastImg [<rotate:4>] "hud/" (at $.weaponList $i) "_raw" (*f (gettexaspectwh (+s "data/interface/hud/" (at $.weaponList $i) "_raw.png")) $uiPad:4XL) $uiPad:4XL
						]
					] $uiPad:DM $uiPad:DXS
				]
			]
		]
	]
] [
	.name = $name
	.ps_team = $getteam
	sleep 100 [
		cameraentityid 1
	]
	showui "profile_bar"
	hideui "main_"
	respawn
] [
	cameraentityid 0
	hideui "profile_bar"
	showui "main_"
]

.customflags = [
	[ _bl    "Black"          ]
	[ _bh    "Brotherhood"    ]
	[ _ce    "Celestia"       ]
	[ _ch    "Chequered"      ]
	[ _ea    "Earth"          ]
	[ _ec    "Ecology"        ]
	[ _eu    "European Union" ]
	[ _jr    "Jolly Roger"    ]
	[ _ol    "Olympics"       ]
	[ _pe    "Peace"          ]
	[ _pr    "Pride"          ]
	[ _rd    "Red"            ]
	[ _rb    "Red & Black"    ]
	[ _re    "Roman Empire"   ]
	[ _cn-xz "Tibet"          ]
	[ _un    "United Nations" ]
	[ _va    "Valhalla"       ]
	[ _ve    "Vegan"          ]
	[ _wh    "White"          ]
]

.customflag_entry = [ // code name body
	uitarget 1 $uiPad:SXL [
		style_button_hold1 "n" "n" "n" (? (=s [@@arg1] $customflag) "d" "n")
		uiclamp*e
		uirelease [
			customflag [@@@arg1]
			hideui "customflags"
		]
		uihlist 0 [
			uifill $uiPad:M 0 []
			uialign -1 0
			@@arg3
			uitext [ @@@arg2 ] 0.6
		]
	]
]
