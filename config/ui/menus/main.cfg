
uiButton:Main = [
    uitarget 0 0 [
        nodebug [ uihover [
            +f= [@arg1.f] 0.25
        ] [ -f= [@arg1.f] 0.12 ] ]
        clampf= [@arg1.f] 0 1
        uiDrawSpriteTiled $arg1 (
            +s (intmul $uiAccentColor) (fade $[@arg1.f])
        ) (
            +s <agrad:0,.91,1,.86> <agrad:.2,.94,0,.99> "data/interface/debug/causticmap"
        ) $uiPad:D6XL $uiPad:D6XL $uiPad:DS 128 0 50 (&& $uiSpriteAnim [!=f $[@arg1.f] 0])
        uiclamp-e
        uiHoverOnce [ uiHoverSound 1 ]
        uipress $arg3
        uispace (+f $uiPad:XL (*f $uiPad:5XL $[@arg1.f])) 0 [
            uiTriangle (
                |A! (*fA $[@arg1.f] 0x30)
            ) (
                |A! (*fA $[@arg1.f] 0xA0)
            ) $uiPad:2XL $uiPad:L 270
        ] ; uialign- -1
        uihlist 0 [
            uifill (*f $uiPad:6XL $[@arg1.f])
            uispace $uiPad:5XL 0 [
                uiFancyText "bold" $arg2 0.9 0 $m0.f
            ]
        ] ; uialign- -1
    ]
]

newui "main_" [
    uieschide 0
    uiclamp.e

    if (uivisible "main") [
        +f= m0.f 0.125
    ] [ -f= m0.f 0.125 ]
    clampf= m0.f 0 1

    if (=f $m0.f 0) [ hideui $uiname ]
    uiallowinput (=f $m0.f 1)

    local x ; x = (maxf 0 (-f $uiaspect $uiWideHUD))
    if (>f $x 0) [ x = (*f $x 0.5 $m0.f) ]

    uispace (+f $uiPad:L $x) $uiPad:L [
        //uioutline (rnd 0xFFFFFF) ; uiclamp-e // debug boundary outline
        uivlist 0 [
            if $mainmenu [
				uiButton:Main m1 "PLAY" [
					changeui "new_game" "main"
				]
			]
            uiButton:Main m3 "SETTINGS" [ changeui "settings" "main" ]
            if $uiDebug [
                uiButton:Main m4 "::DEBUG::" [ changeui "debug" "main" ]
            ]
            if $mainmenu [
                // uiButton:Main m11 "TUTORIAL" [
                // hideui "main"
                // tutorial
                // ]
				uiButton:Main m11 "HELP" [
					changeui "help" "main"
                ]
            ] [
				uiButton:Main m1 "SERVERS" [
					changeui "servers" "main"
				]
				uiButton:Main m2 "CHANGE MAP" [
					changeui "new_game" "main"
				]
				if $isspectator [
                    uiButton:Main m5 "^f1JOIN" [
                        spectator 0
                        hideui "main"
                    ]
                ] [
                    uiButton:Main m5 "SPECTATE" [
                        spectator 1
                        hideui "main"
                    ]
                ]
                if (m_teammode (getmode)) [
                    uiButton:Main m6 "SWAP TEAM" [
                        if (= $getteam 1) [ team "Vanir" ] [ team "Aesir" ]
                    ]
                ]
                uiButton:Main m7 "BOTS" [
                    changeui "bots" "main"
                ]
                uiButton:Main m8 "PLAYERS" [
                    changeui "cci" "main"
                ]
                uiButton:Main m9 "DISCONNECT" [ disconnect ]
            ]
            uiclamp*x
        ] ; uialign- -1
    ] ; uiclamp-e
] [
    m0.f = 0
    uibind "ESCAPE" [ if $mainmenu [] [ hideui "main" ] ]
    uiSetBgBlur 1
    uiSetLanguage "main"
	if (&& $isconnected (! $mainmenu)) [
		pausegame 1
	]
] [
    uiSetBgBlur -1
	if (&& $isconnected (! $mainmenu)) [
		pausegame 0
	]
]

newui "main" [
    uiallowinput 0
    uieschide (! $mainmenu)
] [
    if (!=s $name "player") [
        uiNameSet 1
    ]
    if $uiNameSet [
        showui "main_"
        uisound ui/shelf1.wav 150
    ] [ changeui "welcome" "main" ]
    showhud 0
] [
    uisound ui/shelf0.wav 150
    showhud 1
]

newui "main_welcome" [
    uieschide (! $mainmenu)
	uialign 0 1
	uispace $uiPad:D4XL $uiPad:D4XL [
		uiFastImgStretched [<mad:0.1,0.1,0.1><agrad:.7,.1,0,0><agrad:0,.2,1>] "hud/" "shelf" "" "" 1.25 0.06
		uihlist $uiPad:M [
			alpha = (*f 1.0 (uiCosWave 480))
			uifontcolorblendtext "wide" "PRESS" $c_white $alpha 0.8
			uiKeybox $uiPad:DL $uiPad:5XL [ uitext "SPACE" 0.6 ]
			uifontcolorblendtext "wide" "TO START" $c_white $alpha 0.8
		]
	]
] [
	uibind "SPACE" [
		loadmainmenu
	]
	uibind "ESCAPE" [
		if $mainmenu [
			showui "dialog_quit"
		] [
			hideui "main_welcome"
		]
	]
] []
