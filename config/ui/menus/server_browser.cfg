
uiMenu "servers" [
	refreshservers
	uivlist $uiPad:L [
		uihlist 0 [
			uiclamp.x
			uiButton "hold2" [
				uiFastImg "" "ui/" "refresh" "" 0.030
				uitext "Update Server List" 0.6
			] 0 $uiPad:6XL [ updatefrommaster ] 0 [
				uiSetTip "retrieve registered servers from the masterserver"
			]
			uihlist 0 [
				uiBar 0 1 $uiPad:L $uiPad:S
				uiButtonArea "hold2" $uiPad:DS $uiPad:6XL [
					uiSetTip "scan for servers on your local area network"
					uiFastImg "" "ui/" "lan" $searchlan 0.028
					uirelease [ searchlan (! $searchlan) ]
				]
				uiButtonArea "hold2" $uiPad:DS $uiPad:6XL [
					uiSetTip "automatically update server information on display"
					uiFastImg "" "ui/" "web" $autoupdateservers 0.030
					uirelease [ autoupdateservers (! $autoupdateservers) ]
				]
				uiButtonArea "hold2" $uiPad:DS $uiPad:6XL [
					uiSetTip "automatically sort servers by player amount & latency"
					uiFastImg "" "ui/" "sort" $autosortservers 0.030
					uirelease [
						.autosortservers (! $.autosortservers)
						autosortservers $.autosortservers
					]
				]
				uiBar 0 1 $uiPad:L $uiPad:S
			]
			uiButton "hold2" [
				uiCheckbox $.sb_showfilters 0 0 0 [
					if $arg1 [ uiTriangle $c_line1 0 $uiPad:O5 0 180 ]
				]
				uitext "Server Filtering" 0.6
				uiFastImg "" "ui/" "piechart" "" 0.03
			] 0 $uiPad:6XL [ .sb_showfilters (! $.sb_showfilters) ] 0 [
				uiSetTip "select filters with which to hide servers"
			]
		]
		if $.sb_showfilters [
			uiBox [box d n n d n] 0 0.148 [
				uispace $uiPad:L $uiPad:L [
					uihlist $uiPad:L [
						uiFilterTable 0.13 $uiPad:5XL 0.55 [
							"FFA"      [ uiCheckbox [& $.sb_mode  2] ] [ .sb_mode (^ $.sb_mode  2) ]
							"CTF"      [ uiCheckbox [& $.sb_mode  4] ] [ .sb_mode (^ $.sb_mode  4) ]
							"ELIM"     [ uiCheckbox [& $.sb_mode  8] ] [ .sb_mode (^ $.sb_mode  8) ]
							"LMS"      [ uiCheckbox [& $.sb_mode 16] ] [ .sb_mode (^ $.sb_mode 16) ]
							"JGNT"     [ uiCheckbox [& $.sb_mode 32] ] [ .sb_mode (^ $.sb_mode 32) ]
							"INFC"     [ uiCheckbox [& $.sb_mode 64] ] [ .sb_mode (^ $.sb_mode 64) ]
							"EDIT"     [ uiCheckbox [& $.sb_mode  1] ] [ .sb_mode (^ $.sb_mode  1) ]
							"Disabled" [ uiRadio    [= $.sb_mode  0] ] [ .sb_mode 0 ]
						] 2 "hide servers according to game mode"
						uiBar 0 1
						uiFilterTable 0.13 $uiPad:5XL 0.55 [
							"Default"  [ uiCheckbox [& $.sb_mm  1] ] [ .sb_mm (^ $.sb_mm  1) ]
							"Open"     [ uiCheckbox [& $.sb_mm  2] ] [ .sb_mm (^ $.sb_mm  2) ]
							"Veto"     [ uiCheckbox [& $.sb_mm  4] ] [ .sb_mm (^ $.sb_mm  4) ]
							"Locked"   [ uiCheckbox [& $.sb_mm  8] ] [ .sb_mm (^ $.sb_mm  8) ]
							"Private"  [ uiCheckbox [& $.sb_mm 16] ] [ .sb_mm (^ $.sb_mm 16) ]
							"Password" [ uiCheckbox [& $.sb_mm 32] ] [ .sb_mm (^ $.sb_mm 32) ]
							"" [] []
							"Disabled" [ uiRadio    [= $.sb_mm  0] ] [ .sb_mm 0 ]
						] 2 "hide servers according to master mode"
						uiBar 0 1
						uiFilterTable 0.13 $uiPad:5XL 0.55 [
							"50+"      [ uiRadio [= $.sb_ping   55] ] [ .sb_ping   55 ]
							"100+"     [ uiRadio [= $.sb_ping  110] ] [ .sb_ping  110 ]
							"150+"     [ uiRadio [= $.sb_ping  165] ] [ .sb_ping  165 ]
							"250+"     [ uiRadio [= $.sb_ping  270] ] [ .sb_ping  270 ]
							"500+"     [ uiRadio [= $.sb_ping  525] ] [ .sb_ping  525 ]
							"Disabled" [ uiRadio [= $.sb_ping 9999] ] [ .sb_ping 9999 ]
						] 2 "hide servers according to latency"
						uiBar 0 1
						uiFilterTable 0.13 $uiPad:5XL 0.55 [
							"Empty"    [ uiCheckbox [= $.sb_players_e 1] ] [ .sb_players_e (! $.sb_players_e) ]
							"Full"     [ uiCheckbox [= $.sb_players_f 1] ] [ .sb_players_f (! $.sb_players_f) ]
							"Disabled" [ uiRadio [&& [! $.sb_players_e] [! $.sb_players_f]] ] [ .sb_players_e 0 ; .sb_players_f 0 ]
						] 1 "hide servers according to player amount"
						uialign* -2 -1
					]
				]
			]
		]
		uiToolTipBox 0.96 (? $.sb_showfilters 0.482 0.64) [
			local numfiltered
			uihover [ autosortservers 0 ] [
				autosortservers $.autosortservers
				.serverhover = -1
			]
			uiSetTip "join server"
			uivlist 0 [
				uialign -2 -1
				uiclamp.x
				loop i $numservers [
					if (servinfovalid $i) [
						if (&& [ // latency
							<= (servinfoping $i) $.sb_ping
						] [      // mastermode
							if $.sb_mm [! (& (<< 1 (+ (servinfomastermode $i) 1)) $.sb_mm)] 1
						] [      // gamemode
							if $.sb_mode [! (& (<< 1 (servinfomode $i)) $.sb_mode)] 1
						] [      // empty server
							if $.sb_players_e [servinfonumplayers $i] 1
						] [      // full server
							if $.sb_players_f [< (servinfonumplayers $i) (servinfomaxplayers $i)] 1
						]) [
							uihover+ [ .serverhover = $i ]
							uiServer $i
						]
					] [ numfiltered = (+ $numfiltered 1) ]
				]
				uiclamp*x
			]
		] 0 [
			if (> $.serverhover -1) [
				uicolortext (servinfoname $.serverhover) $c_gray 0.5
				uifontcolortext "wide" ":" $c_gray 0.5
				uitextfill 2.5 0.5 [
					uicolortext (servinfoport $.serverhover) $c_gray 0.5
					uialign- -1
				]
			]
			uiline $c_line1 $uiPad:S
			uiline $c_line1 $uiPad:S
			uiclamp*y
			uitextfill 8.25 0.5 [
				uicolortext (concat (- $numservers $numfiltered) "^f7servers found") $c_green 0.5
				uialign- 1
			]
		]
	]
] [
	sleep 200 [ initservers ]
] [] [
	uiallowinput (! (uivisible "dialog_serverconnect"))
] [ uitext "Server Browser" 0.8 ]


// *** specialized for "servers" UI ***
// uiServer ID
uiServer = [
	arg2 = (&& [=s $connectedip (servinfoname $arg1)] [= $connectedport (servinfoport $arg1)])
	uitarget 0 (*f $uiPad:5XL 2) [
		style_button_hold1 d (? $arg2 d n) (? $uihover? d n)
		uiclamp*e
		uirelease [
			if (!= (servinfonumplayers $arg1) (servinfomaxplayers $arg1)) [
				if (> (servinfomastermode $arg1) 2) [
					.sb_connectid $arg1
					showui "dialog_serverconnect"
				] [ connectservinfo $arg1 ]
			]
		]
		uihlist $uiPad:L [
			uialign -1
			if $arg2 [
				uispace $uiPad:OL1 0 [ uiTriangle $c_line2 0 $uiPad:4XL $uiPad:S 270 ]
			] [ uifill $uiPad:M ]
			uigroup [
				uifill $uiPad:DS
				uicroppedimage "data/interface/ui/mm.png" $uiPad:DSS $uiPad:DSS [@(*f (+ (servinfomastermode $arg1) 1) 48)p] 0 48p 1
				uispace 0 $uiPad:L [ uifonttext "wide.ol" (servinfomodename $arg1) 0.5 ]
				uialign- 0 1
			]
			uivlist $uiPad:S [
				uispace $uiPad:O4 0 [
					uihlist $uiPad:L [
						uifontcolortext "wide" "::" $c_gray 0.6
						uicolortext (servinfodesc $arg1) (? $arg2 $c_olive $c_white) 0.6
					]
				]
				uihlist $uiPad:L [
					if (m_timed (servinfomode $arg1)) [
						uifontcolortext "webdings" "i" $c_gray 0.65
						uifontcolortext "mono" (servinfotime $arg1) $c_green 0.55
					]
					if (servinfomap $arg1) [
						uifontcolortext "wide" ">" $c_gray 0.6
						uicolortext (servinfomap $arg1) $c_gray_l 0.55
					]
					uialign* -2 -1
				]
				uialign* -1
			]
		]
		uihlist $uiPad:L [
			uialign 1
			uifill $uiPad:SXL 0 [
				uivlist $uiPad:O3 [
					loopwhile i 5 [<= (servinfoping $arg1) (at "525 270 165 110 55" $i)] [ arg3 = (+ $i 1) ]
					uicroppedimage "data/interface/ui/conn.png" 0.025 0.025 [@(*f $arg3 64)p] 0 64p 1
					if (> (servinfoping $arg1) 999) [ uicolortext "999+" $c_gray 0.55 ] [
						uitext (servinfoping $arg1) 0.55
					]
				]
			]
			uifill $uiPad:XL $uiPad:6XL [
				uihlist $uiPad:O5- [
					uiclamp.y
					uifill (*f $uiPad:L 3.4) 0 [
						uifonttext "mono" (servinfonumplayers $arg1) 0.6
						uialign- 1 -1
					]
					uioffset 0 $uiPad:L- [
						uiclip 0 $uiPad:SXL [ uicolortext "/" $c_gray 1.3 ]
					] // text takes up a lot of vertical space, have to "cut" it
					uifill $uiPad:SXL 0 [
						uifontcolortext "mono" (servinfomaxplayers $arg1) $c_gray_l 0.55
						uialign- -1 1
					]
					uiclamp*y
				]
			]
		]
	]
]

// *** specialized for "servers" UI ***
// uiFilterTable X Y TEXTSIZE [ "NAME" [ children ] [ action ] .. ] COLUMNS "tooltip"
uiFilterTable = [
	uigrid $arg5 0 0 [
		uiSetTip $arg6
		looplist3 n c a $arg4 [
			if (=s $n "") uifill [
				uiButton "hold2" [
					c ; uitext $n $arg3
				] $arg1 $arg2 $a -1
			]
		]
	]
]


////////////////////////////////////////////////////////////////////////////////////////////////////

// search desc
//    if $.sb_searchdesc [>= (strstr (servinfodesc $i) $.sb_searchdesc) 0] 1
//] [      // search map
//    if $.sb_searchmap [>= (strstr (servinfomap $i) $.sb_searchmap) 0] 1
//] [

//cond (|| [= (servinfonumplayers $arg1) (servinfomaxplayers $arg1)] [> (servinfomastermode $arg1) 2]) [
//    uihgradient 0xFFFFFF 0x80D04040
//] (>= (*f 100 (divf (servinfonumplayers $arg1) (servinfomaxplayers $arg1))) 60) [
//    uihgradient 0xFFFFFF 0x80D0B040
//]

//uiFastImg "" "key" (servinfohaspassword $arg1) $uiPad:2XL
//uialign- -1  1
//uiFastImg "" "fav" (servinfokeep $arg1) $uiPad:2XL
//uialign-  1  1
