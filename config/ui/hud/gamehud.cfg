
///////////////////////////////////////////////////////////////////////////////
//  GAMEHUD UI -- MAKE CHANGES AT THY OWN PERIL                              //
///////////////////////////////////////////////////////////////////////////////

newui "gamehud" [
    uiclamp.e
    if (|| $mainmenu (! $showhud)) [ uiallowinput 0 ] [

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  EDITHUD DISPLAY GROUP                                                                                   //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

        if $editing [
            uieschide 0
            uispace $uiPad:M $uiPad:M [
                uihlist $uiPad:M [
                    if (iskeyheld "LALT") [
                        uiallowinput 1
                        uiHudCell $uiPad:DM $uiPad:DXL [
                            uiFastImg "" "ui/" "config" "" $uiPad:SXL
                        ]
                    ] [
                        uiallowinput 0
                        uiHudCell $uiPad:DM $uiPad:DXL [
                            uivlist 0 [
                                uiFastImg "" "ui/" "speed" "" $uiPad:6XL
                                uitext $floatspeed 0.6
                                uifill 0 $uiPad:S
                                uigroup [
                                    uiFastImg "" "ui/" "grid" "" $uiPad:6XL
                                    uitext $gridpower 0.5
                                    uialign- 1 1
                                ]
                            ]
                        ]
                    ]
                ]
            ] ; uialign- -1 1
        ] [
            .gamehud
        ]
    ]
] [
    uiBitsHUD = 0
    showui "hud_"
]

ANNOUNCEMENT_STATUS = 6

.manageAnnouncements = [
    local announcement
    announcement = $getlastannouncement
    if (>= $announcement $ANNOUNCEMENT_STATUS) [
        .lastAnnouncement = $getmillis
    ] [
        .lastMedal = $getmillis
    ]
]

// Announcement medals: images for artists to personalise.
.medalTextures = [
    "kill_headshot"      // Headshot.
    "kill_first"         // First blood.
    "streak_spree"       // Killing spree.
    "streak_savage"      // Savage spree.
    "streak_unstoppable" // Unstoppable spree.
    "streak_legendary"   // Legendary spree.
]

.showAnnouncementInfo = [
    local millis elapsed step ease initSize
    millis = $.lastMedal
    elapsed = (- $getmillis $millis)
    initSize = 0.0
    if (< $elapsed $hudannouncementmillis) [
        initSize = 1.0
        step = (getstepmillis $millis $hudannouncementfade)
        ease = (easeoutback $step)
        initSize = (*f $initSize $ease)
    ] [
        millis = (+ $millis $hudannouncementmillis)
        initSize = 1.0
        step = (getstepmillis $millis $hudannouncementfade)
        ease = (lerpf 1.0 0.0 $step)
        initSize = (*f $initSize $ease)
    ]
    if $initSize [
        uioffset 0 0.6 [
            local xSize ySize announcement
            xSize = (*f $uiPad:D4XL $initSize)
            ySize = (*f $uiPad:DS $initSize)
            announcement = $getlastannouncement
            uiShadowedImg "" "hud/" "announcement/" (at $.medalTextures $announcement) $xSize $ySize
        ]
    ]
]

DEATH_GIB = 4

// Per-attack obituary: normal kill, gib kill, self-frag (normal, gib).
.attackObituary = [
    // Melee
    [ "pummeled" "shattered" ]
    [ "pummeled" "shattered" ]

    // Scattergun
    [ "shot" "splattered" ]
    [ "shot" "splattered" ]

    // Submachine gun
    [ "sprayed"  "gibbed" ]
    [ "peppered" "gibbed" ]

    // Pulse Rifle
    [ "fried"  "vaporised" ]
    [ "zapped" "gibbed"    ]

    // Rocket Launcher
    [ "blasted" "cooked" ]
    [ "blasted" "cooked" ]

    // Railgun
    [ "skewered" "chunked" ]
    [ "sniped"   "chunked" ]

    // Disruptor Grenades
    [ "bombed" "obliterated" ]
    [ "bombed" "obliterated" ]

    // Quantum Pistol
    [ "beamed"   "gibbed"   ]
    [ "toasted"  "gibbed"   ]
    [ "atomised" "atomised" ]

    // Instagun
    [ "fragged" "gibbed" ]

    // Zombie
    [ "infected" "infected" ]
]

ATTACK_ZOMBIE = 18

.showKillInfo = [
    local millis elapsed ease step textInitSize textSize textAlpha textAlphaSecondary imageInitSize imageTargetSize imageSize padInitSize padSize
    millis = $.lastKill
    elapsed = (- $getmillis $millis)
    textInitSize = $uiPad:USS
    textAlpha = 1.0
    textAlphaSecondary = 1.0
    imageInitSize = $uiPad:5XL
    padInitSize = $uiPad:M
    padSize = $padInitSize
    if (< $elapsed $hudkillmillis) [
        step = (getstepmillis $millis 350)
        ease = (easeoutelastic $step)
        textSize = (*f $textInitSize $ease)
        imageSize = (*f $imageInitSize $ease)
        local half
        half = (div $hudkillmillis 2)
        if (>= $elapsed $half) [
            millis = (+ $millis $half)
            imageTargetSize = 0.0
            step = (getstepmillis $millis 200)
            imageSize = (lerpf $imageInitSize $imageTargetSize $step)
            textAlphaSecondary = (lerpf 1.0 0.0 $step)
            padSize = (lerpf $padInitSize 0.0 $step)
        ]
    ] [
        millis = (+ $millis $hudkillmillis)
        local textTargetSize
        textTargetSize = $uiPad:UXL
        step = (getstepmillis $millis 100)
        textSize = (lerpf $textInitSize $textTargetSize $step)
        textAlpha = (lerpf 1.0 0.0 $step)
        textAlphaSecondary = 0.0
    ]
    uioffset 0 $uiPad:UXS [
        uihlist $padSize [
            local lastVictim lastVictimName attack isAlly isGibbed killInfo
            lastVictim = $getlastkillvictim
            attack = $getlastkillattack
            isAlly = (&& (! (m_hideallies $getmode)) (isally $lastVictim) (!= $attack $ATTACK_ZOMBIE)) // Small trick to prevent infections from showing ally icon.
            if (!=f $imageSize $imageTargetSize) [
                local image
                image = "frag"
                if (= $attack $ATTACK_ZOMBIE) [
                    image = "star" // This specific event is scoring, rather than fragging.
                ]
                if $isAlly [
                    image = "skull"
                ]
                uiShadowedImg "" "hud/" $image "" $imageSize
            ]
            isGibbed = (= $getlastkilldeathstate $DEATH_GIB)
            lastVictimName = (getclientname $lastVictim)
            if $isAlly [
                killInfo = (+s "KILLED AN ALLY " $lastVictimName)
            ] [
                killInfo = (+s (at $.attackObituary $attack (? (! $isGibbed) 0 1)) " " $lastVictimName)
            ]
            uifontcolorblendtext "wide" (strupper $killInfo) $c_white $textAlpha $textSize
            if $isAlly [
                uifontcolorblendtext "modernpics" "!" $c_yellow (*f $textAlphaSecondary (uiCosWave 150)) (*f $textSize 2)
            ]
        ]
    ]
]

.showDeathInfo = [
    uioffset 0 $uiPad:US [
        uivlist $uiPad:M [
            uihlist $uiPad:M [
                uiShadowedImg "" "hud/" "death" "" $uiPad:3XL
                local isGibbed deathInfo isAlly
                isGibbed = (= (getlastdeathstate $cn) $DEATH_GIB)
                deathInfo = (+s (at $.attackObituary $getlastkillerattack (? (! $isGibbed) 0 1)) " by")
                uifonttext "wide.ol" (strupper $deathInfo) 0.5
                isAlly = (&& (! (m_hideallies $getmode)) (isally $lastKiller))
                if $isAlly [
                    uifonttext "wide.ol" "ALLY" 0.5
                    uifontcolorblendtext "modernpics.ol" "!" $c_yellow (*f 1.0 (uiCosWave 250)) $uiPad:UL
                ]
            ]
            uiHudCell $uiPad:D3XL 0.036 [
                uispace $uiPad:S $uiPad:S [
                    uihlist $uiPad:S [
                        uiFastImg [<fade:0.5><agrad:0,0,0.9>] "countryflag/" (strlower (getclientcountrycode $lastKiller)) "" (*f 1.5 $uiPad:DM) $uiPad:DS [
                            uihlist $uiPad:S [
                                if (isai $lastKiller) [
                                    uiShadowedImg "" "hud/" "bot" "" $uiPad:3XL
                                ]
                                uifonttext "wide.ol" (getclientname $lastKiller) $uiPad:US
                            ]
                        ]
                        uiBar 0 1 $uiPad:3XL
                        uivlist $uiPad:S [
                            local lastKillerWeapon
                            lastKillerWeapon = $getlastkillerweapon
                            uiFastImg [<rotate:4>] "hud/" (at $.weaponList $lastKillerWeapon) "_raw" (*f (gettexaspectwh (+s "data/interface/hud/" (at $.weaponList $lastKillerWeapon) "_raw.png")) $uiPad:4XL) $uiPad:4XL
                            uifonttext "wide" (at $.weaponPrettyNames $lastKillerWeapon) 0.4
                        ]
                    ]
                ]
            ] $uiPad:S 0
        ]
    ]
]

.bar.respawn = [
    local y ; y = (*f $arg1 0.045)
    .bar:resource $arg1 $arg2 $arg3 0x00E661 0x00ECB2
]

.showRespawnInfo = [
    uioffset 0 $uiPad:US- [
        uivlist 0 [
            local millis seconds delay expired when countdown alpha
            millis = (getrespawnwait 0)
            seconds = (/ $millis 1000)
            expired = (<= $millis 0)
            when = (? $expired "NOW" "IN")
            delay = $getrespawndelay
            countdown = (? (&& (> $delay 1500) $millis) $seconds "")
            alpha = (? $expired (*f 1.0 (uiCosWave 200)) 1.0)
            uifontcolorblendtext "wide.ol" (+s (? $canspawn "RESPAWN " "SPECTATE ") $when " " $countdown) (? (< (getmillis) (+ 150 (getlastspawnattempt))) $c_red $c_white) $alpha $uiPad:USS
            .bar.respawn 0.35 $millis $delay
        ]
    ]
]

// Status announcements: overtime, kills or time remaining, etc.
.statusAnnouncementList = [
    "1 MINUTE REMAINING"
    "5 MINUTES REMAINING"
    "1 KILL REMAINS"
    "5 KILLS REMAINING"
    "10 KILLS REMAINING"
    "OVERTIME"
]

.showStatusInfo = [
    local announcement millis elapsed step alpha size initSize targetSize offset initSizeOffset targetSizeOffset
    announcement = $getlastannouncement
    millis = $.lastStatusAnnouncement
    elapsed = (- $getmillis $millis)
    initSize = 3.0
    targetSize = $uiPad:US
    initSizeOffset = 0.0
    targetSizeOffset = $uiPad:UM-
    if (< $elapsed $hudannouncementmillis) [
        step = (getstepmillis $millis 150)
        alpha = (lerpf 0.0 1.0 $step)
        size = (lerpf $initSize $targetSize $step)
        offset = (lerpf $initSizeOffset $targetSizeOffset $step)
    ] [
        millis = (+ $millis $hudkillmillis)
        step = (getstepmillis $millis 80)
        alpha = (lerpf 1.0 0.0 $step)
        size = (lerpf $targetSize $initSize $step)
        offset = (lerpf $targetSizeOffset $initSizeOffset $step)
    ]
    uioffset 0 $offset [
        uifontcolorblendtext "mono.ol" (at $.statusAnnouncementList (- $announcement $ANNOUNCEMENT_STATUS)) $c_white $alpha $size
    ]
]

.showIntermissionInfo = [
    local millis elapsed step sizeX sizeY initSizeX initSizeY targetSizeX targetSizeY
    millis = $.lastIntermission
    elapsed = (- $getmillis $millis)
    step = (getstepmillis $millis 250)
    initSizeX = 1.8
    initSizeY = $uiPad:DSXL
    targetSizeX = $uiPad:D4XL
    targetSizeY = $uiPad:DS
    sizeX = (lerpf $initSizeX $targetSizeX $step)
    sizeY = (lerpf $initSizeY $targetSizeY $step)
    local offset initSizeOffset targetSizeOffset timestamp
    timestamp = 1000
    if (>= $elapsed $timestamp) [
        millis = (+ $millis $timestamp)
        step = (getstepmillis $millis 180)
        initSizeOffset = 0.0
        targetSizeOffset = $uiPad:UM-
        offset = (lerpf $initSizeOffset $targetSizeOffset $step)
        if (=f $offset $targetSizeOffset) [
            local waveOffset
            waveOffset = (*f $uiPad:M (uiCosWave 1000))
            sizeX = (+f $sizeX $waveOffset)
            sizeY = (+f $sizeY $waveOffset)
        ]
    ]
    uioffset 0 $offset [
        local image
        image = (? $iswinner "game_win" "game_over")
        uiShadowedImg "" "hud/" "announcement/" $image $sizeX $sizeY
    ]
]

.weaponPrettyNames = [
    [Scattergun]
    [Submachine gun]
    [Pulse Rifle]
    [Rocket Launcher]
    [Railgun]
    [Disruptor Grenades]
    [Quantum Pistol]
    [Instagun]
    [Zombie]
    [Melee]
]

.showItemInfo = [
    hover = $arg1
    uioffset 0 0.45 [
        uivlist 0.01 [
            uifontcolortext "wide" (strupper (at $.weaponPrettyNames $hover)) $c_white 0.45
            uiFastImg [<rotate:4>] "hud/" (at $.weaponList $hover) "_raw" (*f (gettexaspectwh (+s "data/interface/hud/" (at $.weaponList $hover) "_raw.png")) $uiPad:DXS) $uiPad:DXS
        ]
    ]
]

.showTimerInfo = [
    uispace 0 $uiPad:O5 [
        uivlist $uiPad:O5 [
            uihlist $uiPad:O5 [
                uiFastImgStretched [<fade:0.5><mad:0.6,0.6,0.6><agrad:.5,.95,.5,.1>] "hud/" "shelf" "" [
                    uispace $uiPad:M $uiPad:M [
                        uihlist $uiPad:S [
                            if (m_edit $getmode) [
                                uifontcolorblendtext "modernpics.ol" "r" $c_white 1.0 $uiPad:US
                                uifontcolorblendtext "mono.ol" "EDITOR" $c_white 0.95 0.4
                            ] [
                                local icon
                                icon = "frag"
                                if (m_ctf $getmode) [ icon = "flag" ]
                                if (m_round $getmode) [ icon = "star" ]
                                if (m_invasion $getmode) [ icon = "monster" ]
                                uiShadowedImg "" "hud/" $icon "" $uiPad:3XL
                                local scoreLimit
                                if (m_invasion $getmode) [
                                    scoreLimit = (* $spskill 10) 
                                ] [
                                    scoreLimit = $getscorelimit
                                ]
                                uifontcolorblendtext "mono.ol" $scoreLimit $c_white 0.95 $uiPad:US
                            ]
                            uialign 1
                        ]
                        uiFancyText "mono" "      " $uiPad:US
                    ]
                ] $uiPad:L
                uiFastImgStretched [<agrad:0,2>] "hud/" "shelf" "" [
                    uiFastImg <mad:0/0/0><fade:0.5> "hud/" "glow" "" $uiPad:DXL $uiPad:DS
                    if $intermission [
                        uifontcolorblendtext "mono" $timeremaining $c_white 0.9 $uiPad:UM
                    ] [
                        if (|| $isgamewaiting (! (m_timed $getmode))) [
                            uifontcolorblendtext "modernpics" "'" $c_white 1.0 1.4
                        ] [
                            uivlist 0 [
                                uihlist $uiPad:O3 [
                                    local time
                                    time = (div $timeremaining 60)
                                    alpha = (? (< $time 1) (*f 1.0 (uiCosWave 250)) 1.0)
                                    uifontcolorblendtext "mono.ol" $time $c_white $alpha $uiPad:UM
                                    uifontcolorblendtext "mono.ol" ":" $c_white $alpha $uiPad:UM
                                    uifontcolorblendtext "mono.ol" (pad0 (mod $timeremaining 60) 2 "0") $c_white $alpha $uiPad:UM
                                ]
                            ]
                        ]
                    ]
                ] $uiPad:5XL
                uiFastImgStretched [<fade:0.5><mad:0.6,0.6,0.6><agrad:.5,.95,.5,.1>] "hud/" "shelf" "" [
                    uispace $uiPad:M $uiPad:M [
                        uihlist $uiPad:S [
                            uifontcolorblendtext "mono.ol" $numplayers $c_white 0.95 $uiPad:US
                            uiShadowedImg "" "hud/" "player" "" $uiPad:3XL
                            uialign -1
                        ]
                        uiFancyText "mono" "      " $uiPad:US
                    ]
                ] $uiPad:L
            ]
            if (&& $isgamewaiting (! $intermission)) [
                uifontcolorblendtext "wide.ol" "WAITING FOR PLAYERS" $c_white (*f 1.0 (uiCosWave 500)) $uiPad:US
            ]
        ]
    ]
    uialign- 0 -1
]

.showHudInfo = [
    .showTimerInfo
    if $intermission [
        .showIntermissionInfo
    ] [
        if $isdead [
            .showRespawnInfo
        ] [
            hover = $gethoverweapon
            if (> $hover -1) [
                .showItemInfo $hover
            ]	
        ]
        if (&& $.lastAnnouncement (<= (- $getmillis $.lastAnnouncement) (+ $hudannouncementmillis $hudannouncementfade))) [
            .showStatusInfo
        ]
    ]
    if $isdead [
        lastKiller = $getlastkiller
        if (&& (!= $lastKiller $cn) (> $lastKiller -1)) [
            .showDeathInfo
        ]
    ] [
        if (&& $.lastKill (<= (- $getmillis $.lastKill) (+ $hudkillmillis $hudkillfade))) [
            .showKillInfo
        ]
        if (&& $.lastMedal (<= (- $getmillis $.lastMedal) (+ $hudannouncementmillis $hudannouncementfade))) [
            .showAnnouncementInfo
        ]
    ]
]

.gamehud = [
    uiallowinput 0
//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  GENERAL GAME HUD INFORMATION GROUP                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////
    if (! (uivisible "scoreboard")) [
        .showHudInfo
    ]

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  SCOREBOARD DISPLAY GROUP                                                                                //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

    if (&& [iskeyheld "TAB"] [! $iscmdlineopen]) [
        if (uivisible "scoreboard") [] [ showui "scoreboard" ]
        uivlist $uiPad:5XL [
            uialign 0 -1
            uifill 0 $uiPad:DM
            if (m_teammode $getmode) [
                .scoreboard.ctf
            ] [ .scoreboard.ffa ]
                .scoreboard.spec
            ]

            uispace $uiPad:L $uiPad:L [
                uivlist 0 [
                    uiFancyText "wide" (getmodeprettyname $getmode) 1.2
                    uifill 0 $uiPad:DM [ uivlist 0 [
                        if $scoreboardmap [
                            uiFancyText "wide" "- ON -" $uiPad:UM $c_gray
                            uiFancyText "wide" $scoreboardmap 1 $c_green
                        ]
                    ]
                ]
            ]
            if (isconnected 1 0) [
                uigroup [
                    uicolortext $scoreboardservinfo (|A! 0x48) 0.7
                ] ; uialign- -1 1
            ]
        ] ; uiclamp-x ; uialign- 0 1
    ] [
        .playerhud
    ]
]

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  MINIMAP UTILITIES                                                                                       //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

MINIMAP_SIZE = 0.25

// rotate a vec2 by an angle and return the X and Y component, respectively
vec2rotatex = [+f (*f (cos $arg3) $arg1) (*f (sin $arg3) $arg2)] // X cos(@) + Y sin(@)
vec2rotatey = [-f (*f (cos $arg3) $arg2) (*f (sin $arg3) $arg1)] // Y cos(@) - X sin(@)

// x y z angle icon scale=0.014 offsetx offsety mad
radar_icon = [
    local dirx diry dist r_dirx r_diry bx by
    if $arg6 [] [ arg6 = 0.014 ]
    dirx = (*f (divf 1 $calcradarscale) (-f $getcamx $arg1))
    diry = (*f (divf 1 $calcradarscale) (-f $getcamy $arg2))
    dist = (sqrt (+f (*f $dirx $dirx) (*f $diry $diry)))
    if (>=f $dist 0.9) [
        dirx = (*f (divf 0.9 $dist) $dirx)
        diry = (*f (divf 0.9 $dist) $diry)
    ]
    r_dirx = (vec2rotatex $dirx $diry $getcamyaw)
    r_diry = (vec2rotatey $dirx $diry $getcamyaw)
    bx = (+f $arg7 (*f $MINIMAP_SIZE $r_dirx))
    by = (+f $arg8 (*f $MINIMAP_SIZE $r_diry))
    uispace -1 -1 [ uioffset $bx $by [ uiFastImgRotated $arg9 "hud/" $arg5 "" $arg4 $arg6 ] ]
]

.playerhud = [
    if (uivisible "scoreboard") [ hideui "scoreboard" ]
    local x cn
    if $isspectator [
        cn = $getfollow
        uivlist 0 [ // spectating display group
            uispace $uiPad:3XL 0 [
                uiFancyText "wide" (? $isghost "WAITING" "SPECTATING") 1.1 $c_yellow
            ]
            uispace $uiPad:3XL 0 [
                uiFancyText "" (+s (getclientcolorname $cn) " ") 0.95
            ]
            uialign* 1
            uifill 0 $uiPad:DXL
        ] ; uialign- 1 1
    ] [ cn = $getclientnum ]

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  MINIMAP DISPLAY GROUP                                                                                   //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////
    if (&& $showminimap [m_ctf $getmode]) [
        uispace $uiPad:L $uiPad:L [
            local cteam

            if (>= $getfollow 0) [
                cteam = (getclientteam $getfollow)
            ] [ cteam = $getteam ]

            uiminimap $getcamx $getcamy $getcamyaw $MINIMAP_SIZE 16    // minimap background
            uiFastImg "" "hud/" "radar" "" (+f $MINIMAP_SIZE 0.005)    // minimap border

            // flag bases
            loop i $numflags [
                do [ radar_icon @(flagbase $i) 0 (+s "blip_" (? (= (flagteam $i) 1) "blue" "red")) 0.01 ]
            ]

            // players
            looplist cn (listclients 1 1) [
                if (&& $radarteammates [! (isspectator $cn)] [= $cteam (getclientteam $cn)] [!=s $cn (? $isspectator $getfollow $getclientnum)] [! (getclientflag $cn)]) [
                    if (isdead $cn) [
                        do [ radar_icon @(getclientpos $cn) 0 (+s "blip_" (? (= (getclientteam $cn) 1) "blue" "red") "_dead") ]
                    ] [
                        do [ radar_icon @(getclientpos $cn) (- $getcamyaw (getclientyaw $cn)) (+s "blip_" (? (= (getclientteam $cn) 1) "blue" "red") "_alive") ]
                    ]
                ]
            ]

            // flags
            loop i $numflags [
                case (flagstate $i) 0 [
                    // flag is at base
                    do [ radar_icon @(flagbase $i) 0 (+s "blip_" (? (= (flagteam $i) 1) "blue" "red") "_flag") 0.02 0.017 -0.017 ]
                ] 1 [
                    // a player has the flag
                    if (< (mod $getmillis 1000) 500) [
                        do [ radar_icon @(getclientpos (flagowner $i)) 0 (+s "blip_" (? (= (flagteam $i) 1) "blue" "red") "_flag") 0.02 0.017 -0.017 ]
                    ]
                ] 2 [
                    // flag is dropped somewhere
                    if (< (mod $getmillis 300) 150) [
                        do [ radar_icon @(flagdroploc $i) 0 (+s "blip_" (? (= (flagteam $i) 1) "blue" "red") "_flag") 0.02 0.017 -0.017 ]
                    ]
                ] // 3 - flag fell under the map, don't show icon
            ]
        ] ; uialign- 1 -1
    ]

    x = (maxf 0 (-f $uiaspect $uiWideHUD))
    if (>f $x 0) [ x = (*f $x 0.5) ]

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  KILLFEED DISPLAY GROUP                                                                                  //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

    uispace $uiPad:M $uiPad:M [
        uivlist 0 [
            local i timestamp
            if (&& $showminimap [m_ctf $getmode]) [
                uifill 0 (+f $MINIMAP_SIZE $uiPad:L)
            ]
            looplistrev n $fragQueue [
                case $i 0 [ timestamp = $n ] 1 [
                    do [ .killfeed.row @n ]
                ] ; i = (mod (+ $i 1) 3)
            ]
        ]
    ] ; uialign- 1 -1

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  GAMEHUD DISPLAY GROUP                                                                                   //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

    uispace (+f $uiPad:3XL $x) $uiPad:3XL [
        //uioutline (rnd 0xFFFFFF) ; uiclamp-e // debug boundary outline

        if (|| $uiBlurring [< $cn 0] [isdead $cn] [&& $thirdperson [> $getfollow -1]]) [
            if (isSpriteLive "hud") [ disableSprite "hud" ]
        ] [
            .resourcehud
        ]
    ] ; uiclamp-x ; uialign- -1 1
]

.weaponList = [
    scattergun
    smg
    pulse
    rocket
    railgun
    grenade
    pistol
    instagun
    zombie
    melee
]

.weaponBarList = [
    pistol
    smg
    scattergun
    rocket
    pulse
    railgun
    grenade
    instagun
    zombie
]

.powerUpList = [
    doubledmg
    haste
    armor
    infammo
    agility
    invulnerability
]

MAX_WEAPONS = 9

.resourcehud = [
    uihlist 0 [
        // resource (shield/health) display group
        uivlist 0 [
            if (getclientshield $cn) [
                .bar.shield 0.28 (getclientshield $cn) 200 shield
                uifill 0 $uiPad:XL
            ]
            .bar.health 0.36 (getclienthealth $cn) 100 health
        ] ; uialign- 0 1
    ] ; uialign- -1 1
    uivlist $uiPad:3XL [
        pushif cp (getclientpowerup $cn) [ // Power-up and duration display group.
            uihlist $uiPad:M [
                uiFastImg "" "hud/" (at $.powerUpList (- $cp 1)) "" 0.03
                .bar.powerup 0.25 (getclientpowerupmillis $cn) (? (= $cp 6) 15000 30000)
            ]
        ]
        if $hudweaponbar [
            uihlist 0.007 [
                loop i $MAX_WEAPONS [ // All weapons.
                    local weaponId
                    weaponId = (at $.weaponBarList $i)
                    if (hasammo $weaponId) [
                        uifill 0.07 0.07 [
                            uivlist $uiPad:O2 [
                                local interpolation step
                                step = (getstepmillis $getlastswitch 60)
                                interpolation = (lerpf 0.0 $uiPad:M $step)
                                if (<= (- $getmillis $getlastswitchattempt) $hudweaponbarmillis) [
                                    uiFastImg [<fade:0.85>] "hud/" $weaponId "" (? (isgunselect $weaponId) (+f 0.045 $interpolation) 0.045)
                                    uifill $uiPad:3XL 0.01 [
                                        if (isgunselect $weaponId) [
                                            uiFastImg [<fade:0.85>] "hud/" "row" "" 0.015 $uiPad:O5
                                        ]
                                    ]
                                ]
                            ]
                        ]
                    ]
                ]
            ] ; uialign 0 1
        ]
    ]
    // Ammo group
    uivlist 0 [
        if (getclientflag $cn) [
            uioffset 0 $uiPad:USS [
                if (< (mod $getmillis 1000) 500) [
                    uiFastImg [<fade:0.85>] "hud/" "flag_" (? (= (getclientflag $cn) 1) "blue" "red") $uiPad:DM
                ]
            ] ; uialign- 1 1 
        ]
        local millis elapsed
        millis = $getlastpickupmillis
        elapsed = (- $getmillis $millis)
        uispace 0 $uiPad:M [
            if (< $elapsed 3500) [
                local step ease alpha
                alpha = 1.0
                step = (getstepmillis $millis 3500)
                ease = (lerpf 1.0 0.0 $step)
                alpha = $ease
                uifontcolorblendtext "wide.ol" $lasthudpickupinfo $c_white $alpha $uiPad:USS
            ] [
                uifonttext "wide.ol" " " $uiPad:USS
                lasthudpickupinfo = ""
            ]
        ] ; uialign- 1 1
        uihlist $uiPad:S [
            local ease step icon ammo iconSize textSize alpha
            step = (getstepmillis $getlastswitch 120)
            ease = (easeoutback $step)
            iconSize = (maxf $uiPad:S (*f 0.030 $ease))
            ammo = (getclientammo $cn)
            alpha = (? (<= $ammo 5) (*f 1.0 (uiCosWave 150)) (*f 1.0 $ease))
            uiFastImgStretched [<fade:0.35>] "hud/" "shelf" "" [
                uispace $uiPad:M $uiPad:M [
                    uifonttext "mono" "   " 0.65 // Make sure we have enough space for more characters.
                    uifontcolorblendtext "wide" $ammo $c_white $alpha 0.65
                    uialign- 1
                ]
                uialign 0 -1 
            ]
            uigroup [
                uiFastImgStretched [<fade:0.25><mad:0.6,0.6,0.6>] "hud/" "shelf" "" [
                    uispace $uiPad:M $uiPad:M [
                        // Trick to make sure the weapon shelf has the same size as the ammo shelf.
                        uifonttext "mono" " " 0.65
                        uifill 0.178 0
                    ]
                ]
                local weaponIcon
                weaponIcon = (at $.weaponList (getclientweapon $cn))
                uiFastImg [<fade:0.25>] "hud/" "glow" "" (*f (gettexaspectwh (+s "data/interface/hud/" $weaponIcon "_raw.png")) $iconSize) $iconSize
                uiFastImg [<rotate:4><fade:0.85>] "hud/" $weaponIcon "_raw" (*f (gettexaspectwh (+s "data/interface/hud/" $weaponIcon "_raw.png")) $iconSize) $iconSize
            ]
        ]
    ] ; uialign- 1 1
]

// temporary measure to set Y pos of chat input
newui "hud_" [
    uiallowinput 0
    uiabovehud
    uialign -1 1
    uifill 0 $uiPad:D2XL
]


// XXX MOVE TO A DIFFERENT FILE LATER
// linear:HSV  INT1  INT2  X-TIME  WAVELENGTH
linear:HSV = [
    local r1 g1 b1 r2 g2 b2
    INT>HSV $arg1 [ h1 = $h ; s1 = $s ; v1 = $v ]
    INT>HSV $arg2 [ h2 = $h ; s2 = $s ; v2 = $v ]
    HSV>RGB (
        +f $h1 (*f (/f (-f $h2 $h1) $arg4) $arg3) ) (
        +f $s1 (*f (/f (-f $s2 $s1) $arg4) $arg3) ) (
        +f $v1 (*f (/f (-f $v2 $v1) $arg4) $arg3)
    ) [ RGB>INT $r $g $b ]
]

.bar.health = [
    local y ; y = (*f $arg1 0.08)
    if (> $arg2 $arg3) [
        arg5 = 0x80F0FF ; arg6 = 0xD0F0FF // supercharged
    ] [ push n (%f $arg2 $arg3) [         // normal health
        if (<f $n 0.5) [ n = (*f 16 (pow $n 5)) ] [
            n = (-f 1 (/f (pow (+f (*f -2 $n) 2) 4) 2))
        ]
        // smoothstep lerp approach?
        // uncomment lines below and comment lines above to test
        //n = (*f $n $n (-f 3 (*f $n 2)))
        //clampf= n 0 1
        arg5 = (linear:HSV 0xFF3000 0x008060 $n 1)
        arg6 = (linear:HSV 0xF01000 0x00F080 $n 1)
    ] ]
    .bar:toparea $arg1 $arg2 $arg3 $arg4 $arg5 $arg6 25 [
        pushif over (max 0 (- $arg2 $arg3)) [
            uihlist $uiPad:2XL- [
                push y (*f $y 0.75) [ loop i 3 [
                    if (> (ceil (/f $over 50)) $i) [
                        i = (sinBounce $uiPad:XL (* $i 100) 400)
                        uioffset 0 (+f (minf 0 $i) $uiPad:S) [
                            uiShadowedImg (mad $arg6) "hud/" "shield2" "" $y
                        ]
                    ]
                ] ]
            ] ; uiclamp-y ; uialign- 1
        ]
    ]
]

.bar.shield = [
    local y ; y = (*f $arg1 0.085)
    .bar:toparea $arg1 $arg2 $arg3 $arg4 0xE06000 0xFFD000
]

.bar.powerup = [
    local y ; y = (*f $arg1 0.06)
    .bar:resource $arg1 $arg2 $arg3 0x80D0FF 0xD0F0FF
]

// use: 1:WIDTH  2:VALUE  3:MAXVAL  4:IMG/ID  5:COL1  6:COL2  7:LOW  8:[CHILDREN]
.bar:toparea = [
    uivlist 0 [
        uigroup [
            do $arg8
            uihlist $uiPad:S [
                nodebug [
                    if (!= $arg2 $[uiResource:@arg4.val]) [
                        [uiResource:@arg4.mul] = 0.95
                        [uiResource:@arg4.ts]  = $getmillis
                        [uiResource:@arg4.val] = $arg2
                    ]
                    if (> $getmillis (+ $[uiResource:@arg4.ts] 120)) [
                        [uiResource:@arg4.mul] = 0.8
                    ]
                ]
                uifill $y $y [ push y (*f $y $[uiResource:@arg4.mul]) [
                    uiFastImg (intmul $arg6) "hud/" $arg4 1 $y
                    uiFastImg             "" "hud/" $arg4 2 $y
                ] ]
                uiclip 0 $y [ uioffset 0 (*f $y -0.20) [
                    uiFancyText "wide" $arg2 (*f $arg1 2.500) $arg6
                ] ]
            ] ; uialign- -1
        ] ; uiclamp-x
        .bar:resource $arg1 $arg2 $arg3 $arg5 $arg6 $arg7
    ] ; uialign- 1
]

// use: 1:WIDTH  2:VALUE  3:MAXVAL  4:COL1  5:COL2  6:LOW
.bar:resource = [
    uigroup [
        local w.lt w.rt
        if $arg2 [
            w.lt = (%f (min $arg2 $arg3) $arg3 $arg1)
            if (|| (> $arg2 $arg6) ((&& (< (mod $getmillis 300) 150) (<= $arg2 $arg6)))) [
                uihgradient (|A 0xC0 $arg4) (|A 0xE0 $arg5) $w.lt $y [
                    uiDrawSpriteTiled "hud" "<fade:0.15>" "data/interface/debug/causticmap" $uiPad:DM $w.lt $y 128 0 50 $uiSpriteAnim
                    uiFastImgStretched "" "shadow3" "" "" [ uiclamp.e ] (minf $w.lt $uiPad:S)
                ] ; uialign- -1
            ]
        ]
        if (< $arg2 $arg3) [
            w.rt = (-f $arg1 $w.lt)
            uicolor (|A 0x30 $arg4) $w.rt $y [
                uiFastImgTiled "<mad:0/0/0>" "ui/" "ui_bg2" "" [ uiclamp.e ]
                uiFastImgStretched "" "shadow3" "" "" [ uiclamp.e ] (minf $w.rt $uiPad:S)
            ] ; uialign- 1
        ]
        uioutline (INT:TRANS $arg4 % 400) $arg1 $y
    ]
]

// Per-attack obituary for self-frags: normal kill, gib kill.
.attackSelfObituary = [
    // Melee
    [ [] [] ]
    [ [] [] ]

    // Scattergun
    [ [] [] ]
    [ [] [] ]

    // Submachine gun
    [ [] [] ]
    [ [] [] ]

    // Pulse Rifle
    [ [was cooked to a crisp] [turned into a gas] ]
    [                      []                  [] ]

    // Rocket Launcher
    [ [was sent on a one-way rocket ride] [exploded] ]
    [                           [blew up] [exploded] ]

    // Railgun
    [ [died] [died] ]
    [ [died] [died] ]

    // Disruptor Grenades
    [ [blew up]  [got overcharged] ]
    [ [blew up] [turned into dust] ]

    // Quantum Pistol
    [               [died]     [died] ]
    [ [toasted themselves]     [died] ]
    [       [was atomised] [imploded] ]

    // Instagun
    [ [died] [died] ]

    // Zombie
    [ [got infected] [died] ]
]

ANNOUNCEMENT_FIRST = 1

.killStreaks = [
    [drew first blood]
    [is on a killing spree]
    [is on a savage killing spree]
    [is on an unstoppable killing spree]
    [is on a legendary killing spree]
]

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  KILLFEED INTERNALS                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

// 1:          2:           3:      4:    5:        6:         7:     8:          9:     10:
// actor.name, target.name, type,   crit, actor.cn, target.cn, attack death.state weapon medal
.killfeed.row = [
    local msElapsed animIntro animOutro animProgress weaponIcon
    msElapsed = (- $getmillis $timestamp)

    if (< $msElapsed $uiKillDecay) [
        msElapsed = (%f $msElapsed  $uiKillDecay)
        animIntro = (%f $uiKillFade $uiKillDecay)
        animOutro = (-f 1 $animIntro)
        cond [<f $msElapsed $animIntro] [
            animProgress = (%f $msElapsed $animIntro)
        ] [>f $msElapsed $animOutro] [
            animProgress = (-f 1 (%f (-f $msElapsed $animOutro) $animIntro))
        ] [ animProgress = 1 ]

        local actorName victimName type crit actor victim attack death Weapon medal
        actorName = $arg1
        victimName = $arg2
        type = $arg3
        crit = $arg4
        actor = $arg5
        victim = $arg6
        attack = $arg7
        death = $arg8
        Weapon = $arg9
        medal = $arg10
        local actorColor victimColor 
        if (m_teammode $getmode) [
            actorColor = (getclientteamtextcolor $actor)
            victimColor = (getclientteamtextcolor $victim)
        ] [
            actorColor = (INT:TRANS (getclientcolor $actor) * 2)
            victimColor = (INT:TRANS (getclientcolor $victim) * 2)
        ]

        if (<f $msElapsed $animOutro) [
            uifill 0 (*f (-f 1 $animProgress) $uiPad:4XL)
            uiFastImgStretched (? (|| [scoreboardhighlight $arg5] [scoreboardhighlight $arg6]) [<mad:0.6/0.6/0.6>] [<mad:.18/.18/.18>]) "hud/" "row" "" [
                uialign 1 -2
                uispace $uiPad:M 0 [
                    uifill 0 $uiPad:5XL
                    uihlist 0 [
                        case $type 1 [
                            uifontcolorblendtext "wide.ol" $victimName $victimColor 1.0 0.55
                            uifontcolorblendtext "wide.ol" " was stopped by a monster" $c_white 1.0 0.55
                            uifill $uiPad:L
                            uiShadowedImg "" "hud/" "monster" "" $uiPad:3XL
                        ] 2 [
                            uifontcolorblendtext "wide.ol" $victimName $victimColor 1.0 0.5
                            uifontcolorblendtext "wide.ol" " was assassinated" $c_white 1.0 0.55
                            uifill $uiPad:L
                            uiShadowedImg "" "hud/" "skull" "" $uiPad:3XL
                        ] 3 [
                            uifontcolorblendtext "wide.ol" $actorName $actorColor 1.0 0.55
                            if (<= $attack -1) [ // No valid attack, then it must be an environmental/command kill.
                                uifontcolorblendtext "wide.ol" " committed seppuku" $c_white 1.0 0.55
                            ] [
                                local isGibbed deathInfo
                                isGibbed = (= $death $DEATH_GIB)
                                deathInfo = (+s " " (at $.attackSelfObituary $attack (? (! $isGibbed) 0 1)))
                                uifontcolorblendtext "wide.ol" $deathInfo $c_white 1.0 0.55
                            ]
                            uifill $uiPad:L
                            uiShadowedImg "" "hud/" "skull" "" $uiPad:3XL
                        ] 4 [
                            uifontcolorblendtext "wide.ol" $actorName $actorColor 1.0 0.5
                            uifontcolorblendtext "wide.ol" (+s " " (at $.killStreaks (- $medal $ANNOUNCEMENT_FIRST))) $c_white 1.0 0.55
                            uifill $uiPad:L
                            local image
                            if (= $medal $ANNOUNCEMENT_FIRST) [
                                image = "drop"
                            ] [
                                image = "flame"
                            ]
                            uiShadowedImg "" "hud/" $image "" $uiPad:3XL
                        ] 0 [
                            weaponIcon = (at [
                                "melee"   "scattergun" "smg"    "pulse"   "rocket"
                                "railgun" "grenade"    "pistol" "railgun" "zombie"
                                "melee"
                            ] (+ $Weapon 1))
                            uifontcolorblendtext "wide.ol" $actorName $actorColor 1.0 0.55
                            uifill $uiPad:L
                            uiShadowedImg "" "hud/" $weaponIcon "_raw" (*f (gettexaspectwh (
                                +s "data/interface/hud/" $weaponIcon "_raw.png"
                            )) $uiPad:3XL) $uiPad:3XL
                            if (& $crit (<< 1 0)) [
                                uifill $uiPad:O5
                                uiShadowedImg "" "hud/" "crit_scope" "" $uiPad:4XL
                            ]
                            if (& $crit (<< 1 1)) [
                                uifill $uiPad:O5
                                uiShadowedImg "" "hud/" "crit_headshot" "" $uiPad:4XL
                            ]
                            if (& $crit (<< 1 2)) [
                                uifill $uiPad:O5
                                uiShadowedImg "" "hud/" "crit_explosion" "" $uiPad:4XL
                            ]
                            uifill $uiPad:L
                            uifontcolorblendtext "wide.ol" $victimName $victimColor 1.0 0.55
                        ]
                    ]
                ]
            ] $uiPad:4XL
            uifill 0 $uiPad:M
        ] [ uifill 0 (*f $animProgress (+f $uiPad:4XL $uiPad:M)) ]
    ]
]

///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////

newui "scoreboard" [
    uiallowinput 0
    refreshscoreboard
] [
    uiSetBgBlur 1
    .sb_offset = 3
] [ uiSetBgBlur -1 ]

.scoreboard.update = [
    local bit cc

    if $getmode                 [ bit = (^ $bit 0x01) ; ++ cc ] // score
    if $showkills               [ bit = (^ $bit 0x02) ; ++ cc ] // kills
    if $showdeaths              [ bit = (^ $bit 0x04) ; ++ cc ] // deaths
    if $scoreboardmultiplayer [
        if $showpj              [ bit = (^ $bit 0x08) ; ++ cc ] // packet jump
        if $showping            [ bit = (^ $bit 0x10) ; ++ cc ] // ping
    ]
    if $showclientnum           [ bit = (^ $bit 0x20) ; ++ cc ] // client num

    if (|| $arg1 [!= (getalias .sb_bitmask) $bit] [!= (getalias .sb_modemask) $bit]) [
        .sb_bitmask  = $bit
        .sb_modemask = $getmode

        local .sb_namewidth .sb_cellwidth .sb_rowwidth
        local .scoreboard.cells .scoreboard.icons
        local .scoreboard.header .scoreboard.row
        local .scoreboard.shelf.top3 .scoreboard.shelf.rest

        .sb_namewidth = $uiPad:UXS
        .sb_cellwidth = (+f $uiPad:DSS $uiPad:M)
        .sb_rowwidth  = (+f $.sb_namewidth (*f $.sb_cellwidth $cc))

        loop n 6 [ if (& $bit (<< 1 $n)) [
            append .scoreboard.cells [[uifill @@[.sb_cellwidth] 0 [@@@(at [
                [uiFancyText "" (min   (getclientscore  $cn)      9999) 0.58]
                [uiFancyText "" (clamp (getclientfrags  $cn) -999 9999) 0.58]
                [uiFancyText "" (min   (getclientdeaths $cn)      9999) 0.58]
                [if (scoreboardpj   $cn) [uiFancyText "" (min (scoreboardpj   $cn) 9999) 0.58] [uiFancyText "" "--" 0.58 $c_gray]]
                [if (scoreboardping $cn) [uiFancyText "" (min (scoreboardping $cn) 9999) 0.58] [uiFancyText "" "--" 0.58 $c_gray]]
                [uiFancyText "" $cn 0.58 (at [@@@(concat $c_white $c_green $c_orange $c_magenta)] (getclientprivilege $cn))]
            ] $n)]]]
            append .scoreboard.icons (at [
                @@@(at "0 star star flag star star star star" $getmode)
                "frag" "death" "pj" "ping" "cn"
            ] $n)
        ] ]

        // 1:TEAM
        .scoreboard.header = [
            local bflip
            bflip = (= $arg1 2) // right-to-left flip bit
            result [
                uifill @[.sb_rowwidth] $uiPad:DM [
                    uispace 0 $uiPad:S [
                        uiFastImg <reorient:@@@bflip> "hud/" "scorebg"
                        uiclamp-e
                    ] ; uiclamp-e
                    uispace $uiPad:M $uiPad:M- [
                        uiFancyText "wide" @@@(
                            at "FFA Aesir Vanir" $arg1
                        ) 1.4 @@@(INT:TRANS (
                            at [0x40D060 0x4060D0 0xD04040] $arg1
                        ) % 80)
                    ] ; uialign- @@(? $bflip 1 -1) -1
                    uispace $uiPad:O5 (*f $uiPad:O5 2.9) [
                        uiFancyText "wide" (getteamscore @@@arg1) 1 @@@(
                            at [0x40D060 0x4060D0 0xD04040] $arg1
                        )
                    ] ; uialign- @@(? $bflip -1 1) -1
                    if (numscoreboard @@arg1) [
                        uihlist 0 [
                            @@@@(? $bflip [
                                uialign -1 1
                                @(looplistconcatrev x $.scoreboard.icons [ result [
                                    uifill @@[.sb_cellwidth] @@(*f $uiPad:O4 7) [
                                        uiShadowedImg "" "hud/" @@x "" $uiPad:3XL
                                    ]
                                ] ])
                            ] [
                                uialign 1 1
                                @(looplistconcat x $.scoreboard.icons [ result [
                                    uifill @@[.sb_cellwidth] @@(*f $uiPad:O4 7) [
                                        uiShadowedImg "" "hud/" @@x "" $uiPad:3XL
                                    ]
                                ] ])
                            ])
                        ]
                    ]
                ]
            ]
        ]

        // 1:TEAM  2:[CHILDREN]  3:SIDE-EXTRAS
        .scoreboard.row = [
            local bflip
            bflip = (= $arg1 2) // right-to-left flip bit
            result [
                uiFastImgStretched @(? (> $arg1 -1) [(
                    intmul (? (scoreboardhighlight $cn) 0xA0A0A0 (at [
                        [0x303030 0x282828] // ffa
                        [0x1C2E70 0x122466] // aesir
                        [0x701C1C 0x661212] // vanir
                    ] @arg1 (& $n 1)))
                )] [ @(intmul 0x303840) ]) "hud/" "row" "" [
                    uifill @@(? $arg3 $arg3 $.sb_rowwidth)
                    @@(? $arg2 [ uigroup [ uiclamp.e ; @@arg2 ] ])
                    if (scoreboardhighlight $cn) [
                        uispace $uiPad:O3 $uiPad:O3 [
                            uiShadowedImg <reorient:@@@@bflip> "hud/" "edge" "" $uiPad:M
                        ] ; uialign- @@@(? $bflip 1 -1) -1
                    ]
                    @@(? $bflip [
                        uihlist 0 [
                            @@(looplistconcatrev x $.scoreboard.cells [ result [ @x; ] ])
                            if (isai $cn) [
                                uiFastImg "" "hud/" "bot" "" $uiPad:3XL
                            ] [
                                uiFastImg "" "countryflag/" (strlower (getclientcountrycode $cn)) "" (*f 1.5 $uiPad:4XL) $uiPad:4XL
                            ]
                        ] ; uiclamp-y ; uialign- -1
                        uispace $uiPad:M 0 [ uihlist $uiPad:O5 [
                            if (isprivileged $cn) [
                                uiShadowedImg (intmul (getclientprivilegecolor $cn)) "hud/" "crown" "" $uiPad:4XL
                            ]
                            uiFancyText "" (getclientname $cn) 0.6 (scoreboardstatus $cn)
                        ] ] ; uialign- 1
                    ] [
                        uispace $uiPad:M 0 [ uihlist $uiPad:O5 [
                            if (isspectator $cn) [
                                uifontcolortext "modernpics" "E" $c_white 0.6
                            ]
                            uiFancyText "" (getclientname $cn) 0.6 (scoreboardstatus $cn)
                            if (isprivileged $cn) [
                                uiShadowedImg (intmul (getclientprivilegecolor $cn)) "hud/" "crown" "" $uiPad:4XL
                            ]
                        ] ] ; uialign- -1
                        @(? (> $arg1 -1) [
                            uihlist 0 [
                                if (isai $cn) [
                                    uiFastImg "" "hud/" "bot" "" $uiPad:3XL
                                ] [
                                    uiFastImg "" "countryflag/" (strlower (getclientcountrycode $cn)) "" (*f 1.5 $uiPad:4XL) $uiPad:4XL
                                ]
                                @@(looplistconcat x $.scoreboard.cells [ result [ @x; ] ])
                            ] ; uiclamp-y ; uialign- 1
                        ] [
                            @(? $showclientnum [
                                uihlist 0 [
                                    @@(at $.scoreboard.cells -1)
                                    @(? (!=s "" (getclientcountrycode $cn)) [
                                        uiFastImg "" "countryflags/" (strlower (getclientcountrycode $cn)) "" (*f 1.5 $uiPad:4XL) $uiPad:4XL
                                        uispace $uiPad:O2 0 []
                                    ])
                                ]
                            ] [
                                uihlist 0 [
                                    uiFastImg "" "countryflag/" (strlower (getclientcountrycode $cn)) "" (*f 1.5 $uiPad:4XL) $uiPad:4XL
                                    uispace $uiPad:O2 0 []
                                ]
                            ])
                            uiclamp-y ; uialign- 1
                        ])
                    ])
                ] $uiPad:5XL
                ++ n
            ]
        ]

        if (m_teammode $getmode) [
            .scoreboard.aesir = [
                local n
                uivlist 0 [
                    @@(.scoreboard.header 1)
                    loopscoreboard cn 1 [ @@@(.scoreboard.row 1) ]
                ] ; uialign- -2 -1
            ]
            .scoreboard.vanir = [
                local n
                uivlist 0 [
                    @@(.scoreboard.header 2)
                    loopscoreboard cn 2 [ @@@(.scoreboard.row 2) ]
                ] ; uialign- -2 -1
            ]
            .scoreboard.ctf = [
                uigroup [
                    uioffset 0 $uiPad:O5 [
                        uioffset $uiPad:2XL- $uiPad:2XL- [ uiFancyText "wide" "V" 1 ]
                        uioffset $uiPad:2XL  $uiPad:2XL  [ uiFancyText "wide" "S" 1 ]
                    ] ; uialign- 0 -1
                    uihlist $uiPad:DM [
                        .scoreboard.aesir
                        .scoreboard.vanir
                        uialign* -2 -1
                    ]
                ]
            ]
        ]

        .scoreboard.spec = [
            uigrid 3 $uiPad:L 0 [
                loopscoreboard cn -1 [
                    @@@(.scoreboard.row -1 [] (+f $.sb_namewidth (
                        ? $showclientnum $.sb_cellwidth 0
                    )))
                ]
            ]
        ]

        .scoreboard.shelf.top3 = [
            uioffset $uiPad:5XL- 0 [
                uiFastImg (fade 0.5) "hud/" "shelf" "" $uiPad:5XL
                push n (at [<mad:2/1.8/0> <mad:1.8/1.8/1.8> <mad:1.8/1.5/0>] $n) [
                    uiFastImg $n "hud/" "glow"  "" $uiPad:5XL
                ]
                push n (+ $n 1) [
                    uifontcolortext "def.ol" $n $c_white 0.55
                ]
            ] ; uialign- -1
        ]
        .scoreboard.shelf.rest = [
            arg1 = (= $cn $getclientnum)
            uioffset $uiPad:5XL- 0 [
                uiFastImg (fade (? $arg1 0.5 0.25)) "hud/" "shelf" "" $uiPad:5XL
                uiFastImg <mad:.18/.18/.18> "hud/" "glow" "" $uiPad:5XL
                push n (+ $n 1) [
                    uifontcolortext "def.ol" $n (|A! (? $arg1 0xB0 0x60)) 0.55
                ]
            ] ; uialign- -1
        ]

        .scoreboard.ffa = [
            local n x
            uigroup [
                uivlist 0 [
                    @@@(.scoreboard.header 0)
                    loopscoreboard cn 0 [
                        if (< $n 3) [
                            if (= $cn $getclientnum) [ x = 1 ]
                            @@@@@(.scoreboard.row 0 $.scoreboard.shelf.top3)
                            if (= $n 3) [ uifill 0 $uiPad:M ]
                        ] [
                            if (= $cn $getclientnum) [
                                if (> $n (+ $.sb_offset 10)) [ uifill 0 $uiPad:M ]
                                @@@@@@(.scoreboard.row 0 $.scoreboard.shelf.rest)
                                if (< $n (+ $.sb_offset 0)) [ uifill 0 $uiPad:M ]
                            ] [
                                if (&& [<= $n (+ $.sb_offset 10 $x $isspectator)] [>= $n $.sb_offset]) [
                                    @@@@@@@(.scoreboard.row 0 $.scoreboard.shelf.rest)
                                ] [ ++ n ]
                            ]
                        ]
                    ]
                ]
                uioffset $uiPad:2XL 0 [
                    uialign 1
                    // XXX need a way to scan for flag carrier
                ]
            ]
        ]
    ]
]

///////////////////////////////////////////////////////////////////////////////
//  MISCELLANEOUS FUNCTIONS                                                  //
///////////////////////////////////////////////////////////////////////////////

.updateKillfeed = [
    local actor victim
    actor  = $getkillfeedactor
    victim = $getkillfeedvictim
    // add new message to queue, FILO approach
    listsplice= fragQueue (format "%1 [%3] %2" $uiKillNum $getmillis (
        concat (escape (getclientname $actor)) (escape (getclientname $victim)) (
            concat $getkillfeedtype $getkillfeedcrit $actor $victim
        ) $getkillfeedattack $getkillfeeddeath $getkillfeedweapon $getkillfeedmedal
    )) 0 0
    // trip flag of "last" entry to mark it for fade
    if (> (listlen $fragQueue) $uiKillCount) [
        listsplice= fragQueue (
            - $getmillis (- $uiKillDecay $uiKillFade)
        ) (+ $uiKillCount 2) 1
    ]
    // remove the newest entry after a set time
    sleep $uiKillDecay [
        listsplice= fragQueue "" (indexof $fragQueue @uiKillNum) 3
    ]
    ++ uiKillNum
]

.cleanKillfeed = [
    set uiKillNum 0
    set fragQueue ""
]

.manageDeathEffects = [
    if (|| $firstpersondeath [m_story (getmode)]) [
        addpostfx "bw"
    ]
]

.removeDeathEffects = [
    if (findpostfx "bw") [
        removepostfx "bw"
    ]
]

.manageSpawnEffects = [
    addpostfx "invert"
    sleep 125 [
        if (findpostfx "invert") [
            removepostfx "invert"
        ]
    ]
]

.resetHudInfo = [
    .lastKill = 0
    .lastDeath = 0
    .lastMedal = 0
    .lastAnnouncement = 0
    .lastIntermission = 0
]

.checkEditHud = [
    if $editing [
        if $showhud [
            showui "edithud"
            if (uivisible "fullconsole") [] [
                showui "editvars"
            ]
        ]
    ] [
        hideui "edithud"
        hideui "editvars"
        hideui "material"
        hideui "texture_browser"
        hideui "mapmodel_browser"
        hideui "geo_prefab"
    ]
]
