
///////////////////////////////////////////////////////////////////////////////
//  GAMEHUD UI -- MAKE CHANGES AT THY OWN PERIL                              //
///////////////////////////////////////////////////////////////////////////////

newui "hud" [
	uiclamp.e
	if (|| $mainmenu $uiHideHUD) [ uiallowinput 0 ] [

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  EDITHUD DISPLAY GROUP                                                                                  //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

		if $editing [
			uieschide 0
			uispace $uiPad:M $uiPad:M [
				uihlist $uiPad:M [
					if (iskeyheld "LALT") [
						uiallowinput 1
						uiHudCell $uiPad:DM $uiPad:DXL [
							uiFastImg "" "ui/" "config" "" $uiPad:SXL
						]
					] [
						uiallowinput 0
						uiHudCell $uiPad:DM $uiPad:DXL [
							uivlist 0 [
								uiFastImg "" "ui/" "speed" "" $uiPad:6XL
								uitext $floatspeed 0.6
								uifill 0 $uiPad:S
								uigroup [
									uiFastImg "" "ui/" "grid" "" $uiPad:6XL
									uitext $gridpower 0.5
									uialign- 1 1
								]
							]
						]
					]
				]
			] ; uialign- -1 1

		] [

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  MATCH TIMER DISPLAY GROUP                                                                               //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

			uiallowinput 0
			if (m_timed $getmode) [ // var scoreboardtime otherwise
				uiFastImgStretched (fade 0.5) "hud/" "shelf" "" [
					uialign 0 -1
					uiFastImg <mad:0/0/0> "hud/" "glow" "" $uiPad:D2XL $uiPad:DM
					if $intermission [ uifontcolortext "mono" $timeremaining $c_orange 1.2 ] [
						uihlist $uiPad:O3 [
							uiFancyText "mono"       (div $timeremaining 60)        1.2
							uiFancyText "mono" ":"                                  1.2
							uiFancyText "mono" (pad0 (mod $timeremaining 60) 2 "0") 1.2
						]
					]
				] $uiPad:5XL
			]

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  SCOREBOARD DISPLAY GROUP                                                                                //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

			if (iskeyheld "TAB") [
				if (uivisible "scoreboard") [] [ showui "scoreboard" ]
				uivlist $uiPad:5XL [
					uialign 0 -1
					uifill 0 $uiPad:DM
					if (m_teammode $getmode) [
						.scoreboard.ctf
					] [ .scoreboard.ffa ]
					.scoreboard.spec
				]

				uispace $uiPad:L $uiPad:L [
					uivlist 0 [
						uiFancyText "wide" (getmodeprettyname $getmode) 1.2
						uifill 0 $uiPad:DM [ uivlist 0 [
							if $scoreboardmap [
								uiFancyText "wide" "- ON -" 0.8 $c_gray
								uiFancyText "wide" $scoreboardmap 1 $c_green
							]
						] ]
					]
					if (isconnected 1 0) [
						uigroup [
							uicolortext $scoreboardservinfo (|A! 0x48) 0.7
						] ; uialign- -1 1
					]
				] ; uiclamp-x ; uialign- 0 1
			] [
				if (uivisible "scoreboard") [ hideui "scoreboard" ]
				local x cn
				if $spectating [
					cn = $getfollow
					uivlist 0 [ // spectating display group
						uispace $uiPad:3XL 0 [
							uiFancyText "wide" "SPECTATING" 1.1 $c_yellow
						]
						uispace $uiPad:3XL 0 [
							uiFancyText "" (+s (getclientcolorname $cn) " ") 0.95
						]
						uialign* 1
						uifill 0 $uiPad:DXL
					] ; uialign- 1 1
				] [ cn = $getclientnum ]

				x = (maxf 0 (-f $uiaspect $uiWideHUD))
				if (>f $x 0) [ x = (*f $x 0.5) ]

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  KILLFEED DISPLAY GROUP                                                                                  //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

				uispace $uiPad:L $uiPad:L [
					uivlist 0 [
						local i timestamp
						looplistrev n $fragQueue [
							case $i 0 [ timestamp = $n ] 1 [
								do [ .killfeed.row @n ]
							] ; i = (mod (+ $i 1) 3)
						]
					]
				] ; uialign- 1 -1

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  GAMEHUD DISPLAY GROUP                                                                                   //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

				uispace (+f $uiPad:3XL $x) $uiPad:3XL [
					//uioutline (rnd 0xFFFFFF) ; uiclamp-e // debug boundary outline

					if (|| $uiBlurring [< $cn 0] [isdead $cn] [&& $thirdperson [> $getfollow -1]]) [
						if (isSpriteLive "hud") [ disableSprite "hud" ]
					] [
						uihlist 0 [
							// resource (shield/health) display group
							uivlist $uiPad:3XL [
								if (> $getclientshield 0) [
									.bar.shield $uiPad:D6XL (getclientshield $cn) 200 shield
								]
								.bar.health $uiPad:DSXL (getclienthealth $cn) 100 health
							] ; uialign- 0 1
							// powerup and duration display group
							pushif cp (getclientpowerup $cn) [
								uifill $uiPad:2XL
								uiFastImgStretched (fade 0.5) "hud/" "shelf" "" [
									uialign -2 1
									uispace $uiPad:M $uiPad:S [
										uivlist $uiPad:L [
											uiFastImg "" "hud/" (at [
												doubledmg haste armor infammo
												agility invulnerability
											] (- $cp 1)) "" (-f $uiPad:DXL $uiPad:M)
											.bar.powerup $uiPad:DXL (getclientpowerupmillis $cn) (? (= $cp 6) 15000 30000)
										]
									]
								] $uiPad:5XL
							]
						] ; uialign- -1 1
						if 0 [ // flag hold group // XXX incomplete due to lack of detect func
							uiFastImgStretched (fade 0.5) "hud/" "shelf" "" [
								uialign 1
								uispace $uiPad:M $uiPad:M [
									pushif n (getclientteam $cn) [
										n = (? (- $n 1) "blue" "red")
										uiFastImg "" "hud/" "flag_" $n $uiPad:DXL
									]
								]
							] $uiPad:5XL
						]
						if 1 [
							uihlist $uiPad:L [ // temp ammo group
								uiFastImg "" "hud/" (at [
									shotgun smg pulse rocket railgun grenade pistol
								] (getclientweapon $cn)) "" $uiPad:DM
								uiclip 0 $uiPad:DM [
									uifonttext "wide" "    " 2.08
									uiFancyText "wide" (getclientammo $cn) 2
									uialign- -1
								]
							] ; uialign- 1 1
						] [
							uispace $uiPad:L 0 [ // to-be-eventually ammo group
								push n (at [shotgun smg pulse rocket railgun grenade pistol] (getclientweapon $num)) [
									uiShadowedImg "" "hud/" $n "_raw" (*f (gettexaspectwh (
										+s "data/interface/hud/" $n "_raw.png"
									)) $uiPad:DS) $uiPad:DS 2.5
								]
								uiFancyText "wide" (getclientammo $cn) 2
								uialign- 1 1
							] ; uialign- 1 1
						]
					]
				] ; uiclamp-x ; uialign- -1 1
			]
		]
	]
] [
	uiBitsHUD = 0
	showui "hud_"
]


// temporary measure to set Y pos of chat input
newui "hud_" [
	uiallowinput 0
	uiabovehud
	uialign -1 1
	uifill 0 0.14
]


// XXX MOVE TO A DIFFERENT FILE LATER
// linear:HSV  INT1  INT2  X-TIME  WAVELENGTH
linear:HSV = [
	local r1 g1 b1 r2 g2 b2
	INT>HSV $arg1 [ h1 = $h ; s1 = $s ; v1 = $v ]
	INT>HSV $arg2 [ h2 = $h ; s2 = $s ; v2 = $v ]
	HSV>RGB (
		+f $h1 (*f (/f (-f $h2 $h1) $arg4) $arg3) ) (
		+f $s1 (*f (/f (-f $s2 $s1) $arg4) $arg3) ) (
		+f $v1 (*f (/f (-f $v2 $v1) $arg4) $arg3)
	) [ RGB>INT $r $g $b ]
]

.bar.health = [
	local y ; y = (*f $arg1 0.1)
	if (> $arg2 $arg3) [
		arg5 = 0x80F0FF ; arg6 = 0xD0F0FF // supercharged
	] [ push n (%f $arg2 $arg3) [         // normal health
		if (<f $n 0.5) [ n = (*f 16 (pow $n 5)) ] [
			n = (-f 1 (/f (pow (+f (*f -2 $n) 2) 4) 2))
		]
		// smoothstep lerp approach?
		// uncomment lines below and comment lines above to test
		//n = (*f $n $n (-f 3 (*f $n 2)))
		//clampf= n 0 1
		arg5 = (linear:HSV 0xFF3000 0x008060 $n 1)
		arg6 = (linear:HSV 0xF01000 0x00F080 $n 1)
	] ]
	.bar:toparea $arg1 $arg2 $arg3 $arg4 $arg5 $arg6 [
		pushif over (max 0 (- $arg2 $arg3)) [
			uihlist $uiPad:2XL- [
				push y (*f $y 0.75) [ loop i 3 [
					if (> (ceil (/f $over 50)) $i) [
						i = (sinBounce $uiPad:XL (* $i 100) 400)
						uioffset 0 (+f (minf 0 $i) $uiPad:S) [
							uiShadowedImg (mad $arg6) "hud/" "shield2" "" $y
						]
					]
				] ]
			] ; uiclamp-y ; uialign- 1
		]
	]
]

.bar.shield = [
	local y ; y = (*f $arg1 0.1)
	.bar:toparea $arg1 $arg2 $arg3 $arg4 0xE06000 0xFFD000
]

.bar.powerup = [
	local y ; y = (*f $arg1 0.1)
	.bar:resource $arg1 $arg2 $arg3 0x80D0FF 0xD0F0FF
]

// use: 1:WIDTH  2:VALUE  3:MAXVAL  4:IMG/ID  5:COL1  6:COL2  7:[CHILDREN]
.bar:toparea = [
	uivlist 0 [
		uigroup [
			do $arg7
			uihlist $uiPad:S [
				nodebug [
					if (!= $arg2 $[uiResource:@arg4.val]) [
						[uiResource:@arg4.mul] = 0.95
						[uiResource:@arg4.ts]  = $getmillis
						[uiResource:@arg4.val] = $arg2
					]
					if (> $getmillis (+ $[uiResource:@arg4.ts] 120)) [
						[uiResource:@arg4.mul] = 0.8
					]
				]
				uifill $y $y [ push y (*f $y $[uiResource:@arg4.mul]) [
					uiShadowedImg (intmul $arg6) "hud/" $arg4 1 $y
					uiShadowedImg             "" "hud/" $arg4 2 $y
				] ]
				uiclip 0 $y [ uioffset 0 (*f $y -0.15) [
					uiFancyText "wide" $arg2 (*f $arg1 3.125) $arg6
				] ]
			] ; uialign- -1
		] ; uiclamp-x
		.bar:resource $arg1 $arg2 $arg3 $arg5 $arg6
	] ; uialign- 1
]

// use: 1:WIDTH  2:VALUE  3:MAXVAL  4:COL1  5:COL2
.bar:resource = [
	uigroup [
		local w.lt w.rt
		if $arg2 [
			w.lt = (%f (min $arg2 $arg3) $arg3 $arg1)
			uihgradient (|A 0xC0 $arg4) (|A 0xE0 $arg5) $w.lt $y [
				uiDrawSpriteTiled "hud" "<fade:0.15>" "data/interface/debug/causticmap" $uiPad:DM $w.lt $y 128 0 50 1
				uiFastImgStretched "" "shadow3" "" "" [ uiclamp.e ] (minf $w.lt $uiPad:S)
			] ; uialign- -1
		]
		if (< $arg2 $arg3) [
			w.rt = (-f $arg1 $w.lt)
			uicolor (|A 0x30 $arg4) $w.rt $y [
				uiFastImgTiled "<mad:0/0/0>" "ui/" "ui_bg2" "" [ uiclamp.e ]
				uiFastImgStretched "" "shadow3" "" "" [ uiclamp.e ] (minf $w.rt $uiPad:S)
			] ; uialign- 1
		]
		uioutline (INT:TRANS $arg4 % 400) $arg1 $y
	]
]




//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  KILLFEED INTERNALS                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

.killfeed.row = [
	local msElapsed animIntro animOutro animProgress weaponIcon
	msElapsed = (- $getmillis $timestamp)

	if (< $msElapsed $uiKillDecay) [
		msElapsed = (%f $msElapsed  $uiKillDecay)
		animIntro = (%f $uiKillFade $uiKillDecay)
		animOutro = (-f 1 $animIntro)

		cond [<f $msElapsed $animIntro] [
			animProgress = (%f $msElapsed $animIntro)
		] [>f $msElapsed $animOutro] [
			animProgress = (-f 1 (%f (-f $msElapsed $animOutro) $animIntro))
		] [ animProgress = 1 ]

		if (<f $msElapsed $animOutro) [
			uifill 0 (*f (-f 1 $animProgress) $uiPad:5XL)
			uiFastImgStretched <mad:.18/.18/.18> "hud/" "row" "" [
				uialign 1 -2
				uispace $uiPad:M 0 [
					uifill 0 $uiPad:5XL
					uihlist 0 [
						if (> $arg5 127) [
							uiShadowedImg "" "hud/" "bot" "" $uiPad:2XL
							uifill $uiPad:O3
						]
						uiFancyText "" $arg1 0.65 (INT:TRANS $arg7 * 2)
						if (|| [= $arg3 -2] [= $arg5 $arg6]) [
							uiFancyText "" " committed sudoku!" 0.65
							uifill $uiPad:XL
							uiShadowedImg "" "hud/" "death" "" $uiPad:3XL
						] [
							case $arg3 -1 [ weaponIcon = "melee" ] -3 [ weaponIcon = "grenade" ] () [
								weaponIcon = (at "shotgun smg pulse rocket railgun grenade pistol" $arg3)
							]
							uifill $uiPad:XL
							uiShadowedImg "" "hud/" $weaponIcon "_raw" (*f (gettexaspectwh (
								+s "data/interface/hud/" $weaponIcon "_raw.png"
							)) $uiPad:3XL) $uiPad:3XL
							if $arg4 [
								uifill $uiPad:S
								uiShadowedImg "" "hud/" "crit" "" $uiPad:3XL
							]
							uifill $uiPad:XL
							if (> $arg6 127) [
								uiShadowedImg "" "hud/" "bot" "" $uiPad:2XL
								uifill $uiPad:O3
							]
							uiFancyText "" $arg2 0.65 (INT:TRANS $arg8 * 2)
						]
					]
				]
			] $uiPad:5XL
			uifill 0 $uiPad:L
		] [ uifill 0 (*f $animProgress (+f $uiPad:5XL $uiPad:L)) ]
	]
]

///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////

newui "scoreboard" [
	uiallowinput 0
	refreshscoreboard
] [
	uiSetBgBlur 1
	.sb_offset = 3
] [ uiSetBgBlur -1 ]

.scoreboard.update = [
	local bit cc

	if $getmode                 [ bit = (^ $bit 0x01) ; ++ cc ] // score
	if $showkills               [ bit = (^ $bit 0x02) ; ++ cc ] // frags
	if $showdeaths              [ bit = (^ $bit 0x04) ; ++ cc ] // deaths
	if $scoreboardmultiplayer [
		if $showpj              [ bit = (^ $bit 0x08) ; ++ cc ] // packet jump
		if $showping            [ bit = (^ $bit 0x10) ; ++ cc ] // ping
	]

	if (|| $arg1 [!= (getalias .sb_bitmask) $bit] [!= (getalias .sb_modemask) $bit]) [
		.sb_bitmask  = $bit
		.sb_modemask = $getmode

		local .sb_namewidth .sb_cellwidth .sb_rowwidth
		local .scoreboard.cells .scoreboard.icons
		local .scoreboard.header .scoreboard.row
		local .scoreboard.shelf.top3 .scoreboard.shelf.rest

		.sb_namewidth = $uiPad:UXS
		.sb_cellwidth = (+f $uiPad:DSS $uiPad:M)
		.sb_rowwidth  = (+f $.sb_namewidth (*f $.sb_cellwidth $cc))

		loop n 5 [ if (& $bit (<< 1 $n)) [
			append .scoreboard.cells [[uifill @@[.sb_cellwidth] 0 [@@@(at [
				[uiFancyText "" (min   (getclientscore  $cn)      9999) 0.58]
				[uiFancyText "" (clamp (getclientfrags  $cn) -999 9999) 0.58]
				[uiFancyText "" (min   (getclientdeaths $cn)      9999) 0.58]
				[if (scoreboardpj $cn)   [uiFancyText "" (min (scoreboardpj   $cn) 9999) 0.58] [uiFancyText "" "--" 0.58 $c_gray]]
				[if (scoreboardping $cn) [uiFancyText "" (min (scoreboardping $cn) 9999) 0.58] [uiFancyText "" "--" 0.58 $c_gray]]
			] $n)]]]
			append .scoreboard.icons (at [
				@@@(at "0 star star flag star crown crown star" $getmode)
				"frag" "death" "pj" "ping"
			] $n)
		] ]

		// 1:TEAM
		.scoreboard.header = [
			local bflip
			bflip = (= $arg1 2) // right-to-left flip bit
			result [
				uifill @[.sb_rowwidth] $uiPad:DM [
					uispace 0 $uiPad:S [
						uiFastImg <reorient:@@@bflip> "hud/" "scorebg"
						uiclamp-e
					] ; uiclamp-e
					uispace $uiPad:M $uiPad:M- [
						uiFancyText "wide" @@@(
							at "FFA Aesir Vanir" $arg1
						) 1.4 @@@(INT:TRANS (
							at [0x40D060 0x4060D0 0xD04040] $arg1
						) % 80)
					] ; uialign- @@(? $bflip 1 -1) -1
					uispace $uiPad:O5 (*f $uiPad:O5 2.9) [
						uiFancyText "wide" (getteamscore @@@arg1) 1 @@@(
							at [0x40D060 0x4060D0 0xD04040] $arg1
						)
					] ; uialign- @@(? $bflip -1 1) -1
					if (numscoreboard @@arg1) [
						uihlist 0 [
							@@@@(? $bflip [
								uialign -1 1
								@(looplistconcatrev x $.scoreboard.icons [ result [
									uifill @@[.sb_cellwidth] @@(*f $uiPad:O4 7) [
										uiShadowedImg "" "hud/" @@x "" $uiPad:3XL
									]
								] ])
							] [
								uialign 1 1
								@(looplistconcat x $.scoreboard.icons [ result [
									uifill @@[.sb_cellwidth] @@(*f $uiPad:O4 7) [
										uiShadowedImg "" "hud/" @@x "" $uiPad:3XL
									]
								] ])
							])
						]
					]
				]
			]
		]

		// 1:TEAM  2:[CHILDREN]  3:SIDE-EXTRAS
		.scoreboard.row = [
			local bflip
			bflip = (= $arg1 2) // right-to-left flip bit
			result [
				uiFastImgStretched @(? (> $arg1 -1) [(
					intmul (at [
						[0x303030 0x282828] // ffa
						[0x1C2E70 0x122466] // aesir
						[0x701C1C 0x661212] // vanir
					] @arg1 (& $n 1))
				)] [ @(intmul 0x303840) ]) "hud/" "row" "" [
					uifill @@(? $arg3 $arg3 $.sb_rowwidth)
					@@(? $arg2 [ uigroup [ uiclamp.e ; @@arg2 ] ])
					if (scoreboardhighlight $cn) [
						uispace $uiPad:O3 $uiPad:O3 [
							uiShadowedImg <reorient:@@@@bflip> "hud/" "edge" "" $uiPad:M
						] ; uialign- @@@(? $bflip 1 -1) -1
					]
					@@(? $bflip [
						uihlist 0 [
							@@(looplistconcatrev x $.scoreboard.cells [ result [ @x; ] ])
						] ; uiclamp-y ; uialign- -1
						uispace $uiPad:M 0 [ uihlist $uiPad:O3 [
							if (isai $cn) [ uiShadowedImg "" "hud/" "bot" "" $uiPad:2XL ]
							uiFancyText "" (getclientname $cn) 0.6 (scoreboardstatus $cn)
						] ] ; uialign- 1
					] [
						uispace $uiPad:M 0 [ uihlist $uiPad:O3 [
							uiFancyText "" (getclientname $cn) 0.6 (scoreboardstatus $cn)
							if (isai $cn) [ uiShadowedImg "" "hud/" "bot" "" $uiPad:2XL ]
						] ] ; uialign- -1
						@(? (> $arg1 -1) [
							uihlist 0 [
								@@(looplistconcat x $.scoreboard.cells [ result [ @x; ] ])
							] ; uiclamp-y ; uialign- 1
						])
					])
				] $uiPad:5XL
				++ n
			]
		]

		if (m_teammode $getmode) [
			.scoreboard.aesir = [
				local n
				uivlist 0 [
					@@(.scoreboard.header 1)
					loopscoreboard cn 1 [ @@@(.scoreboard.row 1) ]
				] ; uialign- -2 -1
			]
			.scoreboard.vanir = [
				local n
				uivlist 0 [
					@@(.scoreboard.header 2)
					loopscoreboard cn 2 [ @@@(.scoreboard.row 2) ]
				] ; uialign- -2 -1
			]
			.scoreboard.ctf = [
				uigroup [
					uioffset 0 $uiPad:O5 [
						uioffset $uiPad:2XL- $uiPad:2XL- [ uiFancyText "wide" "V" 1 ]
						uioffset $uiPad:2XL  $uiPad:2XL  [ uiFancyText "wide" "S" 1 ]
					] ; uialign- 0 -1
					uihlist $uiPad:DM [
						.scoreboard.aesir
						.scoreboard.vanir
						uialign* -2 -1
					]
				]
			]
		]

		.scoreboard.spec = [
			uigrid 3 $uiPad:L 0 [
				loopscoreboard cn -1 [
					@@@(.scoreboard.row -1 [] $.sb_namewidth)
				]
			]
		]

		.scoreboard.shelf.top3 = [
			uioffset $uiPad:5XL- 0 [
				uiFastImg (fade 0.5) "hud/" "shelf" "" $uiPad:5XL
				push n (at [<mad:1/.8/0> <mad:.8/.8/.8> <mad:.8/.5/0>] $n) [
					uiFastImg $n "hud/" "glow"  "" $uiPad:5XL
					uiFastImg $n "hud/" "crown" "" $uiPad:3XL
				]
			] ; uialign- -1
		]
		.scoreboard.shelf.rest = [
			arg1 = (= $cn $getclientnum)
			uioffset $uiPad:5XL- 0 [
				uiFastImg (fade (? $arg1 0.5 0.25)) "hud/" "shelf" "" $uiPad:5XL
				uiFastImg <mad:.18/.18/.18> "hud/" "glow" "" $uiPad:5XL
				push n (+ $n 1) [
					uifontcolortext "def.ol" $n (|A! (? $arg1 0xB0 0x60)) 0.55
				]
			] ; uialign- -1
		]

		.scoreboard.ffa = [
			local n x
			uigroup [
				uivlist 0 [
					@@@(.scoreboard.header 0)
					loopscoreboard cn 0 [
						if (< $n 3) [
							if (= $cn $getclientnum) [ x = 1 ]
							@@@@@(.scoreboard.row 0 $.scoreboard.shelf.top3)
							if (= $n 3) [ uifill 0 $uiPad:M ]
						] [
							if (= $cn $getclientnum) [
								if (> $n (+ $.sb_offset 10)) [ uifill 0 $uiPad:M ]
								@@@@@@(.scoreboard.row 0 $.scoreboard.shelf.rest)
								if (< $n (+ $.sb_offset 0)) [ uifill 0 $uiPad:M ]
							] [
								if (&& [<= $n (+ $.sb_offset 10 $x $spectating)] [>= $n $.sb_offset]) [
									@@@@@@@(.scoreboard.row 0 $.scoreboard.shelf.rest)
								] [ ++ n ]
							]
						]
					]
				]
				uioffset $uiPad:2XL 0 [
					uialign 1
					// XXX need a way to scan for flag carrier
				]
			]
		]
	]
]
