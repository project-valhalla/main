
UImenu "servers" [
	refreshservers
	uivlist $.UI_pad_L [
		uihlist 0 [
			uiclamp.x
			UIbutton "hold2" [
				UIfastimg "" "ui/" "refresh" "" 0.030
				uitext "Update Server List" 0.6
			] 0 $.UI_pad_6XL [ updatefrommaster ] 0 [
				UIsettip "retrieve registered servers from the masterserver"
			]
			uihlist 0 [
				UIbar 0 1 $.UI_pad_L $.UI_pad_S
				UIbuttonarea "hold2" $.UI_pad_DS $.UI_pad_6XL [
					UIsettip "scan for servers on your local area network"
					UIfastimg "" "ui/" "lan" $searchlan 0.028
					uirelease [ searchlan (! $searchlan) ]
				]
				UIbuttonarea "hold2" $.UI_pad_DS $.UI_pad_6XL [
					UIsettip "automatically update server information on display"
					UIfastimg "" "ui/" "web" $autoupdateservers 0.030
					uirelease [ autoupdateservers (! $autoupdateservers) ]
				]
				UIbuttonarea "hold2" $.UI_pad_DS $.UI_pad_6XL [
					UIsettip "automatically sort servers by player amount & latency"
					UIfastimg "" "ui/" "sort" $autosortservers 0.030
					uirelease [
						.autosortservers (! $.autosortservers)
						autosortservers $.autosortservers
					]
				]
				UIbar 0 1 $.UI_pad_L $.UI_pad_S
			]
			UIbutton "hold2" [
				UIcheckbox $.sb_showfilters 0 0 0 [
					if $arg1 [ UItriangle $c_line1 0 $.UI_pad_ML 0 180 ]
				]
				uitext "Server Filtering" 0.6
				UIfastimg "" "ui/" "piechart" "" 0.03
			] 0 $.UI_pad_6XL [ .sb_showfilters (! $.sb_showfilters) ] 0 [
				UIsettip "select filters with which to hide servers"
			]
		]
		if $.sb_showfilters [
			UIbox [box d n n d n] 0 0.148 [
				uispace $.UI_pad_L $.UI_pad_L [
					uihlist $.UI_pad_L [
						UIactivetable 0.13 $.UI_pad_5XL 0.55 [
							"FFA"      [ UIcheckbox [& $.sb_mode  2] ] [ .sb_mode (^ $.sb_mode  2) ]
							"CTF"      [ UIcheckbox [& $.sb_mode  4] ] [ .sb_mode (^ $.sb_mode  4) ]
							"ELIM"     [ UIcheckbox [& $.sb_mode  8] ] [ .sb_mode (^ $.sb_mode  8) ]
							"LMS"      [ UIcheckbox [& $.sb_mode 16] ] [ .sb_mode (^ $.sb_mode 16) ]
							"JGNT"     [ UIcheckbox [& $.sb_mode 32] ] [ .sb_mode (^ $.sb_mode 32) ]
							"INFC"     [ UIcheckbox [& $.sb_mode 64] ] [ .sb_mode (^ $.sb_mode 64) ]
							"EDIT"     [ UIcheckbox [& $.sb_mode  1] ] [ .sb_mode (^ $.sb_mode  1) ]
							"Disabled" [ UIradio    [= $.sb_mode  0] ] [ .sb_mode 0 ]
						] 2 "hide servers according to game mode"
						UIbar 0 1
						UIactivetable 0.13 $.UI_pad_5XL 0.55 [
							"Auth"     [ UIcheckbox [& $.sb_mm  1] ] [ .sb_mm (^ $.sb_mm  1) ]
							"Open"     [ UIcheckbox [& $.sb_mm  2] ] [ .sb_mm (^ $.sb_mm  2) ]
							"Veto"     [ UIcheckbox [& $.sb_mm  4] ] [ .sb_mm (^ $.sb_mm  4) ]
							"Locked"   [ UIcheckbox [& $.sb_mm  8] ] [ .sb_mm (^ $.sb_mm  8) ]
							"Private"  [ UIcheckbox [& $.sb_mm 16] ] [ .sb_mm (^ $.sb_mm 16) ]
							"Passcode" [ UIcheckbox [& $.sb_mm 32] ] [ .sb_mm (^ $.sb_mm 32) ]
							"" [] []
							"Disabled" [ UIradio    [= $.sb_mm  0] ] [ .sb_mm 0 ]
						] 2 "hide servers according to master mode"
						UIbar 0 1
						UIactivetable 0.13 $.UI_pad_5XL 0.55 [
							"50+"      [ UIradio [= $.sb_ping   55] ] [ .sb_ping   55 ]
							"100+"     [ UIradio [= $.sb_ping  110] ] [ .sb_ping  110 ]
							"150+"     [ UIradio [= $.sb_ping  165] ] [ .sb_ping  165 ]
							"250+"     [ UIradio [= $.sb_ping  270] ] [ .sb_ping  270 ]
							"500+"     [ UIradio [= $.sb_ping  525] ] [ .sb_ping  525 ]
							"Disabled" [ UIradio [= $.sb_ping 9999] ] [ .sb_ping 9999 ]
						] 2 "hide servers according to latency"
						UIbar 0 1
						UIactivetable 0.13 $.UI_pad_5XL 0.55 [
							"Empty"    [ UIcheckbox [= $.sb_players_e 1] ] [ .sb_players_e (! $.sb_players_e) ]
							"Full"     [ UIcheckbox [= $.sb_players_f 1] ] [ .sb_players_f (! $.sb_players_f) ]
							"Disabled" [ UIradio [&& [! $.sb_players_e] [! $.sb_players_f]] ] [ .sb_players_e 0 ; .sb_players_f 0 ]
						] 1 "hide servers according to player amount"
						uialign* -2 -1
					]
				]
			]
		]
		UItooltipbox 0.96 (? $.sb_showfilters 0.482 0.64) [
			local numfiltered
			uihover [ autosortservers 0 ] [
				autosortservers $.autosortservers
				.serverhover = -1
			]
			UIsettip "join server"
			uivlist 0 [
				uialign -2 -1
				uiclamp.x
				loop i $numservers [
					if (servinfovalid $i) [
						if (&& [ // latency
							<= (servinfoping $i) $.sb_ping
						] [      // mastermode
							if $.sb_mm [! (& (<< 1 (+ (servinfomastermode $i) 1)) $.sb_mm)] 1
						] [      // gamemode
							if $.sb_mode [! (& (<< 1 (servinfomode $i)) $.sb_mode)] 1
						] [      // empty server
							if $.sb_players_e [servinfonumplayers $i] 1
						] [      // full server
							if $.sb_players_f [< (servinfonumplayers $i) (servinfomaxplayers $i)] 1
						]) [
							uihover+ [ .serverhover = $i ]
							UIserver $i
						]
					] [ numfiltered = (+ $numfiltered 1) ]
				]
				uiclamp*x
			]
		] 0 [
			if (> $.serverhover -1) [
				uicolortext (servinfoname $.serverhover) $c_gray 0.5
				uifont "wide" [ uicolortext ":" $c_gray 0.5 ]
				uitextfill 2.5 0.5 [
					uicolortext (servinfoport $.serverhover) $c_gray 0.5
					uialign- -1
				]
			]
			uiline $c_line1 $.UI_pad_S
			uiline $c_line1 $.UI_pad_S
			uiclamp*y
			uitextfill 8.25 0.5 [
				uicolortext (concat (- $numservers $numfiltered) "^f7servers found") $c_green 0.5
				uialign- 1
			]
		]
	]
] [ initservers ] [] [
	uiallowinput (! (uivisible "dialog_serverconnect"))
] [ uitext "Server Browser" 0.8 ]




////////////////////////////////////////////////////////////////////////////////////////////////////

// search desc
//    if $.sb_searchdesc [>= (strstr (servinfodesc $i) $.sb_searchdesc) 0] 1
//] [      // search map
//    if $.sb_searchmap [>= (strstr (servinfomap $i) $.sb_searchmap) 0] 1
//] [

//cond (|| [= (servinfonumplayers $arg1) (servinfomaxplayers $arg1)] [> (servinfomastermode $arg1) 2]) [
//    uihgradient 0xFFFFFF 0x80D04040
//] (>= (*f 100 (divf (servinfonumplayers $arg1) (servinfomaxplayers $arg1))) 60) [
//    uihgradient 0xFFFFFF 0x80D0B040
//]

//UIfastimg "" "key" (servinfohaspassword $arg1) $.UI_pad_2XL
//uialign- -1  1
//UIfastimg "" "fav" (servinfokeep $arg1) $.UI_pad_2XL
//uialign-  1  1
