
UIquickdialog "texture_browser" [
	uialign 1
] [
	UIhudcell 0 0 [
		uihlist $.UI_pad_L [
			uiscroll $.x $.y [
				uifill $.x $.y
				uihover [] [ .UI_texsel = -1 ]
				uigrid $.UI_texcol 0 0 [
					uialign -1 -1
					loop i $numslots [ // or looptexmru
						UIbuttonarea "none" $.UI_pad_DXL $.UI_pad_DXL [
							uispace 0.0006 0.0006 [
								uiclamp.e
								if (!= $i $.UI_texsel) [
									uimodcolor $c_boxmod
									uislotview $i
									uioutline $c_line1
								] [
									uimodcolor $c_grdmod
									uislotview $i
									uioutline (coswave.INT $c_green $c_line1 (uihold? 250 500))
								]
								uiclamp*e
							]
							uihover [
								if (!= $.UI_texsel $i) [
									.UI_texsel = $i
									UIsetmillis
								]
								uispace $.UI_pad_S $.UI_pad_MS [
									uialign -1 -1
									uifont "default_outline" [ uicolortext $i 0x98FFFFFF 0.55 ]
								]
							]
							uipress [ UIsetmillis ]
							uirelease [
								settex (getslottex $i)
								hideui $uiname
							]
						]
					]
				]
			]
			UIvscrollbar 0 $.y 4.8
		]
	]
] [
	.UI_texsel = -1
	.UI_texcol = (+ (>f (divf $screenw $screenh) 1.3) 3)
	.x = (*f $.UI_pad_DXL $.UI_texcol)
	.y = (-f 1 (*f $.UI_pad_L 4))
	hideui "mapmodel_browser"
	hideui "map_browser"
	hideui "geo_prefab"
	hideui "editvars"
] [ if $editing [ showui "editvars" ] ]

//==================================================================

UIquickdialog "texture_browser" [
	uialign 1
	if (> $.tb_totalpg 1) [
		if (iskeyheld "LCTRL") [
			uiscrolldown [ UIscrollsound ; .tb_curpage = (- $.tb_totalpg 1) ]
			uiscrollup   [ UIscrollsound ; .tb_curpage = 0 ]
		] [
			uiscrolldown [ UIscrollsound ; ++ .tb_curpage 1 (- $.tb_totalpg 1) ]
			uiscrollup   [ UIscrollsound ; -- .tb_curpage 1 0 ]
		]
	]
] [
	UIhudcell 0 0 [
		uivlist $.UI_pad_L [
			uihlist 0 [
				UIbutton "hold2 $c_diag2" [
					//UIcheckbox [= $.tb_doaction 1] 0 0 0 [
					//	if $arg1 [ UItriangle $c_line1 0 $.UI_pad_ML 0 180 ]
					//]
					uitext "Standard" 0.6
				] $.z 0 [ .tb_doaction = 1 ] -1
				uigroup [
					uifill $.z $.UI_pad_6XL
					uihlist (-f 0 $.UI_pad_MS) [
						uiclamp.y
						uifill $.UI_pad_DXS 0 [
							uifont "mono" [ uitext (+ $.tb_curpage 1) 0.7 ]
							uialign- 1 -1
						]
						uioffset 0 (-f 0 $.UI_pad_L) [
							uiclip 0 $.UI_pad_6XL [ uicolortext "/" $c_gray 1.2 ]
						] // text takes up a lot of vertical space, have to "cut" it
						uifill $.UI_pad_DXS 0 [
							uifont "mono" [ uitext (max $.tb_totalpg 1) 0.7 ]
							uialign- -1 1
						]
						uiclamp*y
					]
				]
				UIbutton "hold2 $c_diag2" [
					uitext "M.R.U." 0.6
					//UIcheckbox [= $.tb_doaction 0] 0 0 0 [
					//	if $arg1 [ UItriangle $c_line1 0 $.UI_pad_ML 0 180 ]
					//]
				] $.z 0 [ .tb_doaction = 0 ] 1
				uiclamp*y
			]
			uigroup [
				uifill $.x $.y
				uihover [] [ uihold [] [ .tb_selidx = -1 ] ]
				uigrid 6 0 0 [
					uialign -1 -1
					loopwhile+ i (* $.tb_curpage 60) 60 [< $i $numslots] [
						n = (mod $i 60) // page normalized index
						uitarget $.w $.w [
							if (= $n $.tb_selidx) [
								uimodcolor $c_grdmod
								uislotview $i
								uiclamp*e
								uispace $.UI_pad_S $.UI_pad_MS [
									uifont "default_outline" [ uicolortext $i (|A 0xA0 $c_white) 0.55 ]
								] ; uialign- -1 -1
								uirelease [
									settex (getslottex $i)
									hideui $uiname
								]
							] [
								uimodcolor $c_boxmod
								uislotview $i
								uioutline $c_line1
								uiclamp*e
								uihover [ .tb_selidx = $n ; UIsetmillis ]
								
							]
							UIhoversound
						]
					]
				]
				if (> $.tb_selidx -1) [ // drawing on top to avoid overlap from other cells
					uioffset (*f $.w (mod $.tb_selidx 6)) (*f $.w (div $.tb_selidx 6)) [
						uioutline (coswave.INT $c_green $c_line1 500) $.w $.w
					] ; uialign- -1 -1
				]
			]
			uihlist 0 [
				uifont "webdings" [
					uifill $.z
					if (> $.tb_curpage 0) [
						UIbuttonarea "hold2 $c_diag2" 0 0 [ uitext "7" 0.9 ] [
							uirelease [-- .tb_curpage 1 ]
						] ; uiclamp-e
					] [ uicolortext "7" $c_gray 0.9 ]
				]
				uifill $.z 0 [ UIfastimg "ui/" "io/" "mouseM" $.UI_pad_6XL ]
				uifont "webdings" [
					uifill $.z
					if (< (+ $.tb_curpage 1) (max $.tb_totalpg 1)) [
						UIbuttonarea "hold2 $c_diag2" 0 0 [ uitext "8" 0.9 ] [
							uirelease [ ++ .tb_curpage 1 ]
						] ; uiclamp-e
					] [ uicolortext "8" $c_gray 0.9 ]
				]
				uiclamp*y
			]
		]
	]
] [
	.y = (-f 1 $.UI_pad_DM $.UI_pad_DS) //0.86
	.z = (divf $.y 5) // 0.172
	.w = (divf $.y 10)  // 0.086
	.x = (*f $.z 3) // 0.516
	.tb_selidx   = -1
	.tb_totalpg  = (ceil (divf $numslots 60))
	.tb_curpage  = 0
	hideui "mapmodel_browser"
	hideui "map_browser"
	hideui "geo_prefab"
	hideui "editvars"
] [ if $editing [ showui "editvars" ] ]
