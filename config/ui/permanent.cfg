
///////////////////////////////////////////////////////////////////////////////
//  PERMANENT UI -- DO *NOT* MAKE CHANGES HERE                               //
///////////////////////////////////////////////////////////////////////////////

newui "permanent" [
	uieschide 0
	uiclamp.e

	// Disables active input field if clicking Out-Of-Bounds
	if $uiInputOOB [
		uiallowinput 1
		uipress    [ uiCleanKB ]
		uiescpress [ uiCleanKB ]
		uitarget
	] [ uiallowinput 0 ]

	nodebug [
		.AnimProgress
	]
	local $mainUi
	mainUi = (? (= $mainmenu 1) "main_welcome" "main")
	uiallowinput (uivisible $mainUi)
	if (= $mainmenu 1) [
		uigroup [
			uiShadowedImg "" "" "" "logo" 1.04 0.24
			uialign 0 -1
		]
		uispace $uiPad:4XL $uiPad:4XL [
			uitext $currentversionname $uiPad:USS
		] ; uialign- 1 -1
	] [
		if (! (uivisible "profile")) [
			uiFastImg "" "shadow"
		]
		uigroup [
			uispace $uiPad:M $uiPad:M [
				uitext $currentversionname $uiPad:USS
			] ; uialign- -1 1
			uitarget 0 0 [
				uipress [
					showui "profile"
				]
				uispace $uiPad:4XL $uiPad:4XL [
					uihlist 0 [
						uiFastImgStretched (+s [<fade:0.9>] (? $uihover? [<mad:0.25,0.25,0.25>] "")) "ui/" "ui_bar0" "" [
							uispace $uiPad:L $uiPad:L [
								uihlist $uiPad:S [
									if $customflag [
										uiFastImg "" "countryflag/" $customflag "" (? (=s $customflag "geo") 0.025 (*f 1.5 0.025)) 0.025
									]
									uifontcolorblendtext "default" $name $c_green 1.0 0.6
								]
							]
						] $uiPad:DM $uiPad:DXS
						uiFastImgStretched [<fade:0.9>] "ui/" "ui_bar0" "" [
							uifontcolorblendtext "modernpics" "f" $c_gray_l 1.0 1.75
						] $uiPad:DM
					]
				]
			] ; uialign- 1 -1
			uitarget 0 0 [
				uipress [
					changeui "credits" $mainUi
				]
				uispace $uiPad:S $uiPad:XL [
					uivlist $uiPad:3XL- [
						uiFastImg "" "logo" "" "" $uiPad:DSXL $uiPad:DXL
					]
				]
			] ; uialign- 1 1
			nodebug [
				.CheckPermBits
			]
		] ; uiclamp*e
	]
	uitarget 0 0 [
		uipress [
			if (uivisible "profile") [
				hideui "profile"
			] [
				if $uiConfirmQuit [
					changeui "dialog_quit" $mainUi
				] [
					quit
				]
			]
		]
		uispace $uiPad:4XL $uiPad:4XL [
			uiFastImgStretched (+s [<fade:0.9>] (? $uihover? [<mad:0.2,0.2,0.2>] "")) "ui/" "ui_bar0" "" [
				uifontcolorblendtext "modernpics" "X" $c_gray_l 1.0 1.75
			] $uiPad:DM
		]
	] ; uialign- -1 -1
] [
	showui "tips"
	uiEffects = 0 // stores bits for fullscreen effects
]

// arg1: var
// arg2: duration
// arg3: condition
uiAnimate = [
	arg1 = [.@arg1.anim] ; if $arg3 [
		if (<f $$arg1 1) [
			$arg1 = (+f $$arg1 (/f $getframemillis $arg2))
		] [ $arg1 = (min 1 $$arg1) ]
	] [
		if (>f $$arg1 0.01) [
			$arg1 = (-f $$arg1 (/f $getframemillis $arg2))
		] [ $arg1 = (max 0 $$arg1) ]
	]
]

.AnimProgress = [
	local animProgress windowsCache
	windowsCache = $uiAnimWindows
	looplist2 ui curState $windowsCache [
	   if $curState [
			animProgress = (- $getmillis $[.@ui.timestamp] $[.@ui.progress] -1)
		] [ animProgress = (- (+ $[.@ui.timestamp] $[.@ui.duration]) $getmillis $[.@ui.progress]) ]
		if (<= 0 $animProgress $[.@ui.duration]) [
			[.@ui.opacity] = (%f $animProgress $[.@ui.duration])
		] [
			listsplice= uiAnimWindows "" (indexof $uiAnimWindows $ui) 2
			[.@ui.opacity]  = $curState
			[.@ui.progress] = 0
			if $curState [] [ hideui $ui ]
		]
	]
]

.CheckPermBits = [
	local bits
		if (&& $uiBlurBg $uiBlurring)   [ ^= bits 1 ] // blur condition
		if (&& $uiVignette $uiBlurring) [ ^= bits 2 ] // vignette condition
		if (|| (isdead $getclientnum)   [ && $spectating [
			< (+ (getclienthealth $getfollow) 1) 0
		] ])                            [ ^= bits 4 ] // damage condition

		uiAnimate effects 120 $bits

		if $bits [ uiEffects = $bits ] [
			if (=f $.effects.anim 0) [ uiEffects = 0 ]
		]

		if (& $uiEffects 7) [
			// uiFastImgTiled (fade $.effects.anim) "ui/" "ui_bg2"
			uiFastImgTiled "" "ui/" "ui_bg2"
		]
		if (& $uiEffects 4) [
			// uiFastImg (+s (fade $.effects.anim) <fade:0.8>) "hud/" "damage"
			// uiFastImg (fade $.effects.anim) "shadow"
			uiFastImg <fade:0.8> "hud/" "damage"
			uiFastImg "" "shadow"
		]
		if (& $uiEffects 2) [
			// uiFastImg (fade $.effects.anim) "shadow"
			uiFastImg "" "shadow"
		]
]
