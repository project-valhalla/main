
//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Master Scroll Controller                                                                                //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

msc_delta = [
	cond $editing [
		cond $.followplayer [
			follownext $arg1
		] (iskeyheld "LCTRL") [
			if $enthavesel [ entautoview $arg1 ]
		] [
			cond [&& $enthavesel [||iskeyheld "1" "2" "3" "4" "5"]] [
				loop i 5 [ if (iskeyheld (+ $i 1)) [ entattrdelta $i $arg1 ] ]
			] [iskeyheld "G"] [                                                       // Change grid power
				gridpower (max 0 (min 12 (+ $arg1 $gridpower) (- $mapscale 1)))
			] [iskeyheld "R"] [                                                       // Selection rotate / blend brush rotate
				if $blendpaintmode [ rotateblendbrush $arg1 ] [ editrotate $arg1 ]
			] [iskeyheld "Q"] [                                                       // Push corner closest to the cursor
				editfacewsel $arg1 2
			] [iskeyheld "F"] [                                                       // Push selected face/corners
				editfacewsel $arg1 0
			] [iskeyheld "Y"] [                                                       // Change MRU textures
				edittex $arg1
			] [iskeyheld "Z"] [                                                       // Change floatspeed
				push step [
					50 60 70 80 90 100
					150 200 250 300 350 400 450
					500 600 700 900
					1000 2000 3000 4000 5000
				] [ floatspeed (at $step (min (+ $arg1 (
					indexof $step $floatspeed
				)) (- (listlen $step) 1))) ]
			] [&& $hmapedit [iskeyheld "H"]] [                                        // Change heightmap brush
				selecthbrush $arg1
			] $blendpaintmode [                                                       // Change blendmap mode / blend brush
				if (iskeyheld "B") [
					arg1 = (+ $blendpaintmode $arg1)
					cond [< $arg1 1] [ blendpaintmode = 5 ] [> $arg1 5] [ blendpaintmode = 1 ] [ blendpaintmode $arg1 ]
				] [ nextblendbrush $arg1 ]
			] [ editfacewsel $arg1 1 ]
		]
	] $mainmenu [
		// NOTHING HERE YET
	] $spectating [
		if $thirdperson [
			loop i 10 [ sleep (* $i 10) [
				thirdpersondistance (clampf (+f $thirdpersondistance (*f $arg1 0.25)) 10 50)
			] ]
		] [ nextfollow $arg1 ]
	] [ nextweapon $arg1 ]
]




//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  Directional Vslot Delta                                                                                 //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

// vdirdelta  DIR  ACTION   X  Y
// vdirdelta  ~~~  voffset  8  0
// DIR: 0 = NORTH, 1 = EAST, 2 = SOUTH, 3 = WEST
// meant to use camera yaw to transform vcommand coordinates for convenience
vdirdelta = [
	vdelta [
		if (> $getselface 3) [
			arg5 = (mod (/ (+f $getcamyaw 45) 90) 4)
			case $arg5 0 [ // LOOKING NORTH
				case $arg1 0 [ // UP
					$arg2 0 (*f $arg4 -1)
				] 1 [          // RIGHT
					$arg2 (*f $arg3 -1)
				] 2 [          // DOWN
					$arg2 0 (*f $arg4 -1)
				] 3 [          // LEFT
					$arg2 (*f $arg3 -1)
				]
			] 1 [   // LOOKING EAST
				if (= $getselface 5) [
					arg3 = (*f $arg3 -1)
					arg4 = (*f $arg4 -1)
				]
				case $arg1 0 [ // UP
					$arg2 (*f $arg4 -1)
				] 1 [          // RIGHT
					$arg2 0 $arg3
				] 2 [          // DOWN
					$arg2 (*f $arg4 -1)
				] 3 [          // LEFT
					$arg2 0 $arg3
				]
			] 2 [   // LOOKING SOUTH
				case $arg1 0 [ // UP
					$arg2 0 $arg4
				] 1 [          // RIGHT
					$arg2 $arg3
				] 2 [          // DOWN
					$arg2 0 $arg4
				] 3 [          // LEFT
					$arg2 $arg3
				]
			] 3 [   // LOOKING WEST
				if (= $getselface 5) [
					arg3 = (*f $arg3 -1)
					arg4 = (*f $arg4 -1)
				]
				case $arg1 0 [ // UP
					$arg2 $arg4
				] 1 [          // RIGHT
					$arg2 0 (*f $arg3 -1)
				] 2 [          // DOWN
					$arg2 $arg4
				] 3 [          // LEFT
					$arg2 0 (*f $arg3 -1)
				]
			]
		] [ $arg2 $arg3 $arg4 ]
	]
]




///////////////////////////////////////////////////////////////////////////////
//  Entity Primary Actions                                                   //
///////////////////////////////////////////////////////////////////////////////

=enttype = [ || [=s * $arg1] [=s (enttype) $arg1] ]

=entattr = [ || [=s * $arg2] [= (entattr $arg1) $arg2] ]

// Remove all entities that match the selected one
// usage: /clearents TYPE
clearents = [
	if $editing [
		entcancel
		entselect [=enttype $arg1]
		echo Deleted (enthavesel) $arg1 entities
		delent
	]
]

// Replace all entities that match the selected one
// usage: /replaceents TYPE 1 2 3 4 5
replaceents = [
	if $editing [
		do [
			entfind @(entget)
			entset @(loopconcat+ i 1 $numargs [result $[arg@i]])
		]
		echo Replaced (enthavesel) entities
	]
]

selentedit     = [ saycommand (concat "/entset" (entget)) ]
selreplaceents = [ saycommand (concat "/replaceents" (entget)) ]
selentfindall  = [ do [ entfind @(entget) ] ]

// modify given attribute of ent by a given amount
// arg1 attribute
// arg2 value
entattrdelta = [ entattr $arg1 (+ (entattr $arg1) $arg2) ]

enttypelist = [
	light mapmodel playerstart envmap particles sound spotlight decal
	teleport teledest jumppad
	flag

	shotgun smg pulse rocket railgun
	health light_shield heavy_shield superhealth megahealth
	double_damage haste armour unlimited_ammo agility invulnerability
]

enttypeselect = [
	local len next
	len = (listlen $enttypelist)
	next = (mod (+ (indexof $enttypelist (enttype)) $arg1) $len)
	if (< $next 0) [ next = (+ $next $len) ]
	do [ entset @(listsplice (entget) (at $enttypelist $next) 0 1) ]
]




///////////////////////////////////////////////////////////////////////////////
//  Copy & Paste Commands                                                    //
///////////////////////////////////////////////////////////////////////////////

// 3 types of copying and pasting
// 1. select only cubes      -> paste only cubes
// 2. select cubes and ents  -> paste cubes and ents. same relative positions
// 3. select only ents       -> paste last selected ent. if ents are selected, replace attrs as paste

opaquepaste = 1
entcopymode = 0

editcopy = [
	if (|| havesel [! (enthavesel)]) [
		entcopymode = 0
		copy
	] [ entcopymode = 1 ]
	entcopy
]

editpaste = [
	local cancelpaste
	cancelpaste = (! (|| enthavesel havesel))
	if $entcopymode [
		entreplace
		if $cancelpaste cancelsel
	] [
		pastehilite
		reorient // temp; real fix will be in octaedit
		onrelease [
			if $opaquepaste delcube
			paste
			entpaste
			if $cancelpaste cancelsel
		]
	]
]




///////////////////////////////////////////////////////////////////////////////
//  Selection Commands                                                       //
///////////////////////////////////////////////////////////////////////////////

// select ents with given properties
// '*' is wildcard
entfind = [
	if $numargs [
		entselect (+s [ && [=enttype @@arg1] ] (loopconcat i (- $numargs 1) [
			result [ [=entattr @@i @@[arg@(+ $i 2)]] ]
		]))
	] [ entselect 1 ]
]

entfindinsel = [
	if $numargs [
		entselect (+s [ && insel [=enttype @@arg1] ] (loopconcat i (- $numargs 1) [
			result [ [=entattr @@i @@[arg@(+ $i 2)]] ]
		]))
	] [ entselect insel ]
]

drag       = [ dragging 1 ; onrelease [ dragging 0 ] ]
corners    = [ selectcorners 1 ; dragging 1 ; onrelease [ selectcorners 0 ; dragging 0 ] ]
editmove   = [ moving 1 ; onrelease [ moving 0 ] ; result $moving ]
entdrag    = [ entmoving 1 ; onrelease [ entmoving 0 ] ; result $entmoving ]
editdrag   = [ cancelsel ; || entdrag drag ]
selcorners = [ if $hmapedit hmapselect [ cancelsel ; || entdrag corners ] ]
editextend = [ || entdrag [ if havesel [ selextend ; reorient ; editmove ] drag ] ]

editmovewith = [
	if (havesel) [
		|| editmove arg1
		onrelease [ moving 0 ; dragging 0 ]
	] arg1
]

editmovecorner = [ editmovewith selcorners ]
editmovedrag   = [ editmovewith editdrag ]




///////////////////////////////////////////////////////////////////////////////
//  Miscellaneous Editing Commands                                           //
///////////////////////////////////////////////////////////////////////////////

editfacewsel = [
	if (|| havesel [! (enthavesel)]) [
		if $moving [ pushsel $arg1 ] [
			entcancel
			editface $arg1 $arg2
		]
	]
]


//entdirection  SCROLL_DELTA
entdirection = [
	if (&& enthavesel [= $havesel 0]) [
		cond (> (indexof "playerstart teledest flag" $enttype) -1) [
			entattr 0 (mod (round (+ (entattr 0) (* 15 $arg1) 360) 15) 360)
		] [> (indexof "decal mapmodel" $enttype) -1] [
			arg2 = (- 4 (/ $getselentface 2) 1)
			entattr $arg2 (mod (round (+ (entattr $arg2) (* 5 $arg1) 360) 5) 360)
		] [=s $enttype "jumppad"] [
			arg2 = (mod (+ (/ $getselentface 2) 1) 3)
			entattr $arg2 (+ (entattr $arg2) $arg1)
		]
		result 1
	] [ result 0 ]
]

editdel    = [ if (! $enthavesel) delcube ; delent ]
editflip   = [ flip ; entflip ]
editrotate = [ || (entdirection $arg1) [ rotate $arg1 ; entrotate $arg1 ] ]
editcut    = [
	if (moving 1) [
		if (= $moving 1) selsave
		onrelease [
			moving 0
			if (selmoved) [
				selswap
				copy ; entcopy
				delcube ; delent
				selrestore
				paste ; entpaste
			]
		]
	]
]

//setting_entediting = 1

passthrough = [
	passthroughsel $arg1
	if $arg1 [
		.passthroughcube $passthroughcube
		passthroughcube 1
	] [ passthroughcube $.passthroughcube ]
	entcancel
	if $.entediting [ entediting (! $arg1) ]
]

entcomplete = [ listcomplete $arg1 $enttypelist ]
entcomplete "newent"
entcomplete "entfind"
entcomplete "clearents"

listcomplete editmat "air water clip glass noclip lava gameclip death nogi alpha"
air = [ editmat air ]
looplist i ["" 1 2 3 4] [
	[water@i] = [ editmat water@i ]
	[lava@i]  = [ editmat lava@i  ]
	[glass@i] = [ editmat glass@i ]
]
clip     = [ editmat clip     ]
noclip   = [ editmat noclip   ]
gameclip = [ editmat gameclip ]
death    = [ editmat death    ]
nogi     = [ editmat nogi     ]
alpha    = [ editmat alpha    ]

blendpaintmodes = [ "Off" "Overwrite" "Merge" "Inverted Erase" "Inverted Merge" "Erase" ]

setminimap = [ minimapheight (at $getcampos 2) ]
setsundir = [
	if $numargs [] [
		arg1 = $getcamyaw
		arg2 = $getcampitch
	]
	sunlightyaw $arg1
	sunlightpitch $arg2
]
getsundir = [ sunlightyaw ; sunlightpitch ]
