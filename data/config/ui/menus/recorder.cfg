
UImenu "recorder" [
	uihlist $.UI_pad_L [
		uivlist 0 [
			uifill $.UI_pad_D6XL
			UIhslider moviefps 10 (min 200 $maxfps) 2 0 $.UI_pad_5XL [] [ concat $$arg1 "fps" ]
			UIhslider .mr_resmul 10 100 5 0 $.UI_pad_5XL [
				moview = (*f (? $fullscreen $desktopw $screenw) (divf $.mr_resmul 100))
				movieh = (*f (? $fullscreen $desktoph $screenh) (divf $.mr_resmul 100))
			] [ concatword $$arg1 "^%" ]
			uispace 0 $.UI_pad_L [
				uiclamp.x
				uitext (format "^f2%1 ^f4x ^f2%2" $moview $movieh) 0.6
				UIbuttonarea "hold2 $c_diag2" $.UI_pad_5XL $.UI_pad_5XL [
					uialign 1
					UIcheckbox $.mr_options 0 0 0 [
						UItriangle $c_line1 0 $.UI_pad_ML 0 270
					]
					uirelease [ .mr_options (! $.mr_options) ]
				]
			]
			UIadvfieldLA moviedir 16 0.6 "" [] [] 0 $.UI_pad_5XL [] [
				uicolortext " location:" $c_gray 0.5
				uialign- -1
				uicolortext "no subfolder set yet" $c_gray 0.5
			]
			UIadvfieldLA .mr_title 16 0.6 "" [] [] 0 $.UI_pad_5XL [] [
				uicolortext " name:" $c_gray 0.5
				uialign- -1
				uicolortext (concatword "movie_" (
					if $mainmenu [ result "menu" ] [
						concatword (getmodename $getmode) "_" (
							? (=s $mapname "") "newmap" $mapname
						)
					]
				) "_" $getmillis) $c_gray 0.5
			]
			UIbar 0 0 0 $.UI_pad_M
			UIbuttonarea "hold2" 0 $.UI_pad_5XL [
				uispace $.UI_pad_M 0 [
					uiclamp.x
					uihlist $.UI_pad_L [
						if (&& $movierecording [< (abs (mod $getmillis 800)) 400])  [
							UIcircle $c_red $c_darkred $.UI_pad_XL
						] [ UIcircle $c_line1 $c_line2 $.UI_pad_XL ]
						uitext (? $movierecording "Stop Recording" "Start Recording") 0.6
					] ; uialign- -1
					uihlist $.UI_pad_MS [
						UIkeybox $.UI_pad_SXL $.UI_pad_4XL [ uitext "Ctrl" 0.5 ]
						UIkeybox $.UI_pad_4XL $.UI_pad_4XL [ uitext "R"    0.5 ]
					] ; uialign- 1
				]
			] 0 $.UI_pad_5XL [ togglerecording ]
			uiclamp*x
		]
		if $.mr_options [
			UIbar 0 1
			uivlist 0 [
				uifill $.UI_pad_D3XL 0
				UIbutton "hold2" [
					UIcheckbox $moviesound
					uitext "Record Sound" 0.6
				] 0 $.UI_pad_5XL [ moviesound (! $moviesound) ] -1
				UIbutton "hold2" [
					UIcheckbox (! $moviesync)
					uitext "Frameskipping" 0.6
				] 0 $.UI_pad_5XL [ moviesync (! $moviesync) ] -1
				UIbar 1 0 $.UI_pad_L $.UI_pad_L
				UIbutton "hold2" [
					UIcheckbox $movieaccel
					uitext "HW Accel" 0.6
				] 0 $.UI_pad_5XL [ movieaccel (! $movieaccel) ] -1
				UIbutton "hold2" [
					UIcheckbox $movieaccelyuv
					uitext "YUV Encoding" 0.6
				] 0 $.UI_pad_5XL [ movieaccelyuv (! $movieaccelyuv) ] -1
				UIbutton "hold2" [
					UIcheckbox $movieaccelblit
					uitext "Blitting" 0.6
				] 0 $.UI_pad_5XL [ movieaccelblit (! $movieaccelblit) ] -1
				UIbutton "hold2" [
					UIcheckbox $dbgmovie
					uitext "Debugging" 0.6
				] 0 $.UI_pad_5XL [ dbgmovie (! $dbgmovie) ] -1
				uiclamp*x
			]
		]
	]
] [
	moviefps = (min $moviefps 200 $maxfps)
	if (|| $movierecording [= (gcd $screenw $screenh) (gcd $moview $movieh)]) [] [
		moview $screenw
		movieh $screenh
		.mr_resmul 100
	]
] [] [] [ uitext "Movie Recorder" 0.8 ]

togglerecording = [	
	if $movierecording [ movie ] [
		if (=s $.mr_title "") [
			movie (concatword "movie_" (
				if $mainmenu [ result "menu" ] [
					concatword (getmodename $getmode) "_" (
						? (=s $mapname "") "newmap" $mapname
					)
				]
			) "_" $getmillis)
		] [ movie $.mr_title ]
	]
]

