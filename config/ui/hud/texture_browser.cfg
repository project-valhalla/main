
uiLiteMenu "texture_browser" [
    uialign 1
] [
    uiHudCell 0 0 [
        uiScrollBox "credits" $.x $.y [
            uihover [] [ .UI_texsel = -1 ]
            uigrid $.UI_texcol 0 0 [
                uialign -1 -1
                loop i $numslots [ // or looptexmru
                    uiButtonArea "none" $uiPad:DXL $uiPad:DXL [
                        uispace 0.0006 0.0006 [
                            uiclamp.e
                            if (!= $i $.UI_texsel) [
                                uimodcolor $c_boxmod
                                uislotview $i
                                uioutline $c_line1
                            ] [
                                uimodcolor $c_grdmod
                                uislotview $i
                                uioutline (uiCosWave:INT $c_green $c_line1 (uihold? 250 500))
                            ]
                            uiclamp*e
                        ]
                        uihover [
                            if (!= $.UI_texsel $i) [
                                .UI_texsel = $i
                                uiSetMillis
                            ]
                            uispace $uiPad:S $uiPad:O3 [
                                uialign -1 -1
                                uifontoutline 1 0x7F [
                                    uifontcolortext "default" $i (|A! 0x98) 0.55
                                ]
                            ]
                        ]
                        uipress [ uiSetMillis ]
                        uirelease [
                            settex (getslottex $i)
                            hideui $uiname
                        ]
                    ]
                ]
            ]
        ]
    ]
] [
    .UI_texsel = -1
    .UI_texcol = (+ (>f (/f $screenw $screenh) 1.3) 3)
    .x = (*f $uiPad:DXL $.UI_texcol)
    .y = (-f 1 (*f $uiPad:L 4))
    hideui "mapmodel_browser"
    hideui "map_browser"
    hideui "geo_prefab"
    hideui "editvars"
] [ if $editing [ showui "editvars" ] ]

//==================================================================

uiLiteMenu "texture_browser" [
    uialign 1
    if (> $.tb_totalpg 1) [
        uiscrolldown [ UIscrollsound .tb_curpage (? (iskeyheld "LCTRL")  999  1) (- $.tb_totalpg 1) ]
        uiscrollup   [ UIscrollsound .tb_curpage (? (iskeyheld "LCTRL") -999 -1) 0 ]
    ]
] [
    uiHudCell 0 0 [
        uivlist $uiPad:L [
            uihlist 0 [
                uiButton "hold2 $c_diag2" [
                    //uiCheckbox [= $.tb_doaction 1] 0 0 0 [
                    //	if $arg1 [ uiTriangle $c_line1 0 $uiPad:O5 0 180 ]
                    //]
                    uitext "Standard" 0.6
                ] $.z 0 [ .tb_doaction = 1 ] -1
                uigroup [
                    uifill $.z $uiPad:6XL
                    uihlist $uiPad:O3- [
                        uiclamp.y
                        uifill $uiPad:DXS 0 [
                            uifonttext "mono" (+ $.tb_curpage 1) 0.7
                            uialign- 1 -1
                        ]
                        uioffset 0 $uiPad:L- [
                            uiclip 0 $uiPad:6XL [ uicolortext "/" $c_gray 1.2 ]
                        ] // text takes up a lot of vertical space, have to "cut" it
                        uifill $uiPad:DXS 0 [
                            uifonttext "mono" (max $.tb_totalpg 1) 0.7
                            uialign- -1 1
                        ]
                        uiclamp*y
                    ]
                ]
                uiButton "hold2 $c_diag2" [
                    uitext "M.R.U." 0.6
                    //uiCheckbox [= $.tb_doaction 0] 0 0 0 [
                    //	if $arg1 [ uiTriangle $c_line1 0 $uiPad:O5 0 180 ]
                    //]
                ] $.z 0 [ .tb_doaction = 0 ] 1
                uiclamp*y
            ]
            uigroup [
                uifill $.x $.y
                uihover [] [ uihold [] [ .tb_selidx = -1 ] ]
                uigrid 6 0 0 [
                    uialign -1 -1
                    loopwhile+ i (* $.tb_curpage 60) 60 [< $i $numslots] [
                        n = (mod $i 60) // page normalized index
                        uitarget $.w $.w [
                            if (= $n $.tb_selidx) [
                                uimodcolor $c_grdmod
                                uislotview $i
                                uiclamp*e
                                uispace $uiPad:S $uiPad:O3 [
                                    uifontoutline 1 0x7F [
                                        uifontcolortext "default" $i (|A! 0xA0) 0.55
                                    ]
                                ] ; uialign- -1 -1
                                uirelease [
                                    settex (getslottex $i)
                                    hideui $uiname
                                ]
                            ] [
                                uimodcolor $c_boxmod
                                uislotview $i
                                uioutline $c_line1
                                uiclamp*e
                                uihover [ .tb_selidx = $n ; uiSetMillis ]
                                
                            ]
                            uiHoverOnce [ uiHoverSound ]
                        ]
                    ]
                ]
                if (> $.tb_selidx -1) [ // drawing on top to avoid overlap from other cells
                    uioffset (*f $.w (mod $.tb_selidx 6)) (*f $.w (/ $.tb_selidx 6)) [
                        uioutline (uiCosWave:INT $c_green $c_line1 500) $.w $.w
                    ] ; uialign- -1 -1
                ]
            ]
            uihlist 0 [
                uifont "modernpics" [
                    uifill $.z
                    if (> $.tb_curpage 0) [
                        uiButtonArea "hold2 $c_diag2" 0 0 [
                            uiclamp.e
                            uitext "<" 0.9
                            uirelease [ -- .tb_curpage ]
                        ]
                    ] [ uicolortext "<" $c_gray 0.9 ]
                ]
                uifill $.z 0 [ uiFastImg "" "ui/" "io/" "mouseM" $uiPad:6XL ]
                uifont "modernpics" [
                    uifill $.z
                    if (< (+ $.tb_curpage 1) $.tb_totalpg) [
                        uiButtonArea "hold2 $c_diag2" 0 0 [
                            uiclamp.e
                            uitext ">" 0.9
                            uirelease [ ++ .tb_curpage ]
                        ]
                    ] [ uicolortext ">" $c_gray 0.9 ]
                ]
                uiclamp*y
            ]
        ]
    ]
] [
    .y = (-f 1 $uiPad:DM $uiPad:DS) //0.86
    .z = (/f $.y 5) // 0.172
    .w = (/f $.y 10)  // 0.086
    .x = (*f $.z 3) // 0.516
    .tb_selidx   = -1
    .tb_totalpg  = (ceil (/f $numslots 60))
    .tb_curpage  = (min $.tb_curpage (- $.tb_totalpg 1))
    hideui "mapmodel_browser"
    hideui "map_browser"
    hideui "geo_prefab"
    hideui "editvars"
] [ if $editing [ showui "editvars" ] ]
