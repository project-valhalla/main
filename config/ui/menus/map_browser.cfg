
UIquickdialog "map_browser" [
	uiallowinput (! (uivisible "dialog_loadmap"))
	uialign 1
	if (> $.mb_totalpg 1) [
		uiscrolldown [ UIscrollsound .mb_curpage (? (iskeyheld "LCTRL")  999  1) (- $.mb_totalpg 1) ]
		uiscrollup   [ UIscrollsound .mb_curpage (? (iskeyheld "LCTRL") -999 -1) 0 ]
	]
] [
	UIhudcell 0 0 [
		uivlist $.UI_pad_L [
			uihlist 0 [
				UIbutton "hold2 $c_diag2" [
					UIcheckbox [= $.mb_maplist 1] 0 0 0 [
						if $arg1 [ UItriangle $c_line1 0 $.UI_pad_ML 0 180 ]
					]
					uitext "Community" 0.6
				] $.z 0 [
					.mb_maplist = 1
					.mb_curmaps  = $.mb_cat1maps
					.mb_totalpg  = (listlen $.mb_curmaps)
					.mb_curpage  = 0
				] -1
				uigroup [
					uifill $.z $.UI_pad_6XL
					uihlist 0 [
						uiclamp.y
						uifill $.UI_pad_DXS 0 [
							uifont "mono" [ uitext (+ $.mb_curpage 1) 0.7 ]
							uialign- 1 -1
						]
						uioffset 0 (!f $.UI_pad_L) [
							uiclip 0 $.UI_pad_6XL [ uicolortext "/" $c_gray 1.2 ]
						] // text takes up a lot of vertical space, have to "cut" it
						uifill $.UI_pad_DXS 0 [
							uifont "mono" [ uitext (max $.mb_totalpg 1) 0.7 ]
							uialign- -1 1
						]
						uiclamp*y
					]
				]
				UIbutton "hold2 $c_diag2" [
					uitext "Integrated" 0.6
					UIcheckbox [= $.mb_maplist 0] 0 0 0 [
						if $arg1 [ UItriangle $c_line1 0 $.UI_pad_ML 0 180 ]
					]
				] $.z 0 [
					.mb_maplist = 0
					.mb_curmaps  = $.mb_cat0maps
					.mb_totalpg  = (listlen $.mb_curmaps)
					.mb_curpage  = 0
				] 1
				uiclamp*y
			]
			uigroup [
				uifill $.x $.y
				uihover [] [ if $uiallowinput [ uihold [] [ .mb_selidx = -1 ] ] ]
				uigrid 3 0 0 [
					uialign -1 -1
					local idx ; idx = -1
					looplist m (at $.mb_curmaps $.mb_curpage) [
						++ idx
						uitarget $.z $.z [
							if (= $idx $.mb_selidx) [
								uimodcolor $c_grdmod
								if (findfile (concatword data/map/ $m .png)) [
									uiimage (concatword data/map/ $m .png)
								] [ uiline $c_line2 ]
								uiclamp*e
								uispace 0 $.UI_pad_S [
									uimodcolor $c_grdmod 0 0 [ uiclamp.e ]
									uifont "default_outline" [
										uiclip (-f $.z $.UI_pad_M) 0 [ uitext $m  0.5 ]
									]
								] ; uiclamp-x ; uialign- 0 1
								uirelease [ .mb_selected = $m ; showui "dialog_loadmap" ]
							] [
								uimodcolor $c_boxmod
								if (findfile (concatword data/map/ $m .png)) [
									uiimage (concatword data/map/ $m .png)
								] [ uiline $c_line2 ]
								uiclamp*e
								uispace 0 $.UI_pad_S [
									uimodcolor $c_grdmod 0 0 [ uiclamp.e ]
									uifont "default_outline" [
										uiclip (-f $.z $.UI_pad_M) 0 [ uicolortext $m (|A 0xA0 $c_white) 0.5 ]
									]
								] ; uiclamp-x ; uialign- 0 1
								uihover [ .mb_selidx = $idx ; UIsetmillis ]
								uioutline $c_line1 $.z $.z
							]
							UIhoversound
						]
					]
				]
				if (> $.mb_selidx -1) [ // drawing on top to avoid overlap from other cells
					uioffset (*f $.z (mod $.mb_selidx 3)) (*f $.z (div $.mb_selidx 3)) [
						uioutline (coswave.INT $c_green $c_line1 500) $.z $.z
					] ; uialign- -1 -1
				]
			]
			uihlist 0 [
				uifont "webdings" [
					uifill $.z
					if (> $.mb_curpage 0) [
						UIbuttonarea "hold2 $c_diag2" 0 0 [
							uiclamp.e
							uitext "7" 0.9
							uirelease [ -- .mb_curpage 1 ]
						]
					] [ uicolortext "7" $c_gray 0.9 ]
				]
				uifill $.z 0 [ UIfastimg "" "ui/" "io/" "mouseM" $.UI_pad_6XL ]
				uifont "webdings" [
					uifill $.z
					if (< (+ $.mb_curpage 1) $.mb_totalpg) [
						UIbuttonarea "hold2 $c_diag2" 0 0 [
							uiclamp.e
							uitext "8" 0.9
							uirelease [ ++ .mb_curpage 1 ]
						]
					] [ uicolortext "8" $c_gray 0.9 ]
				]
				uiclamp*y
			]
		]
	]
] [
	.y = (-f 1 $.UI_pad_DM $.UI_pad_DS)
	.z = (divf $.y 5)
	.x = (*f $.z 3)
	.mb_selidx   = -1
	.mb_cat1maps = (subdivlist $usermaps  15)
	.mb_cat0maps = (subdivlist $allmaps 15)
	.mb_curmaps  = $[.mb_cat@[.mb_maplist]maps]
	.mb_totalpg  = (listlen $.mb_curmaps)
	.mb_curpage  = 0
	hideui "texture_browser"
	hideui "mapmodel_browser"
	hideui "geo_prefab"
	hideui "editvars" 
] [ if $editing [ showui "editvars" ] ]
