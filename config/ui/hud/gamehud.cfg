
///////////////////////////////////////////////////////////////////////////////
//  GAMEHUD UI -- MAKE CHANGES AT THY OWN PERIL                              //
///////////////////////////////////////////////////////////////////////////////

newui "gamehud" [
	uiclamp.e
	if (|| $mainmenu (! $showhud)) [ uiallowinput 0 ] [

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  EDITHUD DISPLAY GROUP                                                                                   //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

		if $editing [
			uieschide 0
			uispace $uiPad:M $uiPad:M [
				uihlist $uiPad:M [
					if (iskeyheld "LALT") [
						uiallowinput 1
						uiHudCell $uiPad:DM $uiPad:DXL [
							uiFastImg "" "ui/" "config" "" $uiPad:SXL
						]
					] [
						uiallowinput 0
						uiHudCell $uiPad:DM $uiPad:DXL [
							uivlist 0 [
								uiFastImg "" "ui/" "speed" "" $uiPad:6XL
								uitext $floatspeed 0.6
								uifill 0 $uiPad:S
								uigroup [
									uiFastImg "" "ui/" "grid" "" $uiPad:6XL
									uitext $gridpower 0.5
									uialign- 1 1
								]
							]
						]
					]
				]
			] ; uialign- -1 1

		] [
            .gamehud
        ]
	]
] [
	uiBitsHUD = 0
	showui "hud_"
]

.gamehud = [

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  MATCH TIMER DISPLAY GROUP                                                                               //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////
    uiallowinput 0
    if (m_timed $getmode) [ // var scoreboardtime otherwise
        uiFastImgStretched (fade 0.5) "hud/" "shelf" "" [
            uialign 0 -1
            uiFastImg <mad:0/0/0> "hud/" "glow" "" $uiPad:D2XL $uiPad:DM
            if $intermission [ uifontcolortext "mono" $timeremaining $c_orange 1.2 ] [
                if $isgamewaiting [
                    uifontcolortext "mono" "waiting" $c_gray 1.2
                ] [
                    uivlist 0 [
                        uihlist $uiPad:O3 [
                            uiFancyText "mono"       (div $timeremaining 60)        1.2
                            uiFancyText "mono" ":"                                  1.2
                            uiFancyText "mono" (pad0 (mod $timeremaining 60) 2 "0") 1.2
                        ]
                        uifontcolortext "mono" (+s "score limit: " $getscorelimit) $c_yellow 0.4
                    ]
                ]
            ]
        ] $uiPad:5XL
    ]

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  SCOREBOARD DISPLAY GROUP                                                                                //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

    if (iskeyheld "TAB") [
        if (uivisible "scoreboard") [] [ showui "scoreboard" ]
        uivlist $uiPad:5XL [
            uialign 0 -1
            uifill 0 $uiPad:DM
            if (m_teammode $getmode) [
                .scoreboard.ctf
            ] [ .scoreboard.ffa ]
                .scoreboard.spec
            ]

            uispace $uiPad:L $uiPad:L [
                uivlist 0 [
                    uiFancyText "wide" (getmodeprettyname $getmode) 1.2
                    uifill 0 $uiPad:DM [ uivlist 0 [
                        if $scoreboardmap [
                            uiFancyText "wide" "- ON -" 0.8 $c_gray
                            uiFancyText "wide" $scoreboardmap 1 $c_green
                        ]
                    ]
                ]
            ]
            if (isconnected 1 0) [
                uigroup [
                    uicolortext $scoreboardservinfo (|A! 0x48) 0.7
                ] ; uialign- -1 1
            ]
        ] ; uiclamp-x ; uialign- 0 1
    ] [
        .playerhud
    ]
]

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  MINIMAP UTILITIES                                                                                       //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

MINIMAP_SIZE = 0.25

// rotate a vec2 by an angle and return the X and Y component, respectively
vec2rotatex = [+f (*f (cos $arg3) $arg1) (*f (sin $arg3) $arg2)] // X cos(@) + Y sin(@)
vec2rotatey = [-f (*f (cos $arg3) $arg2) (*f (sin $arg3) $arg1)] // Y cos(@) - X sin(@)

// x y z angle icon scale=0.014 offsetx offsety mad
radar_icon = [
    local dirx diry dist r_dirx r_diry bx by
    if $arg6 [] [ arg6 = 0.014 ]
	dirx = (*f (divf 1 $calcradarscale) (-f $getcamx $arg1))
	diry = (*f (divf 1 $calcradarscale) (-f $getcamy $arg2))
	dist = (sqrt (+f (*f $dirx $dirx) (*f $diry $diry)))
    if (>=f $dist 0.9) [
		dirx = (*f (divf 0.9 $dist) $dirx)
		diry = (*f (divf 0.9 $dist) $diry)
    ]
	r_dirx = (vec2rotatex $dirx $diry $getcamyaw)
	r_diry = (vec2rotatey $dirx $diry $getcamyaw)
    bx = (+f $arg7 (*f $MINIMAP_SIZE $r_dirx))
    by = (+f $arg8 (*f $MINIMAP_SIZE $r_diry))
    uispace -1 -1 [ uioffset $bx $by [ uiFastImgRotated $arg9 "hud/" $arg5 "" $arg4 $arg6 ] ]
]

.playerhud = [
    if (uivisible "scoreboard") [ hideui "scoreboard" ]
    local x cn
    if $isspectator [
        cn = $getfollow
        uivlist 0 [ // spectating display group
            uispace $uiPad:3XL 0 [
                uiFancyText "wide" (? $isghost "WAITING" "SPECTATING") 1.1 $c_yellow
            ]
            uispace $uiPad:3XL 0 [
                uiFancyText "" (+s (getclientcolorname $cn) " ") 0.95
            ]
            uialign* 1
            uifill 0 $uiPad:DXL
        ] ; uialign- 1 1
    ] [ cn = $getclientnum ]

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  MINIMAP DISPLAY GROUP                                                                                   //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////
	if (&& $showminimap [m_ctf $getmode]) [
		uispace $uiPad:L $uiPad:L [
			local cteam

			if (>= $getfollow 0) [
				cteam = (getclientteam $getfollow)
			] [ cteam = $getteam ]

			uiminimap $getcamx $getcamy $getcamyaw $MINIMAP_SIZE 16    // minimap background
			uiFastImg "" "hud/" "radar" "" (+f $MINIMAP_SIZE 0.005)    // minimap border

			// flag bases
			loop i $numflags [
				do [ radar_icon @(flagbase $i) 0 (+s "blip_" (? (= (flagteam $i) 1) "blue" "red")) 0.01 ]
			]

			// players
			looplist cn (listclients 1 1) [
				if (&& $radarteammates [! (isspectator $cn)] [= $cteam (getclientteam $cn)] [!=s $cn (? $isspectator $getfollow $getclientnum)] [! (getclientflag $cn)]) [
					if (isdead $cn) [
						do [ radar_icon @(getclientpos $cn) 0 (+s "blip_" (? (= (getclientteam $cn) 1) "blue" "red") "_dead") ]
					] [
						do [ radar_icon @(getclientpos $cn) (- $getcamyaw (getclientyaw $cn)) (+s "blip_" (? (= (getclientteam $cn) 1) "blue" "red") "_alive") ]
					]
				]
			]

			// flags
			loop i $numflags [
				case (flagstate $i) 0 [
					// flag is at base
					do [ radar_icon @(flagbase $i) 0 (+s "blip_" (? (= (flagteam $i) 1) "blue" "red") "_flag") 0.02 0.017 -0.017 ]
				] 1 [
					// a player has the flag
					if (< (mod $getmillis 1000) 500) [
						do [ radar_icon @(getclientpos (flagowner $i)) 0 (+s "blip_" (? (= (flagteam $i) 1) "blue" "red") "_flag") 0.02 0.017 -0.017 ]
					]
				] 2 [
					// flag is dropped somewhere
					if (< (mod $getmillis 300) 150) [
						do [ radar_icon @(flagdroploc $i) 0 (+s "blip_" (? (= (flagteam $i) 1) "blue" "red") "_flag") 0.02 0.017 -0.017 ]
					]
				] // 3 - flag fell under the map, don't show icon
			]
		] ; uialign- 1 -1
	]

    x = (maxf 0 (-f $uiaspect $uiWideHUD))
    if (>f $x 0) [ x = (*f $x 0.5) ]

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  KILLFEED DISPLAY GROUP                                                                                  //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

    uispace $uiPad:L $uiPad:L [
        uivlist 0 [
            local i timestamp
			if (&& $showminimap [m_ctf $getmode]) [
				uifill 0 (+f $MINIMAP_SIZE $uiPad:L)
			]
            looplistrev n $fragQueue [
                case $i 0 [ timestamp = $n ] 1 [
                    do [ .killfeed.row @n ]
                ] ; i = (mod (+ $i 1) 3)
            ]
        ]
    ] ; uialign- 1 -1

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  GAMEHUD DISPLAY GROUP                                                                                   //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

    uispace (+f $uiPad:3XL $x) $uiPad:3XL [
        //uioutline (rnd 0xFFFFFF) ; uiclamp-e // debug boundary outline

        if (|| $uiBlurring [< $cn 0] [isdead $cn] [&& $thirdperson [> $getfollow -1]]) [
            if (isSpriteLive "hud") [ disableSprite "hud" ]
        ] [
            .resourcehud
        ]
    ] ; uiclamp-x ; uialign- -1 1
]

.resourcehud = [
    uihlist 0 [
        // resource (shield/health) display group
        uivlist 0 [
            if (getclientshield $cn) [
                .bar.shield $uiPad:D5XL (getclientshield $cn) 200 shield
            	uifill 0 $uiPad:XL
            ]
            .bar.health $uiPad:D6XL (getclienthealth $cn) (? $m_insta 1 100) health
        ] ; uialign- 0 1
        // powerup and duration display group
        pushif cp (getclientpowerup $cn) [
            uifill $uiPad:2XL
            uiFastImgStretched (fade 0.5) "hud/" "shelf" "" [
                uialign -2 1
                uispace $uiPad:M $uiPad:S [
                    uivlist $uiPad:L [
                        uiFastImg "" "hud/" (at [
                            doubledmg haste armor infammo agility invulnerability
                        ] (- $cp 1)) "" (-f $uiPad:DL $uiPad:S)
                        .bar.powerup $uiPad:DL (getclientpowerupmillis $cn) (? (= $cp 6) 15000 30000)
                    ]
                ]
            ] $uiPad:5XL
        ]
    ] ; uialign- -1 1
    uivlist 0 [
        if (getclientflag $cn)  [
            uiFastImgStretched (fade 0.5) "hud/" "shelf" "" [
                uispace $uiPad:S $uiPad:S [
                    uiFastImg "" "hud/" "flag_" (? (= (getclientflag $cn) 1) "blue" "red") $uiPad:DL
                ]
            ] $uiPad:5XL
        ]
        uihlist 0 [
            loop i 7 [ // main weapons
                if (&& $hudWeaponBar [hasammo $i 1]) [
                    uivlist 0 [
                        uiFastImg "" "hud/" (at [
                            scattergun smg pulse rocket railgun grenade pistol
                        ] $i) "" $uiPad:DSS
                        uiKeybox $uiPad:4XL 0 [ (uitext (at [ 3 2 5 4 6 7 1 ] $i) 0.5) ]
                    ]
                ]
            ]
        ] ; uialign 0 1
    ]
    if 1 [
        uivlist 0 [
            uihlist $uiPad:L [ // temp ammo group
                uiFastImg "" "hud/" (at [
                    scattergun smg pulse rocket railgun grenade pistol instagun zombie
                ] (getclientweapon $cn)) "" $uiPad:DM
                uiclip 0 $uiPad:DM [
                    uifonttext "wide" "    " 2.08
                    uiFancyText "wide" (getclientammo $cn) 1.8
                    uialign- -1
                ]
            ]
        ] ; uialign- 1 1
    ] [
        uispace $uiPad:L 0 [ // to-be-eventually ammo group
            push n (at [scattergun smg pulse rocket railgun grenade pistol instagun zombie] (getclientweapon $num)) [
                uiShadowedImg "" "hud/" $n "_raw" (*f (gettexaspectwh ( +s "data/interface/hud/" $n "_raw.png" )) $uiPad:DS) $uiPad:DS 2.5
            ]
            uiFancyText "wide" (getclientammo $cn) 2
            uialign- 1 1
        ] ; uialign- 1 1
	]
]

// temporary measure to set Y pos of chat input
newui "hud_" [
	uiallowinput 0
	uiabovehud
	uialign -1 1
	uifill 0 0.19
]


// XXX MOVE TO A DIFFERENT FILE LATER
// linear:HSV  INT1  INT2  X-TIME  WAVELENGTH
linear:HSV = [
	local r1 g1 b1 r2 g2 b2
	INT>HSV $arg1 [ h1 = $h ; s1 = $s ; v1 = $v ]
	INT>HSV $arg2 [ h2 = $h ; s2 = $s ; v2 = $v ]
	HSV>RGB (
		+f $h1 (*f (/f (-f $h2 $h1) $arg4) $arg3) ) (
		+f $s1 (*f (/f (-f $s2 $s1) $arg4) $arg3) ) (
		+f $v1 (*f (/f (-f $v2 $v1) $arg4) $arg3)
	) [ RGB>INT $r $g $b ]
]

.bar.health = [
	local y ; y = (*f $arg1 0.1)
	if (> $arg2 $arg3) [
		arg5 = 0x80F0FF ; arg6 = 0xD0F0FF // supercharged
	] [ push n (%f $arg2 $arg3) [         // normal health
		if (<f $n 0.5) [ n = (*f 16 (pow $n 5)) ] [
			n = (-f 1 (/f (pow (+f (*f -2 $n) 2) 4) 2))
		]
		// smoothstep lerp approach?
		// uncomment lines below and comment lines above to test
		//n = (*f $n $n (-f 3 (*f $n 2)))
		//clampf= n 0 1
		if $m_insta [
		    arg5 = 0x008060
		    arg6 = 0x00F080
		] [
		    arg5 = (linear:HSV 0xFF3000 0x008060 $n 1)
		    arg6 = (linear:HSV 0xF01000 0x00F080 $n 1)
        ]
	] ]
	.bar:toparea $arg1 $arg2 $arg3 $arg4 $arg5 $arg6 [
		pushif over (max 0 (- $arg2 $arg3)) [
			uihlist $uiPad:2XL- [
				push y (*f $y 0.75) [ loop i 3 [
					if (> (ceil (/f $over 50)) $i) [
						i = (sinBounce $uiPad:XL (* $i 100) 400)
						uioffset 0 (+f (minf 0 $i) $uiPad:S) [
							uiShadowedImg (mad $arg6) "hud/" "shield2" "" $y
						]
					]
				] ]
			] ; uiclamp-y ; uialign- 1
		]
	]
]

.bar.shield = [
	local y ; y = (*f $arg1 0.1)
	.bar:toparea $arg1 $arg2 $arg3 $arg4 0xE06000 0xFFD000
]

.bar.powerup = [
	local y ; y = (*f $arg1 0.1)
	.bar:resource $arg1 $arg2 $arg3 0x80D0FF 0xD0F0FF
]

// use: 1:WIDTH  2:VALUE  3:MAXVAL  4:IMG/ID  5:COL1  6:COL2  7:[CHILDREN]
.bar:toparea = [
	uivlist 0 [
		uigroup [
			do $arg7
			uihlist $uiPad:S [
				nodebug [
					if (!= $arg2 $[uiResource:@arg4.val]) [
						[uiResource:@arg4.mul] = 0.95
						[uiResource:@arg4.ts]  = $getmillis
						[uiResource:@arg4.val] = $arg2
					]
					if (> $getmillis (+ $[uiResource:@arg4.ts] 120)) [
						[uiResource:@arg4.mul] = 0.8
					]
				]
				uifill $y $y [ push y (*f $y $[uiResource:@arg4.mul]) [
					uiShadowedImg (intmul $arg6) "hud/" $arg4 1 $y
					uiShadowedImg             "" "hud/" $arg4 2 $y
				] ]
				uiclip 0 $y [ uioffset 0 (*f $y -0.15) [
					uiFancyText "wide" $arg2 (*f $arg1 3.125) $arg6
				] ]
			] ; uialign- -1
		] ; uiclamp-x
		.bar:resource $arg1 $arg2 $arg3 $arg5 $arg6
	] ; uialign- 1
]

// use: 1:WIDTH  2:VALUE  3:MAXVAL  4:COL1  5:COL2
.bar:resource = [
	uigroup [
		local w.lt w.rt
		if $arg2 [
			w.lt = (%f (min $arg2 $arg3) $arg3 $arg1)
			uihgradient (|A 0xC0 $arg4) (|A 0xE0 $arg5) $w.lt $y [
				uiDrawSpriteTiled "hud" "<fade:0.15>" "data/interface/debug/causticmap" $uiPad:DM $w.lt $y 128 0 50 $uiSpriteAnim
				uiFastImgStretched "" "shadow3" "" "" [ uiclamp.e ] (minf $w.lt $uiPad:S)
			] ; uialign- -1
		]
		if (< $arg2 $arg3) [
			w.rt = (-f $arg1 $w.lt)
			uicolor (|A 0x30 $arg4) $w.rt $y [
				uiFastImgTiled "<mad:0/0/0>" "ui/" "ui_bg2" "" [ uiclamp.e ]
				uiFastImgStretched "" "shadow3" "" "" [ uiclamp.e ] (minf $w.rt $uiPad:S)
			] ; uialign- 1
		]
		uioutline (INT:TRANS $arg4 % 400) $arg1 $y
	]
]




//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  KILLFEED INTERNALS                                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

// 1:          2:           3:      4:    5:        6:         7:           8:
// actor.name, target.name, weapon, crit, actor.cn, target.cn, actor.color, target.color
.killfeed.row = [
	local msElapsed animIntro animOutro animProgress weaponIcon
	msElapsed = (- $getmillis $timestamp)

	if (< $msElapsed $uiKillDecay) [
		msElapsed = (%f $msElapsed  $uiKillDecay)
		animIntro = (%f $uiKillFade $uiKillDecay)
		animOutro = (-f 1 $animIntro)

		cond [<f $msElapsed $animIntro] [
			animProgress = (%f $msElapsed $animIntro)
		] [>f $msElapsed $animOutro] [
			animProgress = (-f 1 (%f (-f $msElapsed $animOutro) $animIntro))
		] [ animProgress = 1 ]

		if (<f $msElapsed $animOutro) [
			uifill 0 (*f (-f 1 $animProgress) $uiPad:5XL)
			uiFastImgStretched <mad:.18/.18/.18> "hud/" "row" "" [
				uialign 1 -2
				uispace $uiPad:M 0 [
					uifill 0 $uiPad:5XL
					uihlist 0 [
						case $arg3 -4 [
							uiFancyText "" $arg2 0.65 (INT:TRANS $arg8 * 2)
							uiFancyText "" " was torn apart" 0.65
							uifill $uiPad:XL
							uiShadowedImg "" "hud/" "monster" "" $uiPad:3XL
						] -3 [
							uiFancyText "" $arg2 0.65 (INT:TRANS $arg8 * 2)
							uiFancyText "" " was assassinated" 0.65
							uifill $uiPad:XL
							uiShadowedImg "" "hud/" "death" "" $uiPad:3XL
						] -2 [
							if (isai $arg5) [
								uiShadowedImg "" "hud/" "bot" "" $uiPad:2XL
								uifill $uiPad:O3
							]
							uiFancyText "" $arg1 0.65 (INT:TRANS $arg7 * 2)
							uiFancyText "" " committed seppuku" 0.65
							uifill $uiPad:XL
							uiShadowedImg "" "hud/" "death" "" $uiPad:3XL
						] () [
							weaponIcon = (at [
								"melee"   "scattergun" "smg"    "pulse"   "rocket"
								"railgun" "grenade"    "pistol" "railgun" "zombie"
							] (+ $arg3 1))

							if (isai $arg5) [
								uiShadowedImg "" "hud/" "bot" "" $uiPad:2XL
								uifill $uiPad:O3
							]
							uiFancyText "" $arg1 0.65 (INT:TRANS $arg7 * 2)
							uifill $uiPad:XL
							uiShadowedImg "" "hud/" $weaponIcon "_raw" (*f (gettexaspectwh (
								+s "data/interface/hud/" $weaponIcon "_raw.png"
							)) $uiPad:3XL) $uiPad:3XL
							if $arg4 [
								uifill $uiPad:S
								uiShadowedImg "" "hud/" "crit" "" $uiPad:3XL
							]
							uifill $uiPad:XL
							if (isai $arg6) [
								uiShadowedImg "" "hud/" "bot" "" $uiPad:2XL
								uifill $uiPad:O3
							]
							uiFancyText "" $arg2 0.65 (INT:TRANS $arg8 * 2)
						]
					]
				]
			] $uiPad:5XL
			uifill 0 $uiPad:L
		] [ uifill 0 (*f $animProgress (+f $uiPad:5XL $uiPad:L)) ]
	]
]

///////////////////////////////////////////////////////////////////////////////
//  Scoreboard                                                               //
///////////////////////////////////////////////////////////////////////////////

newui "scoreboard" [
	uiallowinput 0
	refreshscoreboard
] [
	uiSetBgBlur 1
	.sb_offset = 3
] [ uiSetBgBlur -1 ]

.scoreboard.update = [
	local bit cc

	if $getmode                 [ bit = (^ $bit 0x01) ; ++ cc ] // score
	if $showkills               [ bit = (^ $bit 0x02) ; ++ cc ] // kills
	if $showdeaths              [ bit = (^ $bit 0x04) ; ++ cc ] // deaths
	if $scoreboardmultiplayer [
		if $showpj              [ bit = (^ $bit 0x08) ; ++ cc ] // packet jump
		if $showping            [ bit = (^ $bit 0x10) ; ++ cc ] // ping
	]
	if $showclientnum           [ bit = (^ $bit 0x20) ; ++ cc ] // client num

	if (|| $arg1 [!= (getalias .sb_bitmask) $bit] [!= (getalias .sb_modemask) $bit]) [
		.sb_bitmask  = $bit
		.sb_modemask = $getmode

		local .sb_namewidth .sb_cellwidth .sb_rowwidth
		local .scoreboard.cells .scoreboard.icons
		local .scoreboard.header .scoreboard.row
		local .scoreboard.shelf.top3 .scoreboard.shelf.rest

		.sb_namewidth = $uiPad:UXS
		.sb_cellwidth = (+f $uiPad:DSS $uiPad:M)
		.sb_rowwidth  = (+f $.sb_namewidth (*f $.sb_cellwidth $cc))

		loop n 6 [ if (& $bit (<< 1 $n)) [
			append .scoreboard.cells [[uifill @@[.sb_cellwidth] 0 [@@@(at [
				[uiFancyText "" (min   (getclientscore  $cn)      9999) 0.58]
				[uiFancyText "" (clamp (getclientfrags  $cn) -999 9999) 0.58]
				[uiFancyText "" (min   (getclientdeaths $cn)      9999) 0.58]
				[if (scoreboardpj   $cn) [uiFancyText "" (min (scoreboardpj   $cn) 9999) 0.58] [uiFancyText "" "--" 0.58 $c_gray]]
				[if (scoreboardping $cn) [uiFancyText "" (min (scoreboardping $cn) 9999) 0.58] [uiFancyText "" "--" 0.58 $c_gray]]
				[uiFancyText "" $cn 0.58 (at [@@@(concat $c_white $c_green $c_orange $c_magenta)] (getclientprivilege $cn))]
			] $n)]]]
			append .scoreboard.icons (at [
				@@@(at "0 star star flag star crown crown star" $getmode)
				"frag" "death" "pj" "ping" "cn"
			] $n)
		] ]

		// 1:TEAM
		.scoreboard.header = [
			local bflip
			bflip = (= $arg1 2) // right-to-left flip bit
			result [
				uifill @[.sb_rowwidth] $uiPad:DM [
					uispace 0 $uiPad:S [
						uiFastImg <reorient:@@@bflip> "hud/" "scorebg"
						uiclamp-e
					] ; uiclamp-e
					uispace $uiPad:M $uiPad:M- [
						uiFancyText "wide" @@@(
							at "FFA Aesir Vanir" $arg1
						) 1.4 @@@(INT:TRANS (
							at [0x40D060 0x4060D0 0xD04040] $arg1
						) % 80)
					] ; uialign- @@(? $bflip 1 -1) -1
					uispace $uiPad:O5 (*f $uiPad:O5 2.9) [
						uiFancyText "wide" (getteamscore @@@arg1) 1 @@@(
							at [0x40D060 0x4060D0 0xD04040] $arg1
						)
					] ; uialign- @@(? $bflip -1 1) -1
					if (numscoreboard @@arg1) [
						uihlist 0 [
							@@@@(? $bflip [
								uialign -1 1
								@(looplistconcatrev x $.scoreboard.icons [ result [
									uifill @@[.sb_cellwidth] @@(*f $uiPad:O4 7) [
										uiShadowedImg "" "hud/" @@x "" $uiPad:3XL
									]
								] ])
							] [
								uialign 1 1
								@(looplistconcat x $.scoreboard.icons [ result [
									uifill @@[.sb_cellwidth] @@(*f $uiPad:O4 7) [
										uiShadowedImg "" "hud/" @@x "" $uiPad:3XL
									]
								] ])
							])
						]
					]
				]
			]
		]

		// 1:TEAM  2:[CHILDREN]  3:SIDE-EXTRAS
		.scoreboard.row = [
			local bflip
			bflip = (= $arg1 2) // right-to-left flip bit
			result [
				uiFastImgStretched @(? (> $arg1 -1) [(
					intmul (at [
						[0x303030 0x282828] // ffa
						[0x1C2E70 0x122466] // aesir
						[0x701C1C 0x661212] // vanir
					] @arg1 (& $n 1))
				)] [ @(intmul 0x303840) ]) "hud/" "row" "" [
					uifill @@(? $arg3 $arg3 $.sb_rowwidth)
					@@(? $arg2 [ uigroup [ uiclamp.e ; @@arg2 ] ])
					if (scoreboardhighlight $cn) [
						uispace $uiPad:O3 $uiPad:O3 [
							uiShadowedImg <reorient:@@@@bflip> "hud/" "edge" "" $uiPad:M
						] ; uialign- @@@(? $bflip 1 -1) -1
					]
					@@(? $bflip [
						uihlist 0 [
							@@(looplistconcatrev x $.scoreboard.cells [ result [ @x; ] ])
						] ; uiclamp-y ; uialign- -1
						uispace $uiPad:M 0 [ uihlist $uiPad:O3 [
							if (isai $cn) [ uiShadowedImg "" "hud/" "bot" "" $uiPad:2XL ]
							uiFancyText "" (getclientname $cn) 0.6 (scoreboardstatus $cn)
							uiFastImg "" "countryflags/" (getclientcountrycode $cn) "" $uiPad:4XL $uiPad:3XL
						] ] ; uialign- 1
					] [
						uispace $uiPad:M 0 [ uihlist $uiPad:O3 [
							uiFastImg "" "countryflags/" (getclientcountrycode $cn) "" $uiPad:4XL $uiPad:3XL
							uiFancyText "" (getclientname $cn) 0.6 (scoreboardstatus $cn)
							if (isai $cn) [ uiShadowedImg "" "hud/" "bot" "" $uiPad:2XL ]
						] ] ; uialign- -1
						@(? (> $arg1 -1) [
							uihlist 0 [
								@@(looplistconcat x $.scoreboard.cells [ result [ @x; ] ])
							] ; uiclamp-y ; uialign- 1
						] [
							@(? $showclientnum [
								@(at $.scoreboard.cells -1)
								uiclamp-y ; uialign- 1
							])
						])
					])
				] $uiPad:5XL
				++ n
			]
		]

		if (m_teammode $getmode) [
			.scoreboard.aesir = [
				local n
				uivlist 0 [
					@@(.scoreboard.header 1)
					loopscoreboard cn 1 [ @@@(.scoreboard.row 1) ]
				] ; uialign- -2 -1
			]
			.scoreboard.vanir = [
				local n
				uivlist 0 [
					@@(.scoreboard.header 2)
					loopscoreboard cn 2 [ @@@(.scoreboard.row 2) ]
				] ; uialign- -2 -1
			]
			.scoreboard.ctf = [
				uigroup [
					uioffset 0 $uiPad:O5 [
						uioffset $uiPad:2XL- $uiPad:2XL- [ uiFancyText "wide" "V" 1 ]
						uioffset $uiPad:2XL  $uiPad:2XL  [ uiFancyText "wide" "S" 1 ]
					] ; uialign- 0 -1
					uihlist $uiPad:DM [
						.scoreboard.aesir
						.scoreboard.vanir
						uialign* -2 -1
					]
				]
			]
		]

		.scoreboard.spec = [
			uigrid 3 $uiPad:L 0 [
				loopscoreboard cn -1 [
					@@@(.scoreboard.row -1 [] (+f $.sb_namewidth (
						? $showclientnum $.sb_cellwidth 0
					)))
				]
			]
		]

		.scoreboard.shelf.top3 = [
			uioffset $uiPad:5XL- 0 [
				uiFastImg (fade 0.5) "hud/" "shelf" "" $uiPad:5XL
				push n (at [<mad:1/.8/0> <mad:.8/.8/.8> <mad:.8/.5/0>] $n) [
					uiFastImg $n "hud/" "glow"  "" $uiPad:5XL
					uiFastImg $n "hud/" "crown" "" $uiPad:3XL
				]
			] ; uialign- -1
		]
		.scoreboard.shelf.rest = [
			arg1 = (= $cn $getclientnum)
			uioffset $uiPad:5XL- 0 [
				uiFastImg (fade (? $arg1 0.5 0.25)) "hud/" "shelf" "" $uiPad:5XL
				uiFastImg <mad:.18/.18/.18> "hud/" "glow" "" $uiPad:5XL
				push n (+ $n 1) [
					uifontcolortext "def.ol" $n (|A! (? $arg1 0xB0 0x60)) 0.55
				]
			] ; uialign- -1
		]

		.scoreboard.ffa = [
			local n x
			uigroup [
				uivlist 0 [
					@@@(.scoreboard.header 0)
					loopscoreboard cn 0 [
						if (< $n 3) [
							if (= $cn $getclientnum) [ x = 1 ]
							@@@@@(.scoreboard.row 0 $.scoreboard.shelf.top3)
							if (= $n 3) [ uifill 0 $uiPad:M ]
						] [
							if (= $cn $getclientnum) [
								if (> $n (+ $.sb_offset 10)) [ uifill 0 $uiPad:M ]
								@@@@@@(.scoreboard.row 0 $.scoreboard.shelf.rest)
								if (< $n (+ $.sb_offset 0)) [ uifill 0 $uiPad:M ]
							] [
								if (&& [<= $n (+ $.sb_offset 10 $x $isspectator)] [>= $n $.sb_offset]) [
									@@@@@@@(.scoreboard.row 0 $.scoreboard.shelf.rest)
								] [ ++ n ]
							]
						]
					]
				]
				uioffset $uiPad:2XL 0 [
					uialign 1
					// XXX need a way to scan for flag carrier
				]
			]
		]
	]
]

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
//  KILL MESSAGES                                                                                           //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

newui ".killUI" [
	local t
	uiallowinput 0
	if (&& [! $isdead] [> $getmillis $.killUImillis]) [
        hideui ".killUI"
    ] [
		uivlist 0 [
			uifill 0 0.3
			t = (getrespawnwait)
            uispace 0.02 0.02 [
                uifonttext "wide.ol" $lasthudkillinfo $uiPad:USS
            ]
			uispace 0 0 [
				uioffset 0 $uiPad:D2XL- [
                    if (m_invasion $getmode) [
                        uifonttext "wide.ol" (+s "^f4Lives remaining: " $getclientlives) $uiPad:USS
                    ] [
                        uifontcolortext "wide.ol" (? $t $t " ") (? (< (getmillis) (+ 100 (getlastspawnattempt))) $c_red $c_white) 1
                    ]
				]
			]
		]
	]
] [
    .killUImillis  = (+ $getmillis 3500)
]
