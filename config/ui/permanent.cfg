
///////////////////////////////////////////////////////////////////////////////
//  PERMANENT UI -- DO *NOT* MAKE CHANGES HERE                               //
///////////////////////////////////////////////////////////////////////////////

newui "permanent" [
    uieschide 0
    uiclamp.e

    // Disables active input field if clicking Out-Of-Bounds
    if $uiInputOOB [
        uiallowinput 1
        uipress    [ uiCleanKB ]
        uiescpress [ uiCleanKB ]
        uitarget
    ] [ uiallowinput 0 ]

    nodebug [
        .AnimProgress
    ]

    if $mainmenu [
        uiallowinput (uivisible "main")
        uigroup [
            uitarget 0 0 [
                uipress [ changeui "credits" "main" ]
                uispace $uiPad:4XL $uiPad:4XL [
                    uivlist $uiPad:3XL- [
                        uiFastImg "" "badge_tesseract" "" "" $uiPad:D2XL
                        uifontoutline 1 0x7F [
                            uivlist $uiPad:M- [
                                uifontcolortext "wide" "TESSERACT" $c_cyan_t 0.6
                                uifontcolortext "wide" "ENGINE" $c_cyan_t 0.7
                            ]
                        ]
                    ]
                ]
            ] ; uialign- 1 1
            uispace $uiPad:M $uiPad:M [
                uitext $currentversionstring $uiPad:USS
            ] ; uialign- 1 -1
        ]
    ] [ nodebug [
        .CheckPermBits
    ] ] ; uiclamp*e
] [
    showui "tips"
    uiEffects = 0 // stores bits for fullscreen effects
]

// arg1: var
// arg2: duration
// arg3: condition
uiAnimate = [
    arg1 = [.@arg1.anim] ; if $arg3 [
        if (<f $$arg1 1) [
            $arg1 = (+f $$arg1 (/f $getframemillis $arg2))
        ] [ $arg1 = (min 1 $$arg1) ]
    ] [
        if (>f $$arg1 0.01) [
            $arg1 = (-f $$arg1 (/f $getframemillis $arg2))
        ] [ $arg1 = (max 0 $$arg1) ]
    ]
]

.AnimProgress = [
    local animProgress windowsCache
    windowsCache = $uiAnimWindows
    looplist2 ui curState $windowsCache [
       if $curState [
            animProgress = (- $getmillis $[.@ui.timestamp] $[.@ui.progress] -1)
        ] [ animProgress = (- (+ $[.@ui.timestamp] $[.@ui.duration]) $getmillis $[.@ui.progress]) ]
        if (<= 0 $animProgress $[.@ui.duration]) [
            [.@ui.opacity] = (%f $animProgress $[.@ui.duration])
        ] [
            listsplice= uiAnimWindows "" (indexof $uiAnimWindows $ui) 2
            [.@ui.opacity]  = $curState
            [.@ui.progress] = 0
            if $curState [] [ hideui $ui ]
        ]
    ]
]

.CheckPermBits = [
    local bits
        if (&& $uiBlurBg $uiBlurring)   [ ^= bits 1 ] // blur condition
        if (&& $uiVignette $uiBlurring) [ ^= bits 2 ] // vignette condition
        if (|| (isdead $getclientnum)   [ && $spectating [
            < (+ (getclienthealth $getfollow) 1) 0
        ] ])                            [ ^= bits 4 ] // damage condition

        uiAnimate effects 120 $bits

        if $bits [ uiEffects = $bits ] [
            if (=f $.effects.anim 0) [ uiEffects = 0 ]
        ]

        if (& $uiEffects 7) [
            uiFastImgTiled (fade $.effects.anim) "ui/" "ui_bg2"
        ]
        if (& $uiEffects 4) [
            uiFastImg (fade $.effects.anim) "hud/" "damage"
        ]
        if (& $uiEffects 2) [
            uiFastImg (fade $.effects.anim) "shadow"
        ]
]
