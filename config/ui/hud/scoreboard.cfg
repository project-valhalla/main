
newui "scoreboard" [
	if $mainmenu [ hideui "scoreboard" ]
	uiallowinput 0
	refreshscoreboard
	uicolor (? $sbopaque $c_menu 0xA0202020) 0 0 [
		uivlist 0 [
			_SCOREBOARD_sbheader
			uifill 0 $.UI_pad_ML
			if (m_teammode $getmode) _SCOREBOARD_playertableteam _SCOREBOARD_playertablesolo
			//uifill 0 0.01
			_SCOREBOARD_spectatorlist
		]
		uioutline $c_line1
		uiclamps-
	]
] [ if $mainmenu [ hideui "scoreboard" ] ]

///////////////////////////////////////////////////////////////////////////////

_SCOREBOARD_sbwsolo = 0.68
_SCOREBOARD_sbwteam = 1.18

_SCOREBOARD_sbheader = [
	local _SCOREBOARD_sbw
	_SCOREBOARD_sbw = (? (m_teammode $getmode) $_SCOREBOARD_sbwteam $_SCOREBOARD_sbwsolo)
	uifill 0 $.UI_pad_ML
	uifill $_SCOREBOARD_sbw 0.05 [
		uifill (-f $_SCOREBOARD_sbw (? $showip 0.18 0)) 0 [
			uialign -1
			uiclip (-f $_SCOREBOARD_sbw (? $showip 0.18 0)) 0 [
				uialign -1
				uispace $.UI_pad_L 0 [
					uivlist 0.002 [
						uihlist $.UI_pad_S [
							UIfastimg "cube" "" "" 0.024 0.024
							if $scoreboardservinfo [
								uitext $scoreboardservinfo 0.65
							] [ uitext "^f4Tesseract" 0.65 ]
						]
						uihlist $.UI_pad_XL [
							UIfastimg "ui/" "mm" $getmastermode $.UI_pad_3XL
							uitext (getmodeprettyname $getmode) 0.52
							uitext "^f4~" 0.5
							uitext (concatword "^f9" $scoreboardmap) 0.52
							if (m_timed $getmode) [
								uitext "^f4~" 0.5
								uitext (concatword (? (|| $intermission $paused) "^f3" "^f8") $scoreboardtime) 0.52
							]
						]
						uialign* -1
					]
				]
			]
		]
		if (&& $scoreboardmultiplayer $showip) [
			uifill $.UI_pad_D2XL 0 [
				uialign 1
				UIbar 0 1 ; uialign- -1
				uiclip $.UI_pad_D2XL 0 [
					uialign 1
					uispace $.UI_pad_L 0 [
						uivlist $.UI_pad_MM [
							uicolortext $connectedip   0xA0A0A0 0.52
							uicolortext $connectedport 0xA0A0A0 0.52
							uialign* 1
						]
					]
					uialign- 1
				]
			]
		]
	]
]

//-------------------------------------------------------------------------------------------------

_SCOREBOARD_sbcell = [
	if (arg1) [
		uifill $arg2 $arg3 [
			uicolortext $arg4 $arg5 $arg6
			uialign- $arg7 $arg8
		]
	]
]

_SCOREBOARD_cw_s  = 0.042
_SCOREBOARD_cw_k  = 0.054
_SCOREBOARD_cw_d  = 0.054
_SCOREBOARD_cw_pj = 0.050
_SCOREBOARD_cw_p  = 0.050
_SCOREBOARD_cw_cn = 0.040
_SCOREBOARD_cw_n  = [
	-f (
		if (> $getmode 2) [*f $_SCOREBOARD_sbwteam 0.5] $_SCOREBOARD_sbwsolo
	) (
		if (> $getmode 2) [? $showscore $_SCOREBOARD_cw_s]
	) (
		? $showkills $_SCOREBOARD_cw_k
	) (
		? $showdeaths $_SCOREBOARD_cw_d
	) (
		if $scoreboardmultiplayer [? $showpj $_SCOREBOARD_cw_pj]
	) (
		if $scoreboardmultiplayer [? $showping $_SCOREBOARD_cw_p]
	) (
		if $scoreboardmultiplayer [? $showclientnum $_SCOREBOARD_cw_cn]
	) 0.03
]

_SCOREBOARD_playertablesolo = [
	uifill 0 0 [
		uitable 0 0 [
			uitableheader [
				uifill 0.015
				uifill (_SCOREBOARD_cw_n) 0.022
				_SCOREBOARD_sbcell $showkills         $_SCOREBOARD_cw_k  0.022 "K"  0xBBCC8B 0.45 1 1
				_SCOREBOARD_sbcell $showdeaths        $_SCOREBOARD_cw_d  0.022 "D"  0xE56767 0.45 1 1
				if $scoreboardmultiplayer [
					_SCOREBOARD_sbcell $showpj        $_SCOREBOARD_cw_pj 0.022 "PJ" 0x77A1D9 0.45 1 1
					_SCOREBOARD_sbcell $showping      $_SCOREBOARD_cw_p  0.022 "P"  0x77A1D9 0.45 1 1
					_SCOREBOARD_sbcell $showclientnum $_SCOREBOARD_cw_cn 0.022 "#"  0xA0A0A0 0.45 1 1
				]
				uifill 0.015
			] [ uicolor 0x88161616 0 0 [ uiclamp.e ] ]
			_SCOREBOARD_sbtc = 0
			loopscoreboard cn 0 [
				_SCOREBOARD_sbtc = (! $_SCOREBOARD_sbtc)
				uitablerow [
					uifill 0.015
					_SCOREBOARD_sbcell 1                  (_SCOREBOARD_cw_n) 0.026 (getclientcolorname $cn)                   (scoreboardstatus $cn) 0.57 -1
					_SCOREBOARD_sbcell $showkills         $_SCOREBOARD_cw_k  0.026 (getclientfrags  $cn)                      0xBBCC8B               0.52  1
					_SCOREBOARD_sbcell $showdeaths        $_SCOREBOARD_cw_d  0.026 (getclientdeaths $cn)                      0xE56767               0.52  1
					if $scoreboardmultiplayer [
						_SCOREBOARD_sbcell $showpj        $_SCOREBOARD_cw_pj 0.026 (? (isai $cn) "^f4-" (scoreboardpj $cn))   0x77A1D9               0.52  1
						_SCOREBOARD_sbcell $showping      $_SCOREBOARD_cw_p  0.026 (? (isai $cn) "^f4-" (scoreboardping $cn)) 0x77A1D9               0.52  1
						_SCOREBOARD_sbcell $showclientnum $_SCOREBOARD_cw_cn 0.026 (? (isai $cn) "^f4-" $cn)                  0xA0A0A0               0.52  1
					]
					uifill 0.015
				] [
					uicolor (? $_SCOREBOARD_sbtc 0x99323232 0x99262626) 0 0 [ uiclamp.e ]
					pushif highlight (scoreboardhighlight $cn) [ uioutline 0xA0A0A0 0 0.024 [ uiclamp.x ] ]
				]
			]
		]
	]
]

_SCOREBOARD_playertableteam = [
	uifill 0 0 [
		uihlist 0 [
			uitable 0 0 [
				uialign 0 -1
				uitableheader [
					uifill 0.015
					if $scoreboardmultiplayer [
						_SCOREBOARD_sbcell $showclientnum $_SCOREBOARD_cw_cn 0.042 "#"              0xA0A0A0 0.45 -1 1
						_SCOREBOARD_sbcell $showping      $_SCOREBOARD_cw_p  0.042 "P"              0x77A1D9 0.45 -1 1
						_SCOREBOARD_sbcell $showpj        $_SCOREBOARD_cw_pj 0.042 "PJ"             0x77A1D9 0.45 -1 1
					]
					_SCOREBOARD_sbcell $showdeaths        $_SCOREBOARD_cw_d  0.042 "D"              0xE56767 0.45 -1 1
					_SCOREBOARD_sbcell $showkills         $_SCOREBOARD_cw_k  0.042 "K"              0xBBCC8B 0.45 -1 1
					_SCOREBOARD_sbcell $showscore         $_SCOREBOARD_cw_s  0.042 "Score"          0x62B370 0.45 -1 1
					_SCOREBOARD_sbcell 1                  (_SCOREBOARD_cw_n) 0.042 (getteamscore 1) 0x4060D0 1     1
					uifill 0.015
				] [ uicolor 0x99202860 0 0 [ uiclamp.e ] ]
				_SCOREBOARD_sbtc = 0
				loopscoreboard cn 1 [
					_SCOREBOARD_sbtc = (! $_SCOREBOARD_sbtc)
					uitablerow [
						uifill 0.015
						if $scoreboardmultiplayer [
							_SCOREBOARD_sbcell $showclientnum $_SCOREBOARD_cw_cn 0.026 (? (isai $cn) "^f4-" $cn)                  0xA0A0A0                0.52 -1
							_SCOREBOARD_sbcell $showping      $_SCOREBOARD_cw_p  0.026 (? (isai $cn) "^f4-" (scoreboardping $cn)) 0x77A1D9                0.52 -1
							_SCOREBOARD_sbcell $showpj        $_SCOREBOARD_cw_pj 0.026 (? (isai $cn) "^f4-" (scoreboardpj $cn))   0x77A1D9                0.52 -1
						]
						_SCOREBOARD_sbcell $showdeaths        $_SCOREBOARD_cw_d  0.026 (getclientdeaths $cn)                      0xE56767               0.52 -1
						_SCOREBOARD_sbcell $showkills         $_SCOREBOARD_cw_k  0.026 (getclientfrags  $cn)                      0xBBCC8B               0.52 -1
						_SCOREBOARD_sbcell $showscore         $_SCOREBOARD_cw_s  0.026 (getclientflags  $cn)                      0x62B370               0.52 -1
						_SCOREBOARD_sbcell 1                  (_SCOREBOARD_cw_n) 0.026 (getclientcolorname $cn)                   (scoreboardstatus $cn) 0.57  1
						uifill 0.015
					] [
						uicolor (? $_SCOREBOARD_sbtc 0x99333b40 0x99262b33) 0 0 [ uiclamp.e ]
						pushif highlight (scoreboardhighlight $cn) [ uioutline 0xA0A0A0 0 0.024 [ uiclamp.x ] ]
					]
				]
			]
			uitable 0 0 [
				uialign 0 -1
				uitableheader [
					uifill 0.015
					_SCOREBOARD_sbcell 1                  (_SCOREBOARD_cw_n) 0.042 (getteamscore 2) 0xD04040 1    -1
					_SCOREBOARD_sbcell $showscore         $_SCOREBOARD_cw_s  0.042 "Score"          0x62B370 0.45  1 1
					_SCOREBOARD_sbcell $showkills         $_SCOREBOARD_cw_k  0.042 "K"              0xBBCC8B 0.45  1 1
					_SCOREBOARD_sbcell $showdeaths        $_SCOREBOARD_cw_d  0.042 "D"              0xE56767 0.45  1 1
					if $scoreboardmultiplayer [
						_SCOREBOARD_sbcell $showpj        $_SCOREBOARD_cw_pj 0.042 "PJ"             0x77A1D9 0.45  1 1
						_SCOREBOARD_sbcell $showping      $_SCOREBOARD_cw_p  0.042 "P"              0x77A1D9 0.45  1 1
						_SCOREBOARD_sbcell $showclientnum $_SCOREBOARD_cw_cn 0.042 "#"              0xA0A0A0 0.45  1 1
					]
					uifill 0.015
				] [ uicolor 0x99602020 0 0 [ uiclamp.e ] ]
				_SCOREBOARD_sbtc = 0
				loopscoreboard cn 2 [
					_SCOREBOARD_sbtc = (! $_SCOREBOARD_sbtc)
					uitablerow [
						uifill 0.015
						_SCOREBOARD_sbcell 1                  (_SCOREBOARD_cw_n) 0.026 (getclientcolorname $cn)                   (scoreboardstatus $cn) 0.57 -1
						_SCOREBOARD_sbcell $showscore         $_SCOREBOARD_cw_s  0.026 (getclientflags  $cn)                      0x62B370               0.52  1
						_SCOREBOARD_sbcell $showkills         $_SCOREBOARD_cw_k  0.026 (getclientfrags  $cn)                      0xBBCC8B               0.52  1
						_SCOREBOARD_sbcell $showdeaths        $_SCOREBOARD_cw_d  0.026 (getclientdeaths $cn)                      0xE56767               0.52  1
						if $scoreboardmultiplayer [
							_SCOREBOARD_sbcell $showpj        $_SCOREBOARD_cw_pj 0.026 (? (isai $cn) "^f4-" (scoreboardpj $cn))   0x77A1D9               0.52  1
							_SCOREBOARD_sbcell $showping      $_SCOREBOARD_cw_p  0.026 (? (isai $cn) "^f4-" (scoreboardping $cn)) 0x77A1D9               0.52  1
							_SCOREBOARD_sbcell $showclientnum $_SCOREBOARD_cw_cn 0.026 (? (isai $cn) "^f4-" $cn)                  0xA0A0A0               0.52  1
						]
						uifill 0.015
					] [
						uicolor (? $_SCOREBOARD_sbtc 0x99403333 0x99332626) 0 0 [ uiclamp.e ]
						pushif highlight (scoreboardhighlight $cn) [ uioutline 0xA0A0A0 0 0.024 [ uiclamp.x ] ]
					]
				]
			]
		]
	]
]

//-------------------------------------------------------------------------------------------------

_SCOREBOARD_spectatorlist = [
	if $showspectators [
		uimodcolor $c_boxmod 0 0 [
			uiclamp.e
			uigrid (? (> $getmode 2) 3 2) 0 0 [ // <---------- ????? Wrapping uihlist ?????
				loopscoreboard cn -1 [
					uispace 0.01 0.01 [
						pushif sbhigh (scoreboardhighlight $cn) [
							uioutline 0xA0A0A0 ; uiclamp-e
						]
						uihlist 0.01 [
							uicolortext (getclientcolorname $cn) (scoreboardstatus $cn) 0.57
							if $scoreboardmultiplayer [ if $showclientnum [ uitext $cn 0.57 ] ]
						]
					]
				]
			]
		]
	]
]
