
UImenu "cci" [
	local numspec numplay numazul numrojo
	if $mainmenu [ hideui "cci" ] [
		cp_updateselected (listclients 1)
	]
	//looplist cn (listclients 1) [
	//    if (isspectator $cn) [
	//        numspec = (+ $numspec 1)
	//    ] [
	//        numplay = (+ $numplay 1)
	//        case (getclientteam $cn) 1 [
	//            numazul = (+ $numazul 1)
	//        ] 2 [ numrojo = (+ $numrojo 1) ]
	//    ]
	//]

	uivlist $.UI_pad_L [
		uihlist $.UI_pad_L [
			UIbutton "hold2" [
				UIcheckbox $.cci_showspec
				uitext "Show Spec" 0.6
			] $.UI_pad_D2XL 0 [
				.cci_showspec (! $.cci_showspec)
				.cci_clients (listfilter i $.cci_clients [! (isspectator $i)])
			] -1
			UIbutton "hold2" [
				UIcheckbox $.cci_showbots
				uitext "Show Bots" 0.6
			] $.UI_pad_D2XL 0 [
				.cci_showbots (! $.cci_showbots)
				.cci_clients (listfilter i $.cci_clients [! (isai $i)])
			] -1
			UIbutton "hold2" [
				UIcheckbox $.cci_showself
				uitext "Show Self" 0.6
			] $.UI_pad_D2XL 0 [
				.cci_showself (! $.cci_showself)
				.cci_clients (listdel $.cci_clients $getclientnum)
			] -1
		]
		UItooltipbox $.UI_pad_UM (*f $.UI_pad_5XL 11) [
			uihlist $.UI_pad_L [
				uiclamp.y
				uialign -2 -1
				uivlist 0 [
					if (m_teammode $getmode) [
						cp_clientpanel 1 0
						cp_clientpanel 1 1
					] [ cp_clientpanel 0 0 ]
				]
				uivlist 0 [
					if (m_teammode $getmode) [
						cp_clientpanel 2 0
						cp_clientpanel 2 1
					] [ cp_clientpanel 0 1 ]
				]
				uialign* -2 -1
			]
		] (*f $.UI_pad_D5XL 2) [
			uitext $.cci_clients 0.5
		]
		uihlist $.UI_pad_L [
			uialign -1 -2
			// XXX incomplete buttons, placeholders
			UIbutton "" [
				uitext "Kick" 0.6
			] $.UI_pad_3XL $.UI_pad_6XL [
				.cci_clients (listfilter i $.cci_clients [&& [! (isai $i)] [!= $i $getclientnum]])
				selected_kick = $.cci_clients
				looplist cn $.cci_clients [ kick $cn ]
			] 0 [ UIsettip "Kick players (!bot & !self)" ]
			UIbutton "" [
				uitext "Spectate" 0.6
			] $.UI_pad_3XL $.UI_pad_6XL [
				.cci_clients (listfilter i $.cci_clients [! (isai $i)])
				looplist cn $.cci_clients [ spectator 1 $cn ]
			] 0 [ UIsettip "Put players into spectator mode (!bot)" ]
			UIbutton "" [
				uitext "Unspectate" 0.6
			] $.UI_pad_3XL $.UI_pad_6XL [
				.cci_clients (listfilter i $.cci_clients [! (isai $i)])
				looplist cn $.cci_clients [ spectator 0 $cn ]
			] 0 [ UIsettip "Put players out of spectator mode (!bot)" ]
			UIbutton "" [
				uitext "Swap Team" 0.6
			] $.UI_pad_3XL $.UI_pad_6XL [
				looplist cn $.cci_clients [ case (getclientteam $cn) 1 [ setteam $cn 2 ] 2 [ setteam $cn 1 ] ]
			] 0 [ UIsettip "Swap teams for .cci_clients players" ]
		]
	]
] [] [] [] [ uitext "Client Control Interface" 0.8 ]

////////////////////////////////////////////////////////////////////////////////////////////////////


cp_updateselected = [ .cci_clients (listintersect $.cci_clients $arg1) ]

cp_client = [
	local selected ; selected = (>= (indexof $.cci_clients $arg1) 0)
	UIbuttonarea (
			? $selected "hold1" "hold2"
		) (-f $.UI_pad_D6XL $.UI_pad_L) $.UI_pad_5XL [
		uirelease [
			if $selected [
				.cci_clients (listdel $.cci_clients $arg1)
			] [ .cci_clients (concat $.cci_clients $arg1) ]
		]
		uispace $.UI_pad_S 0 [
			uiclamp.x
			uihlist 0 [
				uiclamp.x
				uicolortext (getclientcolorname $arg1) (scoreboardstatus $arg1) 0.55
				if $showclientnum [
					if (isai $arg1) [
						uitext (concatword (? $selected "^f8" "^f4") "BOT") 0.55
					] [ uitext (concatword (? $selected "^f8" "^f7") $arg1) 0.6 ]
					uialign- 1
				]
			]
		]
	]

]

cp_clientpanel = [
	uifill (-f $.UI_pad_D6XL $.UI_pad_L)
	looplist cn (
		listfilter i (listclients $.cci_showself) [
			&& [
				if $arg2 [isspectator $i] [! (isspectator $i)]
			] [
				case $arg1 1 [= (getclientteam $i) 1] 2 [= (getclientteam $i) 2] () 1
			] [
				if $.cci_showbots [result 1] [! (isai $i)]
			]
		]
	) [ cp_client $cn $arg1 $arg2 ]
]
